///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2017-2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////
// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	Если ЭтоWindowsСервер Тогда
		Элементы.ПутьКВерсиямПлатформыНаСервере.СписокВыбора.Добавить("C:\Program files\1cv8\%ВерсияПлатформы%\bin");
		Элементы.ПутьКВерсиямПлатформыНаСервере.СписокВыбора.Добавить(
			"C:\Program files (x86)\1cv8\%ВерсияПлатформы%\bin");
	Иначе
		// Это Linux
		Элементы.ПутьКВерсиямПлатформыНаСервере.СписокВыбора.Добавить("/opt/1cv8/x86_64/%ВерсияПлатформы%/");
		Элементы.ПутьКВерсиямПлатформыНаСервере.СписокВыбора.Добавить("/opt/1C/v8.3/x86_64/");
	КонецЕсли;

	ПутьКВерсиямПлатформыНаСервереПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьДоступность(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьОчередиВыполненияПриИзменении(Элемент)

	УстановитьДоступность(ЭтотОбъект);
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПутьКВерсиямПлатформыНаСервереПриИзменении(Элемент)

	ПутьКВерсиямПлатформыНаСервереПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОчередиИсполненияНажатие(Элемент)

	Если Модифицированность Тогда

		Если Не Записать() Тогда

			Возврат;

		КонецЕсли;
		
		ОбновитьИнтерфейс();

	КонецЕсли;
	
	ОткрытьФорму("Справочник.ОчередиВыполнения.ФормаСписка");

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(Форма)

	Форма.Элементы.ОткрытьОчередиИсполнения.Доступность = Форма.НаборКонстант.ИспользоватьОчередиВыполнения;

КонецПроцедуры
&НаСервере
Процедура ПутьКВерсиямПлатформыНаСервереПриИзмененииНаСервере()

	Элементы.ДоступныеПлатформы.Заголовок = СтрШаблон(НСтр("ru = 'Доступные версии: %1'"), СтрСоединить(
		ОбщегоНазначения.СписокДоступныхВерсийПлатформы(НаборКонстант.ПутьКВерсиямПлатформыНаСервере), ", "));
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВерсииEDT(Команда)

	НеобходимоЗаполнитьВерсиюEDT = НеобходимоЗаполнитьВерсиюEDT();
	Если НеобходимоЗаполнитьВерсиюEDT И НЕ НаборКонстант.ИспользоватьНесколькоВерсийEDT  Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВключитьИспользованиеВерсийEDT", ЭтотОбъект);
		ПоказатьВопрос(
				ОписаниеОповещения, НСтр("ru = 'Установлено несколько версий EDT, для корретной работоты утилиты Ring необходимо указать номер версии. 
										 |Включить возможность определения номера версии?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат;
	ИначеЕсли НеобходимоЗаполнитьВерсиюEDT И НаборКонстант.ИспользоватьНесколькоВерсийEDT Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'На сервере устновлено несколько версий EDT. Опция должна остаться включенной.'"));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'На сервере устновлена одна версия EDT. Опция может остаться выключенной.'"));
	КонецЕсли;

КонецПроцедуры


&НаСервере
Функция НеобходимоЗаполнитьВерсиюEDT()
	
	Возврат КонвертацияХранилища.НеобходимоЗаполнитьВерсиюEDT();
	
КонецФункции

// Параметры:
// 	КодВозварта - КодВозвратаДиалога
// 	ДополнительныеПараметры - Неопределено
&НаКлиенте
Процедура ВключитьИспользованиеВерсийEDT(КодВозварта, ДополнительныеПараметры) Экспорт 
	
	Если КодВозварта = КодВозвратаДиалога.Да Тогда
		НаборКонстант.ИспользоватьНесколькоВерсийEDT = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти