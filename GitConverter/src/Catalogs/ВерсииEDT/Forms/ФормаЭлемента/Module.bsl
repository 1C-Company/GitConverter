


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКCLIНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачатьВыборРасположения1СEDTCLI();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьВыборРасположения1СEDTCLI()
	ЭтоWindowsСервер = ОбщегоНазначенияВызовСервера.ЭтоWindowsСервер();
	
	Если ЭтоWindowsСервер Тогда
		ИмяИсполняемогоФайла = "1cedtcli.exe";
	Иначе
		ИмяИсполняемогоФайла = "1cedtcli";
	КонецЕсли;
	
	ДиалогВыбораФайлов = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайлов.Фильтр = СтрШаблон("Исполняемый файл (%1)|%1", ИмяИсполняемогоФайла);
	ДиалогВыбораФайлов.МножественныйВыбор = Ложь;
	ДиалогВыбораФайлов.Заголовок = "Путь к CLI (Аналогичный путь должен быть на сервере!)";
	ДиалогВыбораФайлов.Показать(Новый ОписаниеОповещения("ОбработкаВыбораФайлаНаСервере", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайлаНаСервере(МассивФайлов, ДополнительныеПараметры) Экспорт
	Если МассивФайлов <> Неопределено Тогда
		ФайлСуществует = ФайлСуществуетНаСервере(МассивФайлов[0]);
		Если Не ФайлСуществует Тогда
			ПоказатьПредупреждение(, "Такого файла не существует на сервере!");
			Возврат;
		КонецЕсли;
		Объект.ПутьКCLI = МассивФайлов[0];
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФайлСуществуетНаСервере(ПутьКФайлу)
	Файл = Новый Файл(ПутьКФайлу);
	
	Возврат Файл.Существует();
КонецФункции

	
	

#КонецОбласти