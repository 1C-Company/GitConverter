

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриЧтенииСозданииНаСервере();
	
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	КаталогВыгрузкиВерсий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Владелец, "КаталогВыгрузкиВерсий");
	ИмяФайлаЛога = КонвертацияХранилища.ИмяФайлаЛогаКонвертацииХранилища(КаталогВыгрузкиВерсий);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ВыгружатьВОтдельныйКаталог Тогда
	
		ТекущийОбъект.КаталогВыгрузкиВерсий = "";
	
	КонецЕсли; 
	 
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Расписание",    Расписание);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Использование", РегламентноеЗаданиеИспользуется);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегламентноеЗаданиеИспользуетсяПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеСтрокойНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДиалогРасписания = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	ДиалогРасписания.Показать(Новый ОписаниеОповещения("РасписаниеСтрокойНажатиеЗавершение", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура РасписаниеСтрокойНажатиеЗавершение(РасписаниеОтвет, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеОтвет <> Неопределено Тогда
		
		Модифицированность = Истина;
		Расписание         = РасписаниеОтвет;
		РасписаниеСтрокой  = Строка(Расписание);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЛогаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ИмяФайлаЛога) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = Новый ТекстовыйДокумент();
	
	ИмяФайла = "";
	ПрочитатьТекстовыйФайлНаСервере(ИмяФайлаЛога, Текст, ИмяФайла);
	
	Текст.Показать(ИмяФайла, ИмяФайла);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Задание = РегламентныеЗаданияСервер.Задание(Объект.РегламентноеЗадание);
	Если Задание <> Неопределено Тогда
		Расписание = Задание.Расписание;
		РегламентноеЗаданиеИспользуется = Задание.Использование;
		РасписаниеСтрокой = Строка(Расписание);
	Иначе
		Расписание = Новый РасписаниеРегламентногоЗадания;
		РасписаниеСтрокой = РасписаниеСтрокой = Строка(Расписание);
	КонецЕсли;
	
	ВыгружатьВОтдельныйКаталог = ЗначениеЗаполнено(Объект.КаталогВыгрузкиВерсий);
	
	ДругойПользователь = ЗначениеЗаполнено(Объект.ИмяПользователяХранилища);
	
	УстановитьВидимость();
	
	УстановитьДоступность(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Возврат;
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(Форма)
	
	Если Форма.РегламентноеЗаданиеИспользуется Тогда
	
		Форма.Элементы.РасписаниеСтрокой.Доступность = Истина;
	
	Иначе
	
		Форма.Элементы.РасписаниеСтрокой.Доступность = Ложь;
	
	КонецЕсли;
	
	Форма.Элементы.КаталогВыгрузкиВерсий.Доступность = Форма.ВыгружатьВОтдельныйКаталог;
	Форма.Элементы.ИмяПользователяХранилища.Доступность = Форма.ДругойПользователь;
	Форма.Элементы.ПарольПользователяХранилища.Доступность = Форма.ДругойПользователь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьВОтдельныйКаталогПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДругойПользовательПриИзменении(Элемент)
	
	УстановитьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПрочитатьТекстовыйФайлНаСервере(ПутьКФайлу, Текст, ИмяФайла, КодировкаСистемы = Ложь)
	
	ОбщегоНазначения.ПрочитатьТекстовыйФайл(ПутьКФайлу, Текст, ИмяФайла, КодировкаСистемы);
	
КонецПроцедуры

#КонецОбласти