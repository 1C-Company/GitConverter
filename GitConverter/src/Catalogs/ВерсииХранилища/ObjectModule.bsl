///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2017-2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка ИЛИ Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееСостояние = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Состояние");
	
	Если Состояние <> ПредыдущееСостояние Тогда
		
		ДополнительныеСвойства.Вставить("ЗаписыватьНовоеСостояние", Истина);
		
	КонецЕсли;
	
	Если Состояние <> Перечисления.СостоянияВерсии.ПолучениеВерсии 
		И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОчередиВыполнения")
		ИЛИ НЕ ЗначениеЗаполнено(Источник) Тогда
		Источник = Неопределено;
	КонецЕсли; 
	Если Состояние <> Перечисления.СостоянияВерсии.ВерсияПомещена И ЗначениеЗаполнено(Хеш) Тогда
		Хеш = "";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Состояние) 
		ИЛИ Состояние = Перечисления.СостоянияВерсии.ПолучениеВерсии
		ИЛИ Состояние = Перечисления.СостоянияВерсии.ВерсияПолучена Тогда
		ВыгрузкаИзменений = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка ИЛИ Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЗаписыватьНовоеСостояние") Тогда
		
		ТекущаяДатаСеанса = ТекущаяДатаСеанса();
		
		НаборЗаписей = РегистрыСведений.СостоянияВерсии.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ВерсияХранилища.Установить(Ссылка);
		НаборЗаписей.Отбор.Период.Установить(ТекущаяДатаСеанса);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Период = ТекущаяДатаСеанса;
		Запись.ВерсияХранилища = Ссылка;
		Запись.Состояние = Состояние;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	Если Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена 
		И ЗначениеЗаполнено(КаталогВременныхФайлов) Тогда
		
		КонвертацияХранилища.УдалитьФайлыВерсииВФоне(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры




#КонецОбласти

#КонецЕсли