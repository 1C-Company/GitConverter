#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Отбор = Новый Структура();
	Если ЗначениеЗаполнено(ТекущийОбъект.Источник) Тогда
		Отбор.Вставить("Ключ", Строка(ТекущийОбъект.Источник.УникальныйИдентификатор()));
	Иначе
		Отбор.Вставить("Ключ", Строка(ТекущийОбъект.Ссылка.УникальныйИдентификатор()));
	КонецЕсли; 
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если МассивФоновыхЗаданий.Количество() > 0 Тогда
		Задание = МассивФоновыхЗаданий[0];
		СостояниеЗадания = НСтр("ru='Выполняется обработка с %Дата%'");
		СостояниеЗадания = СтрЗаменить(СостояниеЗадания, "%Дата%", Задание.Начало);
		
		Элементы.ГруппаСостояние.Видимость = Истина;
		ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(Задание.УникальныйИдентификатор);
		
	Иначе
		СостояниеЗадания = "";
		Элементы.ГруппаСостояние.Видимость = Ложь;
	КонецЕсли;
	
	Если ТекущийОбъект.Состояние = Перечисления.СостоянияВерсии.НачалоКоммита 
		ИЛИ ТекущийОбъект.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена Тогда
		КаталогВыгрузкиВерсий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъект.Владелец, "КаталогВыгрузкиВерсий");
		ИмяФайлаЛога = КонвертацияХранилища.ИмяФайлаЛогаКоммитаВерсии(ТекущийОбъект.Код, КаталогВыгрузкиВерсий);
	Иначе
		ИмяФайлаЛога = КонвертацияХранилища.ИмяФайлаЛогаОбработкиВерсии(ТекущийОбъект.КаталогВременныхФайлов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапуститьОбработкуВерсии(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьОбработкуВерсииНаСервере(Объект.Ссылка);
	
	Прочитать();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояние(Команда)
	
	Прочитать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяФайлаЛогаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ИмяФайлаЛога) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = Новый ТекстовыйДокумент();
	
	ИмяФайла = "";
	ПрочитатьТекстовыйФайлНаСервере(ИмяФайлаЛога, Текст, ИмяФайла);
	
	Текст.Показать(ИмяФайла, ИмяФайла);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗапуститьОбработкуВерсииНаСервере(ВерсияХранилища)
	
	КонвертацияХранилища.ЗапуститьОбработкуВерсииВФоне(ВерсияХранилища);
	
КонецПроцедуры

&НаСервере
Процедура ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(ФоновоеЗаданиеИдентификатор)
	Прогресс = ДлительныеОперации.ПрочитатьПрогресс(ФоновоеЗаданиеИдентификатор);
	Если Прогресс <> Неопределено Тогда
		ФоновоеЗаданиеПроцент   = Прогресс.Процент;
		ФоновоеЗаданиеСостояние = Прогресс.Текст;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПрочитатьТекстовыйФайлНаСервере(ПутьКФайлу, Текст, ИмяФайла, КодировкаСистемы = Ложь)
	
	ОбщегоНазначения.ПрочитатьТекстовыйФайл(ПутьКФайлу, Текст, ИмяФайла, КодировкаСистемы);
	
КонецПроцедуры

#КонецОбласти
