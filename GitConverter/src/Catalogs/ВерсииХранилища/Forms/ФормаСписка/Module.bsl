///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2017-2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
	
		Элементы.Владелец.Видимость = Ложь;
		
	КонецЕсли; 
	
	Состав.Параметры.УстановитьЗначениеПараметра("Ссылки", Справочники.ВерсииХранилища.ПустаяСсылка());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Состав.Параметры.УстановитьЗначениеПараметра("Ссылки", Элементы.Список.ВыделенныеСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСостояниеВерсияПолучена(Команда)
	
	УстановитьСостояниеНаСервере(Элементы.Список.ВыделенныеСтроки, "ВерсияПолучена");
	ОповеститьОбИзмененииВыделенныеСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеВерсияВыгружена(Команда)
	
	УстановитьСостояниеНаСервере(Элементы.Список.ВыделенныеСтроки, "ВерсияВыгружена");
	ОповеститьОбИзмененииВыделенныеСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеМетаданныеЗагружены(Команда)
	
	УстановитьСостояниеНаСервере(Элементы.Список.ВыделенныеСтроки, "МетаданныеЗагружены");
	ОповеститьОбИзмененииВыделенныеСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеВерсияПомещена(Команда)
	
	ТекстВопроса = НСтр("ru = 'Изменения по версиям не будут отражены в репозитории.
						|Вы уверены, что необходимо установить состояние ""Версия помещена""?'");
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("УстановитьСостояниеВерсияПомещенаЗавершение", ЭтотОбъект, Неопределено);
	
	Кнопки = Новый СписокЗначений();
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Уверен, установить ""Версия помещена""'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки);
	
КонецПроцедуры


&НаКлиенте
Процедура СброситьСостояние(Команда)
	
	УстановитьСостояниеНаСервере(Элементы.Список.ВыделенныеСтроки, "ПустаяСсылка");
	ОповеститьОбИзмененииВыделенныеСтроки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьСостояниеНаСервере(Знач ВыделенныеСтроки, Знач Состояние)
	
	Если НЕ ЗначениеЗаполнено(Состояние) ИЛИ ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Состояние = "ПустаяСсылка" Тогда
	
		СостояниеСсылка = Перечисления.СостоянияВерсии.ПустаяСсылка();
	
	Иначе
	
		СостояниеСсылка = Перечисления.СостоянияВерсии[Состояние];
	
	КонецЕсли; 
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
		
	Для Каждого Ссылка Из ВыделенныеСтроки Цикл
		ВерсияОбъект = Ссылка.ПолучитьОбъект();
		УдалятьФайлы  = ПроверитьНеобходимостьУдалятьФайлыВерсии(ВерсияОбъект, СостояниеСсылка);
		
		ВерсияОбъект.Состояние = СостояниеСсылка;
		Если Не ЗначениеЗаполнено(СостояниеСсылка) 
			ИЛИ СостояниеСсылка = Перечисления.СостоянияВерсии.ВерсияПолучена Тогда
			ВерсияОбъект.ВыгрузкаИзменений = Ложь;
		КонецЕсли;
		ВерсияОбъект.Источник = Неопределено;
		ВерсияОбъект.Записать();
		
		// Зачистка файлов команд, если уже были сформированы
		Если УдалятьФайлы Тогда
			КаталогВыгрузкиВерсий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВерсияОбъект.Владелец, "КаталогВыгрузкиВерсий");
			КаталогВыгрузкиВерсий = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиВерсий);
			
			ИмяФайлаКомандыGit = "git_command_" + Формат(ВерсияОбъект.Код, "ЧДЦ=; ЧГ=0") + ?(ЭтоWindowsСервер, ".bat", ".sh");
			ИмяФайлКомментария = "git_comment_" + Формат(ВерсияОбъект.Код, "ЧДЦ=; ЧГ=0") + ".txt";
			Файл = Новый Файл(ИмяФайлаКомандыGit);
			Если Файл.Существует() Тогда
				УдалитьФайлы(ИмяФайлаКомандыGit);
			КонецЕсли;
			Файл = Новый Файл(ИмяФайлКомментария);
			Если Файл.Существует() Тогда
				УдалитьФайлы(ИмяФайлКомментария);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииВыделенныеСтроки()

	Для Каждого Ссылка Из Элементы.Список.ВыделенныеСтроки Цикл
		ОповеститьОбИзменении(Ссылка);
	КонецЦикла;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьНеобходимостьУдалятьФайлыВерсии(ВерсияОбъект, НовоеСостояние)
	
	Если НовоеСостояние = Перечисления.СостоянияВерсии.ПустаяСсылка() Тогда
		Возврат Истина;
	ИначеЕсли (ВерсияОбъект.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены 
		ИЛИ ВерсияОбъект.Состояние = Перечисления.СостоянияВерсии.НачалоКоммита
		ИЛИ ВерсияОбъект.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена)
		И НовоеСостояние <> Перечисления.СостоянияВерсии.МетаданныеЗагружены Тогда 
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСостояниеВерсияПомещенаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСостояниеНаСервере(Элементы.Список.ВыделенныеСтроки, "ВерсияПомещена");
	ОповеститьОбИзмененииВыделенныеСтроки();
	
КонецПроцедуры

#КонецОбласти

