///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2017-2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////
// @strict-types

////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции общего назначения для "Конвертации хранилища в Git"
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Задание запускающее конвертацию
//
// Параметры:
//  Хранилище - СправочникСсылка.ХранилищаКонфигураций, Неопределено - Ссылка на хранилище.
Процедура ВыполнитьКонвертацию(Знач Хранилище = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Хранилище) Тогда
		Возврат;
	КонецЕсли;

	ПроверитьПолученныеВерсии(Хранилище);

	ПроверитьСостоянияЗаданий(Хранилище);
	
	ПроверитьУдалениеВременныхДанныхВерсийПослеКоммита(Хранилище);

	СформироватьФайлыGitНаСервере(Хранилище);

	Параметры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, Новый Структура("Адрес, ВерсияПлатформы, КаталогВыгрузкиВерсий, 
		| ПерваяВерсия, ПоследняяВерсия, МаксимальноеКоличествоПодготавливаемыхВерсий, ОтключитьЛогирование, КонвертироватьВФорматEDT"));
	Параметры.Вставить("Хранилище", Хранилище);
	
	Если НЕ Параметры.КонвертироватьВФорматEDT Тогда
		ВызватьИсключение НСтр("ru = 'Конвертация в формат xml 1С:Предприятия не поддерживается.'");
	КонецЕсли;

	Параметры.КаталогВыгрузкиВерсий = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВыгрузкиВерсий);

	// Если установлено ограничение последней версии - то не проверяем новые версии
	Если Параметры.ПоследняяВерсия = 0 Тогда

	    // Создаем временную базу, загружаем отчет, проверяем необходимость запуска
		КлючОперации = Строка(Хранилище.УникальныйИдентификатор()) + "_"
			+ Строка(Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"))
			+ "_1";
		Параметры.Вставить("Ключ", КлючОперации);
		Параметры.Вставить("КаталогВременныхФайлов", Параметры.КаталогВыгрузкиВерсий);
		Параметры.Вставить("ФайлПараметровПакетнойОперации", Параметры.КаталогВременныхФайлов
			+ "params_" + Параметры.Ключ + ".txt");
		Если НЕ Параметры.ОтключитьЛогирование Тогда
			Параметры.Вставить("ИмяФайлаЛога", ИмяФайлаЛогаКонвертацииХранилища(Параметры.КаталогВременныхФайлов));
		КонецЕсли;
		Параметры.Вставить("ИмяФайлаРезультатов", Параметры.КаталогВременныхФайлов
			+ "result.txt");
		Параметры.Вставить("КаталогИБ", Параметры.КаталогВременныхФайлов
			+ "report_db");
		Параметры.КаталогИБ = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогИБ);
		Параметры.Вставить("СоздатьФайловуюИнформационнуюБазу", Истина);

		ПараметрыПодключенияКХранилищу(Хранилище, Параметры);

		СоздатьФайловуюИнформационнуюБазу(Параметры);

		СтрокаСоединения = " /F ""%Путь%""";
		СтрокаСоединения = СтрЗаменить(СтрокаСоединения, "%Путь%", Параметры.КаталогИБ);
		Параметры.Вставить("СтрокаСоединенияИБ", СтрокаСоединения);
		Параметры.Вставить("ИмяПользователяИБ", "");
		Параметры.Вставить("ПарольПользователяИБ", "");

		ПараметрыРасширения(Хранилище, Параметры);
		ДобавитьРасширениеКонфигурации(Параметры);
		
		ПоследняяВерсия = Справочники.ВерсииХранилища.ПоследняяВерсияХранилища(Хранилище);

		Параметры.Вставить("СформироватьОтчетПоВерсиямХранилища", Истина);
		Параметры.Вставить("ИмяФайлаОтчета", Параметры.КаталогИБ + "Report.mxl");
		Параметры.Вставить("ВерсияНачала", ПоследняяВерсия.Версия + 1);

		СформироватьОтчетПоВерсиямХранилища(Параметры);

		Справочники.ВерсииХранилища.ЗагрузитьВерсииИзОтчета(Хранилище, Параметры.ИмяФайлаОтчета);

		УдалитьФайлы(Параметры.КаталогИБ);

	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВерсииХранилища.Ссылка), 0) КАК Количество
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Владелец
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ПустаяСсылка)
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.НачалоКоммита)
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВерсияПомещена)
		|	И &КоличествоОграничено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	ВерсииХранилища.Ссылка,
		|	ВерсииХранилища.Код КАК Код
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Владелец
		|	И ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ПустаяСсылка)
		|	И ВерсииХранилища.Код >= &ПерваяВерсия
		|	И ВЫБОР
		|			КОГДА &ПоследняяВерсия = 0
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ВерсииХранилища.Код <= &ПоследняяВерсия
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Код";

	Запрос.УстановитьПараметр("КоличествоОграничено", Параметры.МаксимальноеКоличествоПодготавливаемыхВерсий > 0);
	Запрос.УстановитьПараметр("Владелец", Хранилище);
	Запрос.УстановитьПараметр("ПерваяВерсия", Параметры.ПерваяВерсия);
	Запрос.УстановитьПараметр("ПоследняяВерсия", Параметры.ПоследняяВерсия);

	РезультатыЗапроса = Запрос.ВыполнитьПакет();

	Выборка = РезультатыЗапроса[0].Выбрать(); // ВыборкаИзРезультатаЗапроса - 

	// Если установлено ограничение количества версий, проверяем, достигли ли предела
	Если Параметры.МаксимальноеКоличествоПодготавливаемыхВерсий > 0
			И Выборка.Следующий() Тогда

		Если Выборка.Количество >= Параметры.МаксимальноеКоличествоПодготавливаемыхВерсий Тогда

			ЗапуститьКоммитыВФоне(Хранилище);

			ПроверитьСостоянияЗаданий(Хранилище);

			Возврат;

		КонецЕсли;

	КонецЕсли;

	// Предела не достигли или ограничение количества версий не установлено
	Выборка = РезультатыЗапроса[1].Выбрать(); // ВыборкаИзРезультатаЗапроса - 

	Пока Выборка.Следующий() Цикл

		НачатьТранзакцию();

		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВерсииХранилища");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();

			ОбъектВерсии = Выборка.Ссылка.ПолучитьОбъект();

			Если ОбъектВерсии.Состояние <> Перечисления.СостоянияВерсии.ПустаяСсылка() Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;

			ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ПолучениеВерсии;
			ОбъектВерсии.Источник = Хранилище;
			ОбъектВерсии.Записать();
			
			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			Продолжить;

		КонецПопытки;

		ДлительныеОперации.СообщитьПрогресс(10, "Подготовка получения версии");

		Параметры.Вставить("Код", Выборка.Код);
		ВерсияХранилища = Выборка.Ссылка;

		ПолучитьВерсиюВБазу(Параметры, Хранилище, ВерсияХранилища);

		Прервать;

	КонецЦикла;

	ЗапуститьКоммитыВФоне(Хранилище);

	ПроверитьСостоянияЗаданий(Хранилище);

КонецПроцедуры

// Проверяет что у всех помещенных версий удалены временные данные
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище
Процедура ПроверитьУдалениеВременныхДанныхВерсийПослеКоммита(Хранилище) Экспорт
	
	УдалятьВременныеДанныеВерсииПослеКоммита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Хранилище, "УдалятьВременныеДанныеВерсииПослеКоммита");
	
	Если НЕ УдалятьВременныеДанныеВерсииПослеКоммита Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 10
		|	ВерсииХранилища.Ссылка
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Хранилище
		|	И ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВерсияПомещена)
		|	И ВЫРАЗИТЬ(ВерсииХранилища.КаталогВременныхФайлов КАК СТРОКА(100)) <> """"
		|УПОРЯДОЧИТЬ ПО
		|	ВерсииХранилища.Код";
	
	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		УдалитьФайлыВерсииВФоне(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет состояния заданий по хранилищу, если зависли или упали процессы - сбрасывает состояние
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - ссылка на хранилище
Процедура ПроверитьСостоянияЗаданий(Хранилище) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОчередиВыполнения") Тогда
		Возврат;
	КонецЕсли;

	// Контроль состояния фоновых процессов
	// Если Выгружается в XML, загружаются метаданные или выполняется коммит - то можем контролировать фоновые задания.
	// Если упало получение версии - нужно следить в ручную
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ВерсииХранилища.Ссылка,
		|	ВерсииХранилища.Владелец.МинимальноеКоличествоМетаданных КАК МинимальноеКоличествоМетаданных
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Хранилище
		| 	И НЕ ВерсииХранилища.ВыгрузкаИзменений
		|	И (ВерсииХранилища.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВерсияПолучена), ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВыгрузкаВерсии), ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВерсияВыгружена), ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ЗагрузкаМетаданных))
		|			ИЛИ ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.МетаданныеЗагружены)
		| 				И НЕ ВерсииХранилища.ВыгрузкаИзменений
		|				И ВерсииХранилища.Владелец.МинимальноеКоличествоМетаданных > 0
		|				И ВерсииХранилища.КоличествоМетаданных < ВерсииХранилища.Владелец.МинимальноеКоличествоМетаданных)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВерсииХранилища.Код";

	Запрос.УстановитьПараметр("Хранилище", Хранилище);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		НачатьТранзакцию();

		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВерсииХранилища");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаДетальныеЗаписи.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();

			ОбъектВерсии = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ВерсииХранилища -

			Если ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВыгрузкаВерсии
					ИЛИ ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ЗагрузкаМетаданных
					ИЛИ ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияВыгружена
					ИЛИ ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияПолучена Тогда

				КлючЗадания = Строка(ОбъектВерсии.Ссылка.УникальныйИдентификатор());
				Отбор = Новый Структура();
				Отбор.Вставить("Ключ", КлючЗадания);
				Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
				Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.ОбработатьВерсию");

				МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

				Если МассивФоновыхЗаданий.Количество() > 0 Тогда
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;

				Если ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВыгрузкаВерсии Тогда
					ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияПолучена;
					ОбъектВерсии.ВыгрузкаИзменений = Ложь;
					ОбъектВерсии.Записать();
				ИначеЕсли ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ЗагрузкаМетаданных Тогда
					ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияВыгружена;
					ОбъектВерсии.Записать();
				КонецЕсли;

				ЗафиксироватьТранзакцию();

				ЗапуститьОбработкуВерсииВФоне(ОбъектВерсии.Ссылка);

			ИначеЕсли ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.НачалоКоммита Тогда

			// Если фоновое задание коммитов упало, но коммит не завершился
				КлючЗадания = Строка(Хранилище.УникальныйИдентификатор());
				Отбор = Новый Структура();
				Отбор.Вставить("Ключ", КлючЗадания);
				Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
				Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.ВыполнитьКоммиты");

				МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

				Если МассивФоновыхЗаданий.Количество() > 0 Тогда
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;

				ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены;
				ОбъектВерсии.Записать();
				ЗафиксироватьТранзакцию();

				ЗапуститьКоммитыВФоне(Хранилище);

			ИначеЕсли ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены
					И НЕ ОбъектВерсии.ВыгрузкаИзменений
					И ОбъектВерсии.КоличествоМетаданных < ВыборкаДетальныеЗаписи.МинимальноеКоличествоМетаданных Тогда
					// Произошла проблема с выгрузкой - нужно переполучить версию
				ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ПустаяСсылка();
				ОбъектВерсии.ВыгрузкаИзменений = Ложь;
				ОбъектВерсии.Записать();
				ЗафиксироватьТранзакцию();

			Иначе
				ЗафиксироватьТранзакцию();
			КонецЕсли;

		Исключение

			Если ТранзакцияАктивна() Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			Продолжить;

		КонецПопытки;

	КонецЦикла;

КонецПроцедуры

// Захват версии в очереди на выгрузку и получение версии в базу
// 
// Параметры:
// 	Копия - СправочникСсылка.КопииХранилищКонфигурации, Неопределено - Ссылка на копию хранилища
Процедура ВыгрузитьВерсиюИзКопииХранилища(Знач Копия = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Копия) Тогда
		Возврат;
	КонецЕсли;

	ПроверитьПолученныеВерсии(Копия);

	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Копия", Копия);
	Запрос.Текст = "ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(КопииХранилищКонфигурации.Владелец.МаксимальноеКоличествоПодготавливаемыхВерсий), 0) КАК МаксимальноеКоличествоПодготавливаемыхВерсий,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВерсииХранилища.Ссылка), 0) КАК Количество
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КопииХранилищКонфигурации КАК КопииХранилищКонфигурации
		|		ПО ВерсииХранилища.Владелец = КопииХранилищКонфигурации.Владелец
		|ГДЕ
		|	КопииХранилищКонфигурации.Ссылка = &Копия
		|	И КопииХранилищКонфигурации.Владелец.МаксимальноеКоличествоПодготавливаемыхВерсий > 0
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ПустаяСсылка)
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.НачалоКоммита)
		|	И ВерсииХранилища.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ВерсияПомещена)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(КопииХранилищКонфигурации.КаталогВыгрузкиВерсий КАК СТРОКА(100))) = """"
		|			ТОГДА ВерсииХранилища.Владелец.КаталогВыгрузкиВерсий
		|		ИНАЧЕ КопииХранилищКонфигурации.КаталогВыгрузкиВерсий
		|	КОНЕЦ КАК КаталогВыгрузкиВерсий,
		|	ВерсииХранилища.Владелец КАК Хранилище,
		|	ВерсииХранилища.Ссылка КАК ВерсияХранилища,
		|	ВерсииХранилища.Код КАК Код,
		|	ВерсииХранилища.Владелец.ОтключитьЛогирование КАК ОтключитьЛогирование
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КопииХранилищКонфигурации КАК КопииХранилищКонфигурации
		|		ПО ВерсииХранилища.Владелец = КопииХранилищКонфигурации.Владелец
		|ГДЕ
		|	КопииХранилищКонфигурации.Ссылка = &Копия
		|	И ВерсииХранилища.Владелец.КонвертироватьВФорматEDT
		|	И ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ПустаяСсылка)
		|	И (КопииХранилищКонфигурации.ПерваяВерсия = 0
		|			ИЛИ НЕ КопииХранилищКонфигурации.ПерваяВерсия = 0
		|				И КопииХранилищКонфигурации.ПерваяВерсия <= ВерсииХранилища.Код)
		|	И (КопииХранилищКонфигурации.ПоследняяВерсия = 0
		|			ИЛИ НЕ КопииХранилищКонфигурации.ПоследняяВерсия = 0
		|				И КопииХранилищКонфигурации.ПоследняяВерсия >= ВерсииХранилища.Код)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Код";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();

	Выборка = РезультатыЗапроса[0].Выбрать(); // ВыборкаИзРезультатаЗапроса - 

	// Если установлено ограничение количества версий, проверяем, достигли ли предела
	Если Выборка.Следующий() Тогда

		Если Выборка.МаксимальноеКоличествоПодготавливаемыхВерсий > 0
				И Выборка.Количество >= Выборка.МаксимальноеКоличествоПодготавливаемыхВерсий Тогда

			Возврат;

		КонецЕсли;

	КонецЕсли;

	Выборка = РезультатыЗапроса[1].Выбрать();

	Пока Выборка.Следующий() Цикл

		НачатьТранзакцию();

		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВерсииХранилища");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.ВерсияХранилища);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();

			ОбъектВерсии = Выборка.ВерсияХранилища.ПолучитьОбъект(); // СправочникОбъект.ВерсииХранилища -

			Если ОбъектВерсии.Состояние <> Перечисления.СостоянияВерсии.ПустаяСсылка() Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;

			ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ПолучениеВерсии;
			ОбъектВерсии.Источник = Копия;
			ОбъектВерсии.Записать();
			
			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			Продолжить;

		КонецПопытки;

		Параметры = Новый Структура;
		Параметры.Вставить("Код", Выборка.Код);
		Параметры.Вставить("КопияХранилища", Копия);
		Параметры.Вставить("КаталогВыгрузкиВерсий", 
			ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Выборка.КаталогВыгрузкиВерсий));
		Параметры.Вставить("ОтключитьЛогирование", Выборка.ОтключитьЛогирование);
		
		ПолучитьВерсиюВБазу(Параметры, Выборка.Хранилище, Выборка.ВерсияХранилища);

		Прервать;

	КонецЦикла;

КонецПроцедуры

// Запускает выполнение коммитов в фоне, с проверкой что такое задание еще не запущено.
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище, по которому выполняются коммиты
Процедура ЗапуститьКоммитыВФоне(Знач Хранилище) Экспорт

	КлючЗадания = Строка(Хранилище.УникальныйИдентификатор());
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.ВыполнитьКоммиты");

	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

	Если МассивФоновыхЗаданий.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;

	Наименование = НСтр("ru = 'Коммит в хранилище: %Хранилище%'");
	Наименование = СтрЗаменить(Наименование, "%Хранилище%", Строка(Хранилище));

	Параметры = Новый Массив();
	Параметры.Добавить(Хранилище);

	ФоновыеЗадания.Выполнить("КонвертацияХранилища.ВыполнитьКоммиты", Параметры, КлючЗадания, Лев(Наименование, 120));

КонецПроцедуры

// Выполняет коммиты хранилища в Git
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище, по которому выполняются коммиты
Процедура ВыполнитьКоммиты(Знач Хранилище) Экспорт

	РеквизитыХранилища = ПараметрыХранилищаДляКоммита(Хранилище);
	
	Если НЕ РеквизитыХранилища.КонвертироватьВФорматEDT Тогда
		Возврат;
	КонецЕсли;

	Файл = Новый Файл(РеквизитыХранилища.ЛокальныйКаталогGit);
	Если Не Файл.Существует() Тогда
		СоздатьКаталог(РеквизитыХранилища.ЛокальныйКаталогGit);
	КонецЕсли;
	Файл = Новый Файл(РеквизитыХранилища.ЛокальныйКаталогGit);
	Если Не Файл.Существует() Тогда
		// Что делать если не существует?
		Возврат;
	КонецЕсли;

	Файл = Новый Файл(РеквизитыХранилища.ПутьКПроекту);
	Если Не Файл.Существует() Тогда
		СоздатьКаталог(РеквизитыХранилища.ПутьКПроекту);
	КонецЕсли;
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	ТекущаяВерсия = РеквизитыХранилища.ВерсияВGit;

	Если ТипЗнч(РеквизитыХранилища.Код) <> Тип("Число") Тогда
		РеквизитыХранилища.Код = 0;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВерсииХранилища.Ссылка,
	|	ВерсииХранилища.Код,
	|	ВерсииХранилища.КаталогВременныхФайлов,
	|	ВерсииХранилища.Комментарий,
	|	ВерсииХранилища.ДатаСоздания,
	|	ВЫБОР
	|		КОГДА
	|		НЕ ИнформацияПользователей.Имя ЕСТЬ NULL
	|			ТОГДА ИнформацияПользователей.Имя
	|		КОГДА
	|		НЕ ОбщаяИнформацияПользователей.Имя ЕСТЬ NULL
	|			ТОГДА ОбщаяИнформацияПользователей.Имя
	|		КОГДА
	|		НЕ ИнформацияПоУмолчанию.Имя ЕСТЬ NULL
	|		И ИнформацияПоУмолчанию.Имя <> """"
	|			ТОГДА ИнформацияПоУмолчанию.Имя
	|		ИНАЧЕ ВерсииХранилища.Пользователь
	|	КОНЕЦ КАК Пользователь,
	|	ЕСТЬNULL(ИнформацияПользователей.Email, ЕСТЬNULL(ОбщаяИнформацияПользователей.Email,
	|		ЕСТЬNULL(ИнформацияПоУмолчанию.Email, """"))) КАК Email,
	|	ВерсииХранилища.Пользователь КАК ПользовательХранилища,
	|	ВерсииХранилища.КоличествоМетаданных,
	| 	ВерсииХранилища.ВыгрузкаИзменений
	|ИЗ
	|	Справочник.ВерсииХранилища КАК ВерсииХранилища
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПользователей
	|		ПО ВерсииХранилища.Пользователь = ИнформацияПользователей.Пользователь
	|		И ВерсииХранилища.Владелец = ИнформацияПользователей.Хранилище
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ОбщаяИнформацияПользователей
	|		ПО ИнформацияПользователей.Имя ЕСТЬ NULL
	|		И ВерсииХранилища.Пользователь = ОбщаяИнформацияПользователей.Пользователь
	|		И ОбщаяИнформацияПользователей.Хранилище = ЗНАЧЕНИЕ(Справочник.ХранилищаКонфигураций.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПоУмолчанию
	|		ПО ОбщаяИнформацияПользователей.Имя ЕСТЬ NULL
	|		И (ИнформацияПоУмолчанию.Пользователь = """")
	|ГДЕ
	|	ВерсииХранилища.Владелец = &Хранилище
	|	И ВерсииХранилища.Код > &Код
	|	И ВерсииХранилища.Владелец.ВыполнятьКоммиты
	|УПОРЯДОЧИТЬ ПО
	|	ВерсииХранилища.Код";

	Запрос.УстановитьПараметр("Код", РеквизитыХранилища.Код);
	Запрос.УстановитьПараметр("Хранилище", Хранилище);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	// Ограничиваем количество коммитов за соединение
	КоличествоКоммитов = 0;

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		КоличествоКоммитов = КоличествоКоммитов + 1;
		Если КоличествоКоммитов > РеквизитыХранилища.КоличествоКоммитов
				И РеквизитыХранилища.КоличествоКоммитов > 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ РеквизитыХранилища.РазрешитьПомещатьАнонимноЕслиНеНайденПользователь
			И НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Email) Тогда
			Возврат;
		КонецЕсли;

		ПараметрыВерсии = ПараметрыВерсииДляКоммита();
		ПараметрыВерсии.Вставить("ИмяПроектаEDT", РеквизитыХранилища.ИмяПроектаEDT);
	
		ЗаполнитьЗначенияСвойств(ПараметрыВерсии, ВыборкаДетальныеЗаписи);
		
		ВерсияХранилища = ВыборкаДетальныеЗаписи.Ссылка; // СправочникСсылка.ВерсииХранилища - 
		Объект = ВерсияХранилища.ПолучитьОбъект();
		
		// Каталог исходных файлов проекта /path_to_dump/1/p/Project/
		ПараметрыВерсии.Вставить("ПутьКПроектуВерсии", ПутьКПроектуВерсии(ПараметрыВерсии));
		// Каталог исходных файлов проекта /path_to_dump/1/p/Project/src/
		ПараметрыВерсии.Вставить("ПутьКФайламПроектаВерсии", ПутьКФайламПроектаВерсии(ПараметрыВерсии));
		Если РеквизитыХранилища.ОтключитьЛогирование Тогда
			ИмяФайлаЛога = "";
		Иначе
			ИмяФайлаЛога = ИмяФайлаЛогаКоммитаВерсии(ПараметрыВерсии.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий);
			ПараметрыВерсии.Вставить("ИмяФайлаЛога", ИмяФайлаЛога);
		КонецЕсли;
		ИмяФайлаКомандыGit = ИмяФайлаКомандыКоммитаВерсии(ПараметрыВерсии.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий, ЭтоWindowsСервер);
		ИмяФайлКомментария = ИмяФайлаКомментарияКоммитаВерсии(ПараметрыВерсии.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий);
		
		// Если версия помещена, но не произошла запись в проект - переводим указатель на эту версию
		Если ТекущаяВерсия <> ВерсияХранилища
				И Объект.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена Тогда
			
			Объект = Хранилище.ПолучитьОбъект();
			Объект.ВерсияВGit = ВерсияХранилища;
			Объект.Записать();
			ТекущаяВерсия = ВерсияХранилища;

			Запрос.УстановитьПараметр("Код", ВыборкаДетальныеЗаписи.Код);

			РезультатЗапроса = Запрос.Выполнить();

			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Продолжить;
			
		// Если версия помещается - значит было падение процесса коммита, выясняем состояние
		ИначеЕсли ТекущаяВерсия <> ВерсияХранилища
				И Объект.Состояние = Перечисления.СостоянияВерсии.НачалоКоммита Тогда
			
			ФайлЛога = Новый Файл(ИмяФайлаЛога);
			// Если команда была запущена, не известно в каком состоянии процесс. 
			// Проверяем только не запущенную команду
			Если НЕ ФайлЛога.Существует() Тогда
				
				ФайлыКонфигурации = НайтиФайлы(ПараметрыВерсии.ПутьКФайламПроектаВерсии, "*", Истина);
				ТаблицаСостояний = РегистрыСведений.СостоянияВерсии.СрезПоследних(Неопределено, Новый Структура("ВерсияХранилища", ВерсияХранилища));
				Если ФайлыКонфигурации.Количество() = Объект.КоличествоМетаданных
					И ТаблицаСостояний.Количество() = 1 
					И ТаблицаСостояний[0].Состояние = Перечисления.СостоянияВерсии.НачалоКоммита 
					И ТаблицаСостояний[0].Период + 1200 < ТекущаяДатаСеанса()  Тогда
					// Файлы не были скопированы - можно попробовать еще раз через 20 мин
					Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.МетаданныеЗагружены);
				КонецЕсли;
			КонецЕсли;
			
			Возврат;
			
		ИначеЕсли ТекущаяВерсия = ВерсияХранилища
				ИЛИ Объект.Состояние <> Перечисления.СостоянияВерсии.МетаданныеЗагружены Тогда
			Возврат;
		КонецЕсли;

		Если РеквизитыХранилища.МинимальноеКоличествоМетаданных > 0
				И РеквизитыХранилища.МинимальноеКоличествоМетаданных > Объект.КоличествоМетаданных
				И НЕ Объект.ВыгрузкаИзменений Тогда
			Возврат;
		КонецЕсли;

		Объект = Неопределено;

		Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.НачалоКоммита);
		
		ФайлКомандыGit = Новый Файл(ИмяФайлаКомандыGit);
		ФайлКомментария = Новый Файл(ИмяФайлКомментария);
		Если НЕ ФайлКомандыGit.Существует() ИЛИ ФайлКомандыGit.Размер() = 0
				ИЛИ НЕ ФайлКомментария.Существует() Тогда

			ЗаписатьФайлыКоммитаВерсии(ВерсияХранилища, ТекущаяВерсия, РеквизитыХранилища, ПараметрыВерсии, 
				ИмяФайлаКомандыGit, ИмяФайлаЛога, ИмяФайлКомментария);

		КонецЕсли;

		Файл = Новый Файл(РеквизитыХранилища.ПутьКПроекту);
		Если Не Файл.Существует() Тогда
			СоздатьКаталог(РеквизитыХранилища.ПутьКПроекту);
		КонецЕсли;

		ФайлКонфигурации = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПараметрыВерсии.ПутьКФайламПроектаВерсии 
			+ "Configuration") + "Configuration.mdo";
		Файл = Новый Файл(ФайлКонфигурации);
		
		Если Не Файл.Существует() 
			И (НЕ РеквизитыХранилища.ВыгружатьИзменения 
			ИЛИ НЕ ПараметрыВерсии.ВыгрузкаИзменений) Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Коммит.Отсутствуют файлы конфигурации';", 
					Метаданные.ОсновнойЯзык.КодЯзыка), 
				УровеньЖурналаРегистрации.Ошибка, 
				Метаданные.Справочники.ВерсииХранилища, 
				ПараметрыВерсии.Ссылка, 
				НСтр("ru = 'Отсутствует файл Configuration.* в каталоге выгрузки версии';", 
					Метаданные.ОсновнойЯзык.КодЯзыка));
			Возврат;
		КонецЕсли;

		Если ПараметрыВерсии.ВыгрузкаИзменений Тогда
			ИмяФайлаИндексов = ПараметрыВерсии.ПутьКФайламПроектаВерсии + "DumpFilesIndex.txt";
			ТаблицаИндексов = ПрочитатьТаблицуИндексов(ИмяФайлаИндексов);
			ИмяФайлаИндексовПердыдущейВерсии = РеквизитыХранилища.ПутьКФайламПроекта + "DumpFilesIndex.txt";
			ТаблицаИндексовПредыдущейВерсии = ПрочитатьТаблицуИндексов(ИмяФайлаИндексовПердыдущейВерсии);
			Для Каждого СтрокаТЧ Из ТаблицаИндексов Цикл
				Отбор = Новый Структура();
				Отбор.Вставить("Уровень", СтрокаТЧ.Уровень);
				Отбор.Вставить("UUID",    СтрокаТЧ.UUID);
			
				НайденныеСтроки = ТаблицаИндексовПредыдущейВерсии.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() = 0 Тогда
					НоваяСтрока = ТаблицаИндексовПредыдущейВерсии.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
				КонецЕсли;
			КонецЦикла;
			ЗаписатьТаблицуИндексов(ТаблицаИндексовПредыдущейВерсии, ИмяФайлаИндексов);
			Объект = ВерсияХранилища.ПолучитьОбъект();
			Объект.КоличествоМетаданных = ТаблицаИндексовПредыдущейВерсии.Количество();
			Объект.Записать();
		КонецЕсли;
		
		МассивПутейПриемника = СтрРазделить(РеквизитыХранилища.ПутьКПроекту, ПолучитьРазделительПути(), Ложь);
		МассивПутейИсточника = СтрРазделить(ПараметрыВерсии.ПутьКПроектуВерсии, ПолучитьРазделительПути(), Ложь);
		
		// Для ускорения перемещаем исходные файлы если они на одном диске
		// А если на другом - нужно копировать рекурсивно
		Если НРег(МассивПутейИсточника[0]) <> НРег(МассивПутейПриемника[0]) Тогда
			// Сначала удаляем все файлы из src
			Если НЕ ПараметрыВерсии.ВыгрузкаИзменений Тогда
				УдалитьФайлы(РеквизитыХранилища.ПутьКФайламПроекта);
			КонецЕсли;
			СоздатьКаталог(РеквизитыХранилища.ПутьКФайламПроекта);
			// копируем всю директорию проекта
			СкопироватьФайлыРекурсивно(ПараметрыВерсии.ПутьКПроектуВерсии, РеквизитыХранилища.ПутьКПроекту);
		КонецЕсли;
		
		Если НЕ ВыполнитьДействияПередКоммитом(ПараметрыВерсии, РеквизитыХранилища) Тогда
			Объект = ВерсияХранилища.ПолучитьОбъект();
			Объект.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены;
			Объект.Записать();
			ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Начало коммита в Git'", НСтр("ru='Помещение изменений версии отмененео из-за предыдущих ошибок.'")));
			Возврат;
		КонецЕсли;
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Начало коммита в Git'"));

		КодВозврата = Неопределено;
		ЗапуститьПриложение(?(ЭтоWindowsСервер, "", "bash ")
			+ ИмяФайлаКомандыGit, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание коммита в Git'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));
		
		Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
			ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание коммита в Git'"),
				НСтр("ru='Коммит в Git выполнен не успешно. Необходимо проанализировать лог, устранить причину и вручную завершить коммит.'"));
			Возврат;
		КонецЕсли;

		ХешКоммита = "";
		ИмяФайлаХеша = ПолучитьИмяВременногоФайла("txt");
		ТекстКоманды = "git rev-parse HEAD > " + ИмяФайлаХеша;
		ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "") + ТекстКоманды, 
			РеквизитыХранилища.ЛокальныйКаталогGit, Истина);
		ФайлХеша = Новый Файл(ИмяФайлаХеша);
		Если ФайлХеша.Существует() Тогда
			ТекстФайлаХеша = Новый ТекстовыйДокумент();
			ТекстФайлаХеша.Прочитать(ИмяФайлаХеша);
			ХешКоммита = СокрЛП(ТекстФайлаХеша.ПолучитьТекст());
		КонецЕсли;
		
		Объект = ВерсияХранилища.ПолучитьОбъект();
		Если Объект <> Неопределено И (Объект.Состояние <> Перечисления.СостоянияВерсии.ВерсияПомещена
			ИЛИ ЗначениеЗаполнено(ХешКоммита) И Объект.Хеш <> ХешКоммита) Тогда
			Объект.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена;
			Объект.Хеш = ХешКоммита;
			Объект.Записать();
		КонецЕсли;
		
		Объект = Хранилище.ПолучитьОбъект();
		Объект.ВерсияВGit = ВерсияХранилища;
		Объект.Записать();
		ТекущаяВерсия = ВерсияХранилища;
		РеквизитыХранилища.Вставить("ВерсияВGit", ВерсияХранилища);
		РеквизитыХранилища.Вставить("Код", ВыборкаДетальныеЗаписи.Код);
		
		Если НЕ ВыполнитьДействаияПослеКоммита(ПараметрыВерсии, РеквизитыХранилища) Тогда
			Возврат;
		КонецЕсли;

		Запрос.УстановитьПараметр("Код", РеквизитыХранилища.Код);

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	КонецЦикла;

КонецПроцедуры


// Процедура - Сформирует файлы коммита версии
//
// Параметры:
//  ВерсияХранилища	 - СправочникСсылка.ВерсииХранилища	 - ссылка на версию
//
Процедура СформироватьФайлыКоммитаВерсии(ВерсияХранилища) Экспорт

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	ИменаРеквизитов = Новый Структура("Код, ЛокальныйКаталогGit, ИмяПроектаEDT, КаталогВыгрузкиВерсий, 
		| АдресРепозиторияGit, Хранилище, ИмяВетки, МинимальноеКоличествоМетаданных, 
		| РазрешитьПомещатьАнонимноЕслиНеНайденПользователь, ВыгружатьИзменения, ОтключитьЛогирование",
		"Код", 
		"Владелец.ЛокальныйКаталогGit",
		"Владелец.ИмяПроектаEDT", 
		"Владелец.КаталогВыгрузкиВерсий", 
		"Владелец.АдресРепозиторияGit", 
		"Владелец", 
		"Владелец.ИмяВетки", 
		"Владелец.МинимальноеКоличествоМетаданных",
		"Владелец.РазрешитьПомещатьАнонимноЕслиНеНайденПользователь",
		"Владелец.ВыгружатьИзменения",
		"Владелец.ОтключитьЛогирование");
	РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВерсияХранилища, ИменаРеквизитов);

	Файл = Новый Файл(РеквизитыХранилища.ЛокальныйКаталогGit);
	Если Не Файл.Существует() Тогда
		// Что делать если не существует?
		Возврат;
	КонецЕсли;

	Если РеквизитыХранилища.ОтключитьЛогирование Тогда
		ИмяФайлаЛога = "";
	Иначе
		ИмяФайлаЛога = ИмяФайлаЛогаКоммитаВерсии(РеквизитыХранилища.Код, 
			РеквизитыХранилища.КаталогВыгрузкиВерсий);
	КонецЕсли;
	ИмяФайлаКомандыGit = ИмяФайлаКомандыКоммитаВерсии(РеквизитыХранилища.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий, ЭтоWindowsСервер);
	ИмяФайлКомментария = ИмяФайлаКомментарияКоммитаВерсии(РеквизитыХранилища.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий);

	ФайлКомандыGit = Новый Файл(ИмяФайлаКомандыGit);
	ФайлКомментария = Новый Файл(ИмяФайлКомментария);
	Если ФайлКомандыGit.Существует() ИЛИ ФайлКомментария.Существует() Тогда
		Возврат;
	КонецЕсли;

	// Путь к проекту в репозитории /path_to_git/Project/
	РеквизитыХранилища.Вставить("ПутьКПроекту", ПутьКПроекту(РеквизитыХранилища));
	// Путь к файлам конфигурации проекта в репозитории /path_to_git/Project/src/
	РеквизитыХранилища.Вставить("ПутьКФайламПроекта", ПутьКФайламПроекта(РеквизитыХранилища));
	
	ПутьКИсходнымФайлам = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit);
	ПутьКИсходнымФайлам = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПутьКИсходнымФайлам
		+ КаталогФайловПроектаВРепозитории(РеквизитыХранилища));

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВерсииХранилища.Ссылка,
		|	ВерсииХранилища.Состояние,
		| 	ВерсииХранилища.ВыгрузкаИзменений
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Хранилище
		|	И ВерсииХранилища.Код < &Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВерсииХранилища.Код УБЫВ";

	Запрос.УстановитьПараметр("Код", РеквизитыХранилища.Код);
	Запрос.УстановитьПараметр("Хранилище", РеквизитыХранилища.Хранилище);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда

		Если ВыборкаДетальныеЗаписи.Состояние <> Перечисления.СостоянияВерсии.МетаданныеЗагружены 
			ИЛИ ВыборкаДетальныеЗаписи.ВыгрузкаИзменений Тогда
			Возврат;
		КонецЕсли;

		ПредыдущаяВерсия = ВыборкаДетальныеЗаписи.Ссылка;

	Иначе
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВерсииХранилища.Ссылка,
	|	ВерсииХранилища.Код,
	|	ВерсииХранилища.КаталогВременныхФайлов,
	|	ВерсииХранилища.Комментарий,
	|	ВерсииХранилища.ДатаСоздания,
	|	ВЫБОР
	|		КОГДА
	|		НЕ ИнформацияПользователей.Имя ЕСТЬ NULL
	|			ТОГДА ИнформацияПользователей.Имя
	|		КОГДА
	|		НЕ ОбщаяИнформацияПользователей.Имя ЕСТЬ NULL
	|			ТОГДА ОбщаяИнформацияПользователей.Имя
	|		КОГДА
	|		НЕ ИнформацияПоУмолчанию.Имя ЕСТЬ NULL
	|		И ИнформацияПоУмолчанию.Имя <> """"
	|			ТОГДА ИнформацияПоУмолчанию.Имя
	|		ИНАЧЕ ВерсииХранилища.Пользователь
	|	КОНЕЦ КАК Пользователь,
	|	ЕСТЬNULL(ИнформацияПользователей.Email, ЕСТЬNULL(ОбщаяИнформацияПользователей.Email,
	|		ЕСТЬNULL(ИнформацияПоУмолчанию.Email, """"))) КАК Email,
	|	ВерсииХранилища.Пользователь КАК ПользовательХранилища,
	|	ВерсииХранилища.КоличествоМетаданных,
	| 	ВерсииХранилища.ВыгрузкаИзменений
	|ИЗ
	|	Справочник.ВерсииХранилища КАК ВерсииХранилища
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПользователей
	|		ПО ВерсииХранилища.Пользователь = ИнформацияПользователей.Пользователь
	|		И ВерсииХранилища.Владелец = ИнформацияПользователей.Хранилище
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ОбщаяИнформацияПользователей
	|		ПО ИнформацияПользователей.Имя ЕСТЬ NULL
	|		И ВерсииХранилища.Пользователь = ОбщаяИнформацияПользователей.Пользователь
	|		И ОбщаяИнформацияПользователей.Хранилище = ЗНАЧЕНИЕ(Справочник.ХранилищаКонфигураций.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПоУмолчанию
	|		ПО ОбщаяИнформацияПользователей.Имя ЕСТЬ NULL
	|		И ИнформацияПоУмолчанию.Пользователь = """"
	|ГДЕ
	|	ВерсииХранилища.Ссылка = &ВерсияХранилища";

	Запрос.УстановитьПараметр("ВерсияХранилища", ВерсияХранилища);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда

		Если РеквизитыХранилища.МинимальноеКоличествоМетаданных > 0
				И РеквизитыХранилища.МинимальноеКоличествоМетаданных > ВыборкаДетальныеЗаписи.КоличествоМетаданных
				И НЕ ВыборкаДетальныеЗаписи.ВыгрузкаИзменений Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ РеквизитыХранилища.РазрешитьПомещатьАнонимноЕслиНеНайденПользователь 
			И НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Email) Тогда
			Возврат;
		КонецЕсли;

		Если ФайлКомандыGit.Существует() ИЛИ ФайлКомментария.Существует() Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыВерсии = Новый Структура("Ссылка, Комментарий, ДатаСоздания, Пользователь, Email, Код,
			|ПользовательХранилища, ВыгрузкаИзменений, КаталогВременныхФайлов, ИмяПроектаEDT");

		ЗаполнитьЗначенияСвойств(ПараметрыВерсии, ВыборкаДетальныеЗаписи);
		ПараметрыВерсии.ИмяПроектаEDT = РеквизитыХранилища.ИмяПроектаEDT;
		
		ЗаписатьФайлыКоммитаВерсии(ВерсияХранилища, ПредыдущаяВерсия, РеквизитыХранилища, ПараметрыВерсии, 
			ИмяФайлаКомандыGit, ИмяФайлаЛога, ИмяФайлКомментария);

	КонецЕсли;

КонецПроцедуры

// Запускает фоновую операцию по версии
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - ссылка на версию хранилища
Процедура ЗапуститьОбработкуВерсииВФоне(ВерсияХранилища) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьОчередиВыполнения") Тогда
		Возврат;
	КонецЕсли;

	Отбор = Новый Структура();
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.ОбработатьВерсию");

	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Если МассивФоновыхЗаданий.Количество() > 50 Тогда
		Возврат;
	КонецЕсли;

	КлючЗадания = Строка(ВерсияХранилища.УникальныйИдентификатор());

	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.ОбработатьВерсию");

	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

	Если МассивФоновыхЗаданий.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;

	Наименование = НСтр("ru = 'Обработка версии: %Версия%'");
	Наименование = СтрЗаменить(Наименование, "%Версия%", Строка(ВерсияХранилища));

	Параметры = Новый Массив();
	Параметры.Добавить(ВерсияХранилища);

	ФоновыеЗадания.Выполнить("КонвертацияХранилища.ОбработатьВерсию", Параметры, КлючЗадания, Лев(Наименование, 120));

КонецПроцедуры

// Выполняет обработку версии хранилища
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - ссылка на версию хранилища
Процедура ОбработатьВерсию(Знач ВерсияХранилища) Экспорт

	Параметры = ПолучитьПараметрыОбработкиВерсии(ВерсияХранилища);

	// Выгружаем версию в файлы
	ВыгрузитьВерсию(ВерсияХранилища, Параметры);

	// Загружает файлы конфигурации версии в проект EDT и составляет индекс для переименований в Git-е
	ЗагрузитьВерсию(ВерсияХранилища, Параметры);

	// Далее коммит будет отработан в потоке по хранилищу для всех готовых версий.
КонецПроцедуры

// Получает структуру параметров версии
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - ссылка на версию хранилища
// Возвращаемое значение:
// 	Структура - Параметры версии:
// 	* Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище
// 	* Код - Число - Код версии
// 	* Состояние - ПеречислениеСсылка.СостоянияВерсии - Текущее состояние версии
// 	* КаталогВременныхФайлов - Строка - Каталог временных файлов версии
// 	* ВыгрузкаИзменений - Булево - Признак выгрузки изменений в текущей версии
// 	* КаталогВыгрузкиВерсий - Строка - Общий каталог в котором создаются подкаталоги для каждой версии
// 	* ВерсияПлатформы - Строка - Версия Платформы
// 	* МинимальноеКоличествоМетаданных - Число - Минимальное количество метаданных
// 	* УдалятьКонфигурацииПоставщиков - Булево - Удалять ли файлы конфигураций поставщиков
// 	* ВыгружатьИзменения - Булево - Призанк необходимости выгружать изменения
// 	* КонвертироватьВФорматEDT - Булево - Признак выгрузки в формат EDT
// 	* ИмяПроектаEDT - Строка - Имя проекта EDT или сегмент пути в репозитории, где последний сегмент является именем проекта
// 	* ВерсияEDT - Строка - Версия EDT
// 	* ОтключитьЛогирование - Булево - Флаг отключения логирования
// 	* КаталогФайловКонфигурации - Строка - Каталог выгрузки xml файлов версии в формате 1С:Предприятия
// 	* РабочийКаталогEDT - Строка - Путь к каталогу рабочей области EDT для версии
// 	* КаталогПроекта - Строка - Путь к временному каталогу проекта в формате EDT для подготовлеваемой версии
// 	* КаталогФайловПроекта - Строка - Путь к временному каталогу файлов в формате EDT для версии
// 	* КаталогИБ - Строка - Каталог БД для версии полученной из хранилища
// 	* Ключ - Строка - Уникальный идентификатор операции
// 	* ФайлПараметровПакетнойОперации - Строка - 
// 	* ИмяФайлаЛога - Строка - 
// 	* ИмяФайлаРезультатов - Строка - 
// 	
Функция ПолучитьПараметрыОбработкиВерсии(Знач ВерсияХранилища) Экспорт

	ИменаРеквизитов = Новый Структура("Хранилище, Код, Состояние, КаталогВременныхФайлов, ВыгрузкаИзменений", 
		"Владелец");
	РеквизитыВерсии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВерсияХранилища, ИменаРеквизитов);

	Хранилище = РеквизитыВерсии.Хранилище;

	Параметры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, "КаталогВыгрузкиВерсий, ВерсияПлатформы,
		|МинимальноеКоличествоМетаданных, УдалятьКонфигурацииПоставщиков, ВыгружатьИзменения,
		|КонвертироватьВФорматEDT, ИмяПроектаEDT, ВерсияEDT, ОтключитьЛогирование");
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Параметры, РеквизитыВерсии);

	КлючОперации = Строка(Хранилище.УникальныйИдентификатор()) + "_"
		+ Строка(ВерсияХранилища.УникальныйИдентификатор()) + "_2";

	Если НЕ ЗначениеЗаполнено(Параметры.КаталогВременныхФайлов) Тогда

		Параметры.Вставить("КаталогВременныхФайлов", ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВыгрузкиВерсий)
			+ Формат(Параметры.Код, "ЧДЦ=; ЧГ=0"));

	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Параметры.ВерсияПлатформы) Тогда
		СисИнфо = Новый СистемнаяИнформация();
		Параметры.ВерсияПлатформы = СисИнфо.ВерсияПриложения;
	КонецЕсли;
	Параметры.Вставить("Это8315", ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.15.0") > 0);
	
	Параметры.КаталогВременныхФайлов = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВременныхФайлов);
	Параметры.Вставить("КаталогФайловКонфигурации", КаталогФайловКонфигурации(Параметры.КаталогВременныхФайлов));
	Параметры.КаталогФайловКонфигурации = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогФайловКонфигурации);

	Параметры.Вставить("РабочийКаталогEDT", ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВременныхФайлов + "ws"));
	
	Параметры.Вставить("КаталогПроекта", ПутьКПроектуВерсии(Параметры));
	Параметры.Вставить("КаталогФайловПроекта", ПутьКФайламПроектаВерсии(Параметры));
	
	Параметры.Вставить("КаталогИБ", Параметры.КаталогВременныхФайлов + "db");
	Параметры.КаталогИБ = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогИБ);
	
	Файл = Новый Файл(Параметры.КаталогВременныхФайлов);
	Если НЕ Файл.Существует() Тогда
		СоздатьКаталог(Параметры.КаталогВременныхФайлов);
	КонецЕсли;

	Параметры.Вставить("Ключ", КлючОперации);
	Параметры.Вставить("ФайлПараметровПакетнойОперации", Параметры.КаталогВременныхФайлов
		+ "params_" + Параметры.Ключ + ".txt");
	Если НЕ Параметры.ОтключитьЛогирование Тогда
		Параметры.Вставить("ИмяФайлаЛога", ИмяФайлаЛогаОбработкиВерсии(Параметры.КаталогВременныхФайлов));
	КонецЕсли;
	Параметры.Вставить("ИмяФайлаРезультатов", Параметры.КаталогВременныхФайлов
		+ "result.txt");

	ПараметрыРасширения(Хранилище, Параметры);
	
	Возврат Параметры;

КонецФункции

// Формирует полное имя файла лога конвертации хранилища
// 
// Параметры:
// 	КаталогВременныхФайлов - Строка - Каталог временных файлов хранилища
// Возвращаемое значение:
// 	Строка - Полное имя файла лога конвертации хранилища
Функция ИмяФайлаЛогаКонвертацииХранилища(КаталогВременныхФайлов) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВременныхФайлов) + "log.txt";
	
КонецФункции

// Формирует полное имя файла лога обработки версии
// 
// Параметры:
// 	КаталогВременныхФайлов - Строка - Каталог временных файлов версии
// Возвращаемое значение:
// 	Строка - Полное имя файла лога обработки версии
Функция ИмяФайлаЛогаОбработкиВерсии(КаталогВременныхФайлов) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВременныхФайлов) 
			+ "log.txt";
	
КонецФункции

// Формирует полное имя файла лога коммита версии
// 
// Параметры:
// 	КодВерсии - Строка - Код версии хранилища
// 	КаталогВыгрузкиВерсий - Строка - Каталог выгрузки версий хранилища
// Возвращаемое значение:
// 	Строка - Полное имя файла лога коммита версии
Функция ИмяФайлаЛогаКоммитаВерсии(КодВерсии, КаталогВыгрузкиВерсий) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиВерсий)
			+ "git_log_ver_" + Формат(КодВерсии, "ЧДЦ=; ЧГ=0") + ".txt";
КонецФункции

// Формирует полное имя файла команды коммита версии для запуска в системе
// 
// Параметры:
// 	КодВерсии - Строка - Код версии хранилища
// 	КаталогВыгрузкиВерсий - Строка - Каталог выгрузки версий хранилища
// 	ЭтоWindowsСервер - Булево - Признак текущей операционной системы, Истина - это ОС Windows, 
// 						Ложь - это ОС Linux или macOS
// Возвращаемое значение:
// 	Строка - Полное имя файла команды коммита версии
Функция ИмяФайлаКомандыКоммитаВерсии(КодВерсии, КаталогВыгрузкиВерсий, ЭтоWindowsСервер) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиВерсий)
			+ "git_command_" + Формат(КодВерсии, "ЧДЦ=; ЧГ=0")
			+ ?(ЭтоWindowsСервер, ".bat", ".sh");
КонецФункции

// Формирует полное имя файла комментария коммита версии
// 
// Параметры:
// 	КодВерсии - Строка - Код версии хранилища
// 	КаталогВыгрузкиВерсий - Строка - Каталог выгрузки версий хранилища
// Возвращаемое значение:
// 	Строка - Полное имя файла комментария коммита версии
Функция ИмяФайлаКомментарияКоммитаВерсии(КодВерсии, КаталогВыгрузкиВерсий) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВыгрузкиВерсий)
			+ "git_comment_" + Формат(КодВерсии, "ЧДЦ=; ЧГ=0") + ".txt";
КонецФункции

// Выгружает версию хранилища в файлы
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - Ссылка на версию
// 	Параметры - Структура - Параметры запуска
Процедура ВыгрузитьВерсию(Знач ВерсияХранилища, Параметры) Экспорт

	Если Параметры.Состояние <> Перечисления.СостоянияВерсии.ВерсияПолучена ИЛИ НЕ Параметры.КонвертироватьВФорматEDT Тогда
		Возврат;
	КонецЕсли;

	ДлительныеОперации.СообщитьПрогресс(10, "Выгрузка версии из базы в xml");

	Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.ВыгрузкаВерсии);
	Параметры.Состояние = Перечисления.СостоянияВерсии.ВыгрузкаВерсии;

	ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);

	Файл = Новый Файл(Параметры.КаталогФайловКонфигурации);
	Если НЕ Файл.Существует() Тогда
		СоздатьКаталог(Параметры.КаталогФайловКонфигурации);
	КонецЕсли;

	Параметры.Вставить("ВыгрузитьКонфигурациюВФайлы", Истина);
	СтрокаСоединения = " /F ""%Путь%""";
	СтрокаСоединения = СтрЗаменить(СтрокаСоединения, "%Путь%", Параметры.КаталогИБ);
	Параметры.Вставить("СтрокаСоединенияИБ", СтрокаСоединения);
	Параметры.Вставить("ИмяПользователяИБ", "");
	Параметры.Вставить("ПарольПользователяИБ", "");
	
	Параметры.Вставить("ФайлИзменений", Параметры.КаталогВременныхФайлов + "diff.txt");
	Параметры.Вставить("ПолучитьСписокИзменений", Ложь); // Получение списка изменений отдельно
	Параметры.Вставить("ФайлСостоянияПредыдущейВерсии", Параметры.КаталогВременныхФайлов + "PreviousState.xml");
		
	Если Параметры.ВыгружатьИзменения Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВерсииХранилища.Ссылка,
			|	ВерсииХранилища.КаталогВременныхФайлов
			|ИЗ
			|	Справочник.ВерсииХранилища КАК ВерсииХранилища
			|ГДЕ
			|	ВерсииХранилища.Владелец = &Владелец
			| 	И ВерсииХранилища.Код < &Код
			|УПОРЯДОЧИТЬ ПО
			|  ВерсииХранилища.Код УБЫВ";

		Запрос.УстановитьПараметр("Владелец", Параметры.Хранилище);
		Запрос.УстановитьПараметр("Код", Параметры.Код);

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			НачатьТранзакцию();

			Попытка

				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.ВерсииХранилища");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаДетальныеЗаписи.Ссылка);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				// Ответственное чтение версии, держим версию заблокированой до окончания копирования
				Состояние = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДетальныеЗаписи.Ссылка, "Состояние");

				Если Параметры.Это8315 И (Состояние = Перечисления.СостоянияВерсии.ВерсияПолучена
					ИЛИ Состояние = Перечисления.СостоянияВерсии.ВыгрузкаВерсии)
					ИЛИ Состояние = Перечисления.СостоянияВерсии.ВерсияВыгружена
					ИЛИ Состояние = Перечисления.СостоянияВерсии.ЗагрузкаМетаданных
					ИЛИ Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены Тогда
					
					ФайлСостоянияПредыдущейВерсии = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
						ВыборкаДетальныеЗаписи.КаталогВременныхФайлов) + "ConfigDumpInfo.xml";
					Файл = Новый Файл(ФайлСостоянияПредыдущейВерсии);
					Если Файл.ЭтоФайл() И Файл.Существует() Тогда
						КопироватьФайл(ФайлСостоянияПредыдущейВерсии, Параметры.ФайлСостоянияПредыдущейВерсии);
					Иначе
						Параметры.ВыгружатьИзменения = Ложь;
					КонецЕсли;
				ИначеЕсли Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена Тогда
					Реквизиты = "ЛокальныйКаталогGit, ИмяПроектаEDT";
					РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Хранилище, Реквизиты);
					ФайлСостоянияПредыдущейВерсии = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
						КаталогФайловПроектаВРепозитории(РеквизитыХранилища)) + "ConfigDumpInfo.xml";
					ФайлСостоянияПредыдущейВерсии = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
						РеквизитыХранилища.ЛокальныйКаталогGit) + ФайлСостоянияПредыдущейВерсии;
					Файл = Новый Файл(ФайлСостоянияПредыдущейВерсии);
					Если Файл.Существует() И Файл.ЭтоФайл() Тогда
						КопироватьФайл(ФайлСостоянияПредыдущейВерсии, Параметры.ФайлСостоянияПредыдущейВерсии);
					Иначе
						Параметры.ВыгружатьИзменения = Ложь;
					КонецЕсли;
				ИначеЕсли Состояние = Перечисления.СостоянияВерсии.ПолучениеВерсии И Параметры.Это8315 Тогда
					// Если предыдущая версия еще в процессе получения - не выгружаем
					Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.ВерсияПолучена);
					Параметры.Состояние = Перечисления.СостоянияВерсии.ВерсияПолучена;
					Возврат;
				Иначе
					Параметры.ВыгружатьИзменения = Ложь;
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();

			Исключение

				ОтменитьТранзакцию();
				Параметры.ВыгружатьИзменения = Ложь;

			КонецПопытки;
		Иначе
			Параметры.ВыгружатьИзменения = Ложь;
		КонецЕсли;
	КонецЕсли;

	УдалитьФайлы(Параметры.КаталогФайловКонфигурации);
	
	ВыгрузитьКонфигурациюВФайлы(Параметры);
	
	// Если в хранилище 2 версии с одинаковым состоянием ConfigDumpInfo и выгрузки изменений не произошло
	// то - выполняем полную выгрузку
	Если Параметры.ВыгружатьИзменения Тогда
		НайденныеФайлы = НайтиФайлы(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогФайловКонфигурации), "*", Истина);
		Если НайденныеФайлы.Количество() = 0 
			ИЛИ НайденныеФайлы.Количество() = 1 
			И НайденныеФайлы[0].Имя = "ConfigDumpInfo.xml" Тогда
			Параметры.ВыгружатьИзменения = Ложь;
			ВыгрузитьКонфигурациюВФайлы(Параметры);
		КонецЕсли;
	КонецЕсли;
	
	// Если выгрузка изменений - получаем список изменений в файл
	Если Параметры.ВыгружатьИзменения Тогда
		Параметры.Вставить("ПолучитьСписокИзменений", Истина);
		ВыгрузитьКонфигурациюВФайлы(Параметры);
	КонецЕсли;

	ИмяФайлаИсточника = Параметры.КаталогФайловКонфигурации + "ConfigDumpInfo.xml";
	ИмяФайлаПриемника = Параметры.КаталогВременныхФайлов + "ConfigDumpInfo.xml";
	Файл = Новый Файл(ИмяФайлаПриемника);
	Если Файл.Существует() Тогда
		УдалитьФайлы(ИмяФайлаПриемника);
	КонецЕсли;
	Файл = Новый Файл(ИмяФайлаИсточника);
	Если Файл.Существует() Тогда
		КопироватьФайл(ИмяФайлаИсточника, ИмяФайлаПриемника);
		УдалитьФайлы(ИмяФайлаИсточника);
	КонецЕсли;
	
	
	Если Параметры.ВыгружатьИзменения Тогда
		ФайлИзменений = Новый ТекстовыйДокумент();
		ФайлИзменений.Прочитать(Параметры.ФайлИзменений);
		
		Параметры.Вставить("ВыгрузкаИзменений", Ложь);
		ТекстИзменений = ФайлИзменений.ПолучитьТекст();
		Если НРег(СокрЛП(ТекстИзменений)) <> "fulldump" Тогда
			ВерсияОбъект = ВерсияХранилища.ПолучитьОбъект();
			ВерсияОбъект.ВыгрузкаИзменений = Истина;
			ВерсияОбъект.Записать();
			Параметры.Вставить("ВыгрузкаИзменений", Истина);
		КонецЕсли;
		
		Если Параметры.ВыгрузкаИзменений Тогда
			ДлительныеОперации.СообщитьПрогресс(50, "Анализ частичной выгрузки для конвертации в EDT");
			ОбъектыДовыгрузки = ПолучитьСписокОбъектовДовыгрузки(ФайлИзменений, ИмяФайлаПриемника);
			Если ОбъектыДовыгрузки.Количество() > 0 Тогда
				ДлительныеОперации.СообщитьПрогресс(55, "Подготовка частичной выгрузки для конвертации в EDT");
				Параметры.ВыгружатьИзменения = Ложь;
				Параметры.Вставить("ФайлСпискаВыгрузки", Параметры.КаталогВременныхФайлов + "dumplist.txt");
				Параметры.Вставить("ВыгружатьПоСписку", Истина);
				ФайлОбъектовВыгрузки = Новый ТекстовыйДокумент();
				Для Каждого ОбъектВыгрузки Из ОбъектыДовыгрузки Цикл
					ФайлОбъектовВыгрузки.ДобавитьСтроку(ОбъектВыгрузки);
				КонецЦикла;
				ФайлОбъектовВыгрузки.Записать(Параметры.ФайлСпискаВыгрузки, КодировкаТекста.UTF8);
				
				ДлительныеОперации.СообщитьПрогресс(60, "Довыгрузка объектов для конвертации в EDT");
				ВыгрузитьКонфигурациюВФайлы(Параметры);
				Параметры.ВыгружатьИзменения = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если Параметры.УдалятьКонфигурацииПоставщиков Тогда
		ДлительныеОперации.СообщитьПрогресс(90, "Удаление конфигураций поставщиков");
		
		КаталогКонфигурации = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогФайловКонфигурации + "Ext");
		
		ФайлНастроекПоставщиков = КаталогКонфигурации + "ParentConfigurations.bin";
		Файл = Новый Файл(ФайлНастроекПоставщиков);
		Если Файл.Существует() Тогда
			УдалитьФайлы(ФайлНастроекПоставщиков);	
		КонецЕсли;
		
		КаталогПоставщиков = КаталогКонфигурации + "ParentConfigurations";
		Файл = Новый Файл(КаталогПоставщиков);
		Если Файл.Существует() Тогда
			УдалитьФайлы(КаталогПоставщиков);	
		КонецЕсли;
		
	КонецЕсли;

	ДлительныеОперации.СообщитьПрогресс(100, "Выгрузка версии из базы в xml завершена");
	
	Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.ВерсияВыгружена);
	Параметры.Состояние = Перечисления.СостоянияВерсии.ВерсияВыгружена;

КонецПроцедуры

// Загружает файлы конфигурации версии в проект EDT и составляет индекс
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - Ссылка на версию хранилища
// 	Параметры - Структура - Параметры запуска
Процедура ЗагрузитьВерсию(ВерсияХранилища, Параметры) Экспорт
	
	Если Параметры.Состояние <> Перечисления.СостоянияВерсии.ВерсияВыгружена ИЛИ НЕ Параметры.КонвертироватьВФорматEDT Тогда
		Возврат;
	КонецЕсли;

	Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.ЗагрузкаМетаданных);
	Параметры.Состояние = Перечисления.СостоянияВерсии.ЗагрузкаМетаданных;
	
	ИмпортироватьВерсиюВEDT(ВерсияХранилища, Параметры);
	
	ПрочитатьСтруктуруВыгрузкиВерсии(ВерсияХранилища, Параметры);
	
КонецПроцедуры

// Импортирует файлы версии в проект в формате EDT
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - Ссылка на версию хранилища
// 	Параметры - Структура - Параметры запуска
Процедура ИмпортироватьВерсиюВEDT(ВерсияХранилища, Параметры) Экспорт
	
	Если Параметры.Состояние <> Перечисления.СостоянияВерсии.ЗагрузкаМетаданных Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало импорта в 1C:EDT'"));
	ДлительныеОперации.СообщитьПрогресс(10, НСтр("ru = 'Импорт версии в 1C:EDT';"));
	
	НомераВерсии = СтрРазделить(Параметры.ВерсияПлатформы, ".");
	НомераВерсии.Удалить(НомераВерсии.ВГраница());
	ВерсияПроектаEDT = СтрСоединить(НомераВерсии, ".");
	
	СтрокаКоманды = "ring edt workspace import %БазовыйПроект% --workspace-location ""%РабочийКаталог%"" --configuration-files ""%КаталогФайловКонфигурации%"" --project ""%КаталогПроектаEDT%"" --version %ВерсияПроектаEDT%";
	
	УстановитьВерсиюEDT(СтрокаКоманды, Параметры.ВерсияEDT);
	
	Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
		СтрокаКоманды = СтрокаКоманды + " >> ""%ИмяФайлаЛога%"" 2>&1";
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
	КонецЕсли;
	Если Прав(Параметры.РабочийКаталогEDT, 1) = ПолучитьРазделительПути() Тогда
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%РабочийКаталог%", Лев(Параметры.РабочийКаталогEDT, СтрДлина(Параметры.РабочийКаталогEDT)-1));
	Иначе
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%РабочийКаталог%", Параметры.РабочийКаталогEDT);
	КонецЕсли;
	Если Прав(Параметры.КаталогФайловКонфигурации, 1) = ПолучитьРазделительПути() Тогда
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогФайловКонфигурации%", Лев(Параметры.КаталогФайловКонфигурации, СтрДлина(Параметры.КаталогФайловКонфигурации)-1));
	Иначе
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогФайловКонфигурации%", Параметры.КаталогФайловКонфигурации);
	КонецЕсли;
	Если Прав(Параметры.КаталогПроекта, 1) = ПолучитьРазделительПути() Тогда
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогПроектаEDT%", Лев(Параметры.КаталогПроекта, СтрДлина(Параметры.КаталогПроекта)-1));
	Иначе
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогПроектаEDT%", Параметры.КаталогПроекта);
	КонецЕсли;
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ВерсияПроектаEDT%", ВерсияПроектаEDT);
	Если Параметры.Свойство("ТипХранилища") И Параметры.ТипХранилища = Перечисления.ТипыХранилищаКонфигураций.Расширение Тогда
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%БазовыйПроект%", "--base-project-name """ + Параметры.БазовыйПроект + """");
	Иначе
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%БазовыйПроект%", "");
	КонецЕсли;

	Файл = Новый Файл(Параметры.РабочийКаталогEDT);
	Если Файл.Существует() Тогда
		УдалитьФайлы(Параметры.РабочийКаталогEDT, "*");
	КонецЕсли;
	Файл = Новый Файл(Параметры.КаталогПроекта);
	Если Файл.Существует() Тогда
		УдалитьФайлы(Параметры.КаталогПроекта, "*");
	КонецЕсли;
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "")
		+ СтрокаКоманды, Параметры.КаталогВременныхФайлов, Истина, КодВозврата);

	ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание импорта в 1C:EDT'"), "Код возврата: "
		+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));
	
	Если КодВозврата <> 0 Тогда
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'При импорте в 1C:EDT возникли ошибки. Подробнее см. файл лога:
				|%1'", Параметры.ИмяФайлаЛога));
		Иначе
			ВызватьИсключение НСтр("ru = 'При импорте в 1C:EDT возникли ошибки. Включите логирование и повторите операцию для анализа ошибок.'");
		КонецЕсли;
	КонецЕсли;
	
	// Копируем файл дампа в каталог проекта EDT чтобы был доступен для формирования частичной выгрузки
	ИмяФайлаИсточника = Параметры.КаталогВременныхФайлов + "ConfigDumpInfo.xml";
	ИмяФайлаПриемника = Параметры.КаталогФайловПроекта + "ConfigDumpInfo.xml";
	Файл = Новый Файл(ИмяФайлаИсточника);
	Если Файл.Существует() Тогда
		КопироватьФайл(ИмяФайлаИсточника, ИмяФайлаПриемника);
	КонецЕсли;
		
КонецПроцедуры

// Получает список версий платформы поддерживаемых в EDT
// 
// Параметры:
//	ВерсияEDT - Строка - номер версии EDT;
//	
// Возвращаемое значение:
// 	Массив из Строка - Список версий поддерживаемых в EDT
//
Функция ПолучитьСписокВерсийПлатформыEDT(ВерсияEDT) Экспорт
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	ИмяФайлаВывода = ПолучитьИмяВременногоФайла("txt");
	
	СтрокаКоманды = "ring edt platform-versions > ""%ИмяФайлаВывода%""";
	
	УстановитьВерсиюEDT(СтрокаКоманды, ВерсияEDT);
	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ИмяФайлаВывода%", ИмяФайлаВывода);
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "")
		+ СтрокаКоманды, КаталогВременныхФайлов(), Истина, КодВозврата);
	
	ФайлВывода = Новый ТекстовыйДокумент();
	ФайлВывода.Прочитать(ИмяФайлаВывода, КодировкаТекста.Системная);
	
	Версии = СтрРазделить(ФайлВывода.ПолучитьТекст(), ",");
	Индекс = Версии.Количество();
	Пока Индекс > 0 Цикл
		Индекс = Индекс -1;
		Версия = СокрЛП(Версии[Индекс]);
		Квалификаторы = СтрРазделить(Версия, ".");
		Если Квалификаторы.Количество() <> 3 Тогда
			Версии.Удалить(Индекс);
		Иначе
			Версии[Индекс] = Версия;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Версии;
	
КонецФункции

// Определяет количестов установленных версий EDT. Если Истина,
// при работе с ring необходимо указать номер версии. 
// 
// Параметры:
//	ВерсияEDT - Строка - номер версии EDT;
//	
// Возвращаемое значение:
// 	Булево - признак необходимости указания версии EDT.
//
Функция НеобходимоЗаполнитьВерсиюEDT() Экспорт
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	ИмяФайлаВывода = ПолучитьИмяВременногоФайла("txt");
	
	СтрокаКоманды = "ring help modules > ""%ИмяФайлаВывода%""";
	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ИмяФайлаВывода%", ИмяФайлаВывода);
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "")
		+ СтрокаКоманды, КаталогВременныхФайлов(), Истина, КодВозврата);
	
	ФайлВывода = Новый ТекстовыйДокумент();
	ФайлВывода.Прочитать(ИмяФайлаВывода, КодировкаТекста.Системная);
	
	Возврат СтрЧислоВхождений(ФайлВывода.ПолучитьТекст(), "edt@") > 1;
	
КонецФункции

// Получает информацию о серверном репозитории или возвращает текст ошибки доступа
// 
// Параметры:
//	АдресРепозиторияGit - Строка - адрес репозитория Git на сервере
//	
// Возвращаемое значение:
// 	Структура - Результат запроса информации с сервера, содержит:
// 	* Успешно - Булево - Истина, если результат запроса к серверу был успешный
// 	* ТекстОшибки - Строка - текст ошибки если запрос к серверу был не успешный
// 	* Ветки - Массив из Строка - список имен веток доступных на сервере
//
Функция ПолучитьИнформациюРепозиторияGitНаСервере(АдресРепозиторияGit) Экспорт
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	ИмяФайлаВывода = ПолучитьИмяВременногоФайла("txt");
	
	СтрокаКоманды = "git ls-remote --heads %АдресРепозиторияGit%> ""%ИмяФайлаВывода%"" 2>&1";
	
	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ИмяФайлаВывода%", ИмяФайлаВывода);
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%АдресРепозиторияGit%", АдресРепозиторияGit);
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "")
		+ СтрокаКоманды, КаталогВременныхФайлов(), Истина, КодВозврата);
	
	Результат = Новый Структура("Успешно, ТекстОшибки, Ветки", Ложь, "", Новый Массив());
	
	ФайлВывода = Новый ТекстовыйДокумент();
	ФайлВывода.Прочитать(ИмяФайлаВывода, КодировкаТекста.UTF8);
	
	Если КодВозврата = 0 Тогда
		Результат.Успешно = Истина;
		Для НомерСтроки = 1 По ФайлВывода.КоличествоСтрок() Цикл
			ИмяВетки = СокрЛП(ФайлВывода.ПолучитьСтроку(НомерСтроки));
			ПозицияТаб = СтрНайти(ИмяВетки, Символы.Таб);
			Если ПозицияТаб > 0 Тогда
				ИмяВетки = СокрЛП(Сред(ИмяВетки, ПозицияТаб + 1));
			КонецЕсли;
			Если СтрНачинаетсяС(ИмяВетки, "refs/heads/") Тогда
				ИмяВетки = Сред(ИмяВетки, 12);
			КонецЕсли;
			Результат.Ветки.Добавить(ИмяВетки);
		КонецЦикла;
		
	Иначе
		Результат.Успешно = Ложь;
		Результат.ТекстОшибки = ФайлВывода.ПолучитьТекст();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Считывает структуру выгрузки версии из файлов
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - Ссылка на версию хранилища
// 	Параметры - Структура - Параметры запуска
Процедура ПрочитатьСтруктуруВыгрузкиВерсии(ВерсияХранилища, Параметры) Экспорт
	
	Если Параметры.Состояние <> Перечисления.СостоянияВерсии.ЗагрузкаМетаданных Тогда
		Возврат;
	КонецЕсли;
	
	ДлительныеОперации.СообщитьПрогресс(0, НСтр("ru = 'Чтение общего количества файлов';"));
	
	ТаблицаИндексов = СозадатьТаблицуИндексов();
	ТаблицаИндексов.Индексы.Добавить("ХешПолногоИмени, Уровень, ПолноеИмя");
	ТаблицаИндексов.Колонки.Добавить("ПодчиненныеОбъекты");
	
	КвалифицированныеИменаДочернихОбъектов = ОбщегоНазначенияПовтИсп.КвалифицированныеИменаДочернихОбъектов();

	КвалифицированныеОбъекты = Новый Соответствие;
	Для каждого КлючИЗначение Из КвалифицированныеИменаДочернихОбъектов Цикл
		КвалифицированныеОбъекты.Вставить(КлючИЗначение.Значение, КлючИЗначение.Ключ);
	КонецЦикла;
	
	КоличествоОбработано = ТаблицаИндексов.Количество();
	
	ИмяФайлаИндексов = Параметры.КаталогФайловПроекта + "DumpFilesIndex.txt";
	
	Файлы = НайтиФайлы(Параметры.КаталогФайловПроекта, "*", Истина);
	
	КоличествоВсего = Файлы.Количество();
	
	НачалоЗагрузки = ТекущаяДатаСеанса();
	
	ДлительныеОперации.СообщитьПрогресс(100 * КоличествоОбработано
		/ ?(КоличествоВсего = 0, 1, КоличествоВсего), СтрШаблон("Загрузка метаданных: %1 из %2", КоличествоОбработано, КоличествоВсего));
	
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ПолноеИмя = ИмяФайлаИндексов Тогда
			Продолжить;
		КонецЕсли;
		
		Если Файл.ЭтоФайл() Тогда
			ПолноеИмя = СтрЗаменить(Файл.ПолноеИмя, Параметры.КаталогФайловПроекта, "");

			Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
			Хеширование.Добавить(ПолноеИмя);
			ХешПолногоИмени = Строка(Хеширование.ХешСумма);
			ХешПолногоИмени = СтрЗаменить(ХешПолногоИмени, " ", "");
			Пути = СтрРазделить(ПолноеИмя, ПолучитьРазделительПути());
			Отбор = Новый Структура;
			Отбор.Вставить("ХешПолногоИмени", ХешПолногоИмени);
			Отбор.Вставить("ПолноеИмя", ПолноеИмя);
			Отбор.Вставить("Уровень", Пути.Количество());

			НайденныеСтроки = ТаблицаИндексов.НайтиСтроки(Отбор);
			Если НайденныеСтроки.Количество() = 0 Тогда

				ПодчиненныеОбъекты = Новый Массив();

				Если СтрЗаканчиваетсяНа(ПолноеИмя, ".mdo") Тогда
					Отбор.Вставить("ФайлПолноеИмя", Файл.ПолноеИмя);
					ПрочитатьСвойстваОбъектаМетаданныхEDT(Отбор, КвалифицированныеОбъекты, ПодчиненныеОбъекты);
				КонецЕсли;

				НоваяСтрока = ТаблицаИндексов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Отбор);
				НоваяСтрока.ПодчиненныеОбъекты = ПодчиненныеОбъекты;
			КонецЕсли;

		КонецЕсли;

		КоличествоОбработано = КоличествоОбработано + 1;

		Если (КоличествоОбработано % 100) = 0 И Файл.ЭтоФайл() Тогда
			Время = Окр((ТекущаяДатаСеанса() - НачалоЗагрузки) / 60, 2);
			Если Время = 0 Тогда
				Время = 1;
			КонецЕсли;
			Скорость = КоличествоОбработано / Время;
			Осталось = Окр((КоличествоВсего - КоличествоОбработано) / (Скорость / 60));
			ОсталосьВремени = Дата(1, 1, 1, 0, 0, 0) + ?(Осталось > 0, Осталось, 0);
			ШаблонСообщения = "Загрузка метаданных: %1 из %2. Скорость %3 шт/мин. Уровень %4. Осталось: %5";
			ДлительныеОперации.СообщитьПрогресс(100 * КоличествоОбработано
				/ ?(КоличествоВсего = 0, 1, КоличествоВсего), 
				СтрШаблон(ШаблонСообщения, 
					КоличествоОбработано, 
					КоличествоВсего, 
					Формат(Скорость, "ЧДЦ=2; ЧГ=0"), 
					Отбор.Уровень, 
					Строка(ДеньГода(ОсталосьВремени) - 1) + Формат(ОсталосьВремени, "ДФ=' ''дн.'' HH:mm:ss'")));
		КонецЕсли;
	КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(0, "Расчет идентификаторов подчиненных объектов");
	
	КоличествоОбработано = 0;
	
	КоличествоВсего = ТаблицаИндексов.Количество();
	
	Для Каждого СтрокаТЧ Из ТаблицаИндексов Цикл
		Если ПустаяСтрока(СтрокаТЧ.UUID) Тогда
			СтрокаТЧ.UUID = СформироватьUUIDПоРодителюEDT(СтрокаТЧ.ПолноеИмя, ТаблицаИндексов);
		КонецЕсли;
		
		КоличествоОбработано = КоличествоОбработано + 1;

		Если (КоличествоОбработано % 100) = 0 Тогда
			Время = Окр((ТекущаяДатаСеанса() - НачалоЗагрузки) / 60, 2);
			Если Время = 0 Тогда
				Время = 1;
			КонецЕсли;
			Скорость = КоличествоОбработано / Время;
			Осталось = Окр((КоличествоВсего - КоличествоОбработано) / (Скорость / 60));
			ОсталосьВремени = Дата(1, 1, 1, 0, 0, 0) + ?(Осталось > 0, Осталось, 0);
			ШаблонСообщения = "Расчет идентификаторов подчиненных объектов: %1 из %2. Скорость %3 шт/мин. Уровень %4. Осталось: %5";
			ДлительныеОперации.СообщитьПрогресс(100 * КоличествоОбработано
				/ ?(КоличествоВсего = 0, 1, КоличествоВсего), 
				СтрШаблон(ШаблонСообщения, 
					КоличествоОбработано, 
					КоличествоВсего, 
					Формат(Скорость, "ЧДЦ=2; ЧГ=0"), 
					Отбор.Уровень, 
					Строка(ДеньГода(ОсталосьВремени) - 1) + Формат(ОсталосьВремени, "ДФ=' ''дн.'' HH:mm:ss'")));
		КонецЕсли;
	КонецЦикла;
	
	ДлительныеОперации.СообщитьПрогресс(100, "Запись файла индексов");
	
	ЗаписатьТаблицуИндексов(ТаблицаИндексов, ИмяФайлаИндексов);
	
	ОбъектВерсии = ВерсияХранилища.ПолучитьОбъект();
	ОбъектВерсии.КоличествоМетаданных = ТаблицаИндексов.Количество();
	ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены;
	ОбъектВерсии.Записать();
	
	Параметры.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены;
	
	Если Параметры.МинимальноеКоличествоМетаданных > 0 
		И (НЕ Параметры.ВыгружатьИзменения 
		ИЛИ НЕ Параметры.ВыгрузкаИзменений) Тогда
		КоличествоМетаданных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВерсияХранилища, "КоличествоМетаданных");
		Если Параметры.МинимальноеКоличествоМетаданных > КоличествоМетаданных Тогда
		// Произошла проблема с выгрузкой - нужно переполучать версию
			ОбъектВерсии = ВерсияХранилища.ПолучитьОбъект();
			ОбъектВерсии.Состояние = Перечисления.СостоянияВерсии.ПустаяСсылка();
			ОбъектВерсии.ВыгрузкаИзменений = Ложь;
			ОбъектВерсии.Записать();
			Параметры.Состояние = Перечисления.СостоянияВерсии.ПустаяСсылка();
			
			ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Загрузка метаданных'"), 
				"Произошла проблема при выгрузке или загрузке метаданных. Минимальное количество метаданных: " 
				+ Формат(Параметры.МинимальноеКоличествоМетаданных, "ЧГ=0;")
				+ " текущее количество: " + Формат(КоличествоМетаданных, "ЧГ=0;")
				+ ". Сброс состояния, для повторного получения версии из хранилища.");

		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет поиск уникального идентификатора родителя для указанного файла и формирует UUID для файла
// 
// Параметры:
// 	ПолноеИмя - Строка - полное имя файла для которого необходимо найти UUID родителя
// 	ТаблицаИндексов - ТаблицаЗначений - Таблица индексов для поиска
// Возвращаемое значение:
// 	Строка - Уникальный идентификатор для текущего файла
Функция СформироватьUUIDПоРодителюEDT(ПолноеИмя, ТаблицаИндексов)

	UUID = ПолноеИмя;

	Пути = СтрРазделить(ПолноеИмя, ПолучитьРазделительПути());
	ПолноеИмяРодителя = "";
	Уровень = 0;
	Путь = "";
	ИмяДочернегоОбъекта = "";
	ТипДочернегоОбъекта = "";
	
	КвалифицированныеИмена = ОбщегоНазначенияПовтИсп.КвалифицированныеИменаДочернихОбъектов();

	Если Пути.Количество() > 1 И Пути[0] = "Configuration" Тогда
		Пути.Очистить();
		Пути.Добавить("Configuration");
		Пути.Добавить("Configuration");
		Уровень = Пути.Количество();
		ПолноеИмяРодителя = СтрСоединить(Пути, ПолучитьРазделительПути()) + ".mdo";
		Пути.Удалить(Пути.ВГраница());
	Иначе
		Пока Пути.Количество() > 1 Цикл
			Путь = Пути[Пути.ВГраница()];
			Пути.Удалить(Пути.ВГраница());
			Если Пути.Количество() = 2 Тогда
				Пути.Добавить(Пути[Пути.ВГраница()] + ".mdo");
				ПолноеИмяРодителя = СтрСоединить(Пути, ПолучитьРазделительПути());
				Уровень = Пути.Количество();
				Пути.Удалить(Пути.ВГраница());
				Прервать;
			ИначеЕсли Пути.Количество() > 2 И КвалифицированныеИмена.Получить(Пути[Пути.ВГраница()]) <> Неопределено Тогда
				ИмяДочернегоОбъекта = Путь;
				СегментПутиДочернегоОбъекта = Пути[Пути.ВГраница()];
				ТипДочернегоОбъекта = КвалифицированныеИмена.Получить(СегментПутиДочернегоОбъекта);
				Пути.Удалить(Пути.ВГраница());
				Пути.Добавить(Пути[Пути.ВГраница()] + ".mdo");
				ПолноеИмяРодителя = СтрСоединить(Пути, ПолучитьРазделительПути());
				Уровень = Пути.Количество();
				Пути.Удалить(Пути.ВГраница());
				Пути.Добавить(СегментПутиДочернегоОбъекта);
				Пути.Добавить(ИмяДочернегоОбъекта);
				Прервать;
			ИначеЕсли Пути[0] = "Subsystems" И Пути[Пути.ВГраница()-1] = "Subsystems" Тогда
				Пути.Добавить(Пути[Пути.ВГраница()] + ".mdo");
				ПолноеИмяРодителя = СтрСоединить(Пути, ПолучитьРазделительПути());
				Уровень = Пути.Количество();
				Пути.Удалить(Пути.ВГраница());
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ЗначениеЗаполнено(ПолноеИмяРодителя) Тогда
		Отбор = Новый Структура();
		Отбор.Вставить("ПолноеИмя", ПолноеИмяРодителя);
		Отбор.Вставить("Уровень", Уровень);
		Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
		Хеширование.Добавить(ПолноеИмяРодителя);
		ХешПолногоИмени = Строка(Хеширование.ХешСумма);
		ХешПолногоИмени = СтрЗаменить(ХешПолногоИмени, " ", "");
		Отбор.Вставить("ХешПолногоИмени", ХешПолногоИмени);

		Имя = СтрЗаменить(ПолноеИмя, СтрСоединить(Пути, ПолучитьРазделительПути())
			+ ПолучитьРазделительПути(), "");

		НайденныеСтроки = ТаблицаИндексов.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Если ЗначениеЗаполнено(ИмяДочернегоОбъекта)
					И ТипЗнч(НайденныеСтроки[0].ПодчиненныеОбъекты) = Тип("Массив") Тогда
				Для Каждого ПодчиненныйОбъект Из НайденныеСтроки[0].ПодчиненныеОбъекты Цикл
					Если ПодчиненныйОбъект.Тип = ТипДочернегоОбъекта
							И ПодчиненныйОбъект.Имя = ИмяДочернегоОбъекта Тогда
						UUID = ПодчиненныйОбъект.UUID + "_" + Имя;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ЗначениеЗаполнено(НайденныеСтроки[0].UUID) Тогда
				UUID = НайденныеСтроки[0].UUID + "_" + Имя;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат UUID;
КонецФункции

// Процедура дополняет параметры свойством "КаталогИсполняемогоФайла" для указанной версии платформы в параметрах
// 
// Параметры:
// 	Параметры - Структура - параметры в которые вставляется каталог исполняемого файла
Процедура ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры) Экспорт

	Параметры.Вставить("КаталогИсполняемогоФайла", ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогПрограммы()));
	ПутьКВерсиямПлатформыНаСервере = Константы.ПутьКВерсиямПлатформыНаСервере.Получить();
	Если ЗначениеЗаполнено(ПутьКВерсиямПлатформыНаСервере) Тогда

		Если НЕ Параметры.Свойство("ВерсияПлатформы")
				ИЛИ НЕ ЗначениеЗаполнено(Параметры.ВерсияПлатформы) Тогда
			СисИнфо = Новый СистемнаяИнформация;
			Параметры.Вставить("ВерсияПлатформы", СисИнфо.ВерсияПриложения);
		КонецЕсли;
		ПутьКВерсиямПлатформыНаСервере = СтрЗаменить(ПутьКВерсиямПлатформыНаСервере, "%ВерсияПлатформы%", Параметры.ВерсияПлатформы);
		Файл = Новый Файл(ПутьКВерсиямПлатформыНаСервере);
		Если Файл.Существует() Тогда
			Параметры.Вставить("КаталогИсполняемогоФайла", ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПутьКВерсиямПлатформыНаСервере));
		Иначе
			ТекстОшибки = НСтр("ru = 'Не найдена указанная версия платформы: %ВерсияПлатформы% на сервере: %ИмяСервера%.
				|Путь к 1С:Конфигуратору на сервере: %Путь%.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВерсияПлатформы%", Параметры.ВерсияПлатформы);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИмяСервера%", ИмяКомпьютера());
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Путь%", ПутьКВерсиямПлатформыНаСервере);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Дополняет "Параметры" подключением к хранилищу
// Параметры:
//  Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище
//  Параметры - Структура - структура, которая будет дополнена параметрами подключения к хранилищу  
Процедура ПараметрыПодключенияКХранилищу(Знач Хранилище, Параметры) Экспорт

	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;

	ИменаРеквизитов = Новый Структура("АдресХранилища, ВерсияПлатформы, ИмяПользователяХранилища, 
		| ПарольПользователяХранилища", "Адрес", "ВерсияПлатформы");
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);

	Если Параметры.Свойство("КопияХранилища") Тогда

		РеквизитыКопии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.КопияХранилища, "Адрес, ИмяПользователяХранилища, ПарольПользователяХранилища");
		Реквизиты.АдресХранилища = РеквизитыКопии.Адрес;
		Если ЗначениеЗаполнено(РеквизитыКопии.ИмяПользователяХранилища) Тогда

			Реквизиты.ИмяПользователяХранилища = РеквизитыКопии.ИмяПользователяХранилища;
			Реквизиты.ПарольПользователяХранилища = РеквизитыКопии.ПарольПользователяХранилища;

		КонецЕсли;

	КонецЕсли;

	Параметры.Вставить("АдресХранилища", Реквизиты.АдресХранилища);

	// Получаем каталог исполняемого файла
	Параметры.Вставить("ВерсияПлатформы", Реквизиты.ВерсияПлатформы);

	ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);

	Параметры.Вставить("ИмяПользователяХранилища", Реквизиты.ИмяПользователяХранилища);
	Параметры.Вставить("ПарольПользователяХранилища", Реквизиты.ПарольПользователяХранилища);

КонецПроцедуры

// Дополняет "Параметры" для работы с расширением конфигурации
// 
// Параметры:
//  Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище
//  Параметры - Структура - структура, которая будет дополнена параметрами расширения конфигурации  
Процедура ПараметрыРасширения(Знач Хранилище, Параметры) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;

	ИменаРеквизитов = Новый Структура("ТипХранилища,БазовыйПроект,ИмяРасширения");
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);
	
	Параметры.Вставить("ТипХранилища", Реквизиты.ТипХранилища);
	Если ТипЗнч(Реквизиты.БазовыйПроект) = Тип("СправочникСсылка.ХранилищаКонфигураций") 
		И ЗначениеЗаполнено(Реквизиты.БазовыйПроект) Тогда
		ИмяПроектаEDT = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.БазовыйПроект, "ИмяПроектаEDT");
		СегментыПути = СтрРазделить(ИмяПроектаEDT, ПолучитьРазделительПути());
		Если СегментыПути.Количество() > 0 Тогда
			ИмяПроектаEDT = СегментыПути[СегментыПути.ВГраница()];
		КонецЕсли;
		Параметры.Вставить("БазовыйПроект", ИмяПроектаEDT);
	Иначе
		Параметры.Вставить("БазовыйПроект", Строка(Реквизиты.БазовыйПроект));
	КонецЕсли;
	Параметры.Вставить("ИмяРасширения", Реквизиты.ИмяРасширения);
	Параметры.Вставить("КаталогПустогоРасширения", Параметры.КаталогВременныхФайлов + "empty_ext" + ПолучитьРазделительПути());
	
КонецПроцедуры

// Выполняет инициализацию хранилища в Git
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище, по которому выполняются коммиты
// 	ЛогОперации - ТекстовыйДокумент - документ в который будет прочитан лог выполенения операции
// 	Клонировать - Булево - клонировать с сервера существующий репозиторий
// 	СоздатьВетку - Булево - Создать новую ветку от основной ветки серверного репозитория
Процедура СоздатьРепозиторийGit(Хранилище, ЛогОперации, Клонировать = Ложь, СоздатьВетку = Ложь) Экспорт

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	ИменаРеквизитов = Новый Структура("ВерсияВGit, Код, ЛокальныйКаталогGit, КаталогВыгрузкиВерсий, АдресРепозиторияGit, 
		| ИмяПроектаEDT, ПользовательСервераGit, ПарольСервераGit, ИмяВетки", 
		"ВерсияВGit", "ВерсияВGit.Код");
	РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);
	
	СоздатьКаталогиВИерерахии(РеквизитыХранилища.КаталогВыгрузкиВерсий);
	СоздатьКаталогиВИерерахии(РеквизитыХранилища.ЛокальныйКаталогGit);
	
	КаталогВыгрузкиВРепозитории = КаталогФайловПроектаВРепозитории(РеквизитыХранилища);
	
	ФайлЛога = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.КаталогВыгрузкиВерсий)
		+ "git_log_init" + ".txt";
	ФайлКомандыGit = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.КаталогВыгрузкиВерсий)
		+ "git_command_init" + ?(ЭтоWindowsСервер, ".bat", ".sh");
	
	Параметры = Новый Структура("ИмяФайлаЛога", ФайлЛога);
	ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Начало инициализации Git репозитория'"));
	
	ФайлКоманды = Новый ТекстовыйДокумент;

	Если ЭтоWindowsСервер Тогда
		ТекстКоманды = "chcp 65001";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	Иначе
		ТекстКоманды = "#!/bin/bash";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	КонецЕсли;

	ТекстКоманды = ?(ЭтоWindowsСервер, "set ", "") + "LOGFILE=""%ФайлЛога%""";
	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ФайлЛога);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	Если ЭтоWindowsСервер Тогда
		ВыводЛога = " >> %LOGFILE% 2>&1";
	Иначе
		ВыводЛога = " >> $LOGFILE 2>&1";
	КонецЕсли;
	
	Если ЭтоWindowsСервер Тогда
		ТекстКомандыУстановкиКаталога = "cd /D ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	Иначе
		ТекстКомандыУстановкиКаталога = "cd ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	КонецЕсли;
	ТекстКомандыУстановкиКаталога = СтрЗаменить(ТекстКомандыУстановкиКаталога, "%ЛокальныйКаталогGit%", РеквизитыХранилища.ЛокальныйКаталогGit);

	ФайлКоманды.ДобавитьСтроку(ТекстКомандыУстановкиКаталога);

	// Инициализация репозитория
	АдресРепозиторияGit = РеквизитыХранилища.АдресРепозиторияGit;
	ПозицияРазделителя = СтрНайти(АдресРепозиторияGit, "://");
	Если ПозицияРазделителя > 0 Тогда
		АдресРепозиторияGit = Лев(АдресРепозиторияGit, ПозицияРазделителя + 2)
			+ РеквизитыХранилища.ПользовательСервераGit + ":" + РеквизитыХранилища.ПарольСервераGit + "@" + Прав(
				АдресРепозиторияGit, СтрДлина(АдресРепозиторияGit) - ПозицияРазделителя - 2);
	КонецЕсли;
	
	Если Клонировать И Не СоздатьВетку И ЗначениеЗаполнено(АдресРепозиторияGit) Тогда
		ТекстКоманды = "git clone -b ""%ИмяВетки%"" %АдресРепозиторияGit% ." + ВыводЛога;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%АдресРепозиторияGit%", АдресРепозиторияGit);
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяВетки%", РеквизитыХранилища.ИмяВетки);
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ИначеЕсли Клонировать И СоздатьВетку И ЗначениеЗаполнено(АдресРепозиторияGit) Тогда
		ТекстКоманды = "git clone %АдресРепозиторияGit% ." + ВыводЛога;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%АдресРепозиторияGit%", АдресРепозиторияGit);
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		ТекстКоманды = "git checkout -b ""%ИмяВетки%""" + ВыводЛога;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяВетки%", РеквизитыХранилища.ИмяВетки);
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	Иначе
		ТекстКоманды = "git init" + ВыводЛога;
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		Если ЗначениеЗаполнено(АдресРепозиторияGit) Тогда
			ТекстКоманды = "git remote add origin %АдресРепозиторияGit%" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%АдресРепозиторияGit%", АдресРепозиторияGit);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			ТекстКоманды = "git fetch origin" + ВыводЛога;
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЕсли;
	КонецЕсли;
	

	// Делаем пути к файлам русскоязычными
	ТекстКоманды = "git config --local core.quotepath false" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// кодировка по умолчанию при просмотре
	ТекстКоманды = "git config --local gui.encoding utf-8" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// Кодировка сообщений в коммитах
	ТекстКоманды = "git config --local i18n.commitEncoding utf-8" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// Устанавливаем количество переименованных файлов = 0, т.к. все переименования мы делаем вручную
	ТекстКоманды = "git config --local diff.renameLimit 1" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// Отключаем автоопределение переименования, переименования устанавливаем вручную
	ТекстКоманды = "git config --local diff.renames false" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	
	// Настраиваем символы окончания строк
	Если ЭтоWindowsСервер Тогда
		ТекстКоманды = "git config --local core.autocrlf true" + ВыводЛога;
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	Иначе
		ТекстКоманды = "git config --local core.autocrlf input" + ВыводЛога;
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	КонецЕсли;
	ТекстКоманды = "git config --local core.safecrlf warn" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = "git status" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	Если ЭтоWindowsСервер Тогда
		ФайлКоманды.Записать(ФайлКомандыGit, "CESU-8");
	Иначе
		ФайлКоманды.Записать(ФайлКомандыGit, "CESU-8", Символы.ПС);
	КонецЕсли;

	ЗапуститьПриложение(?(ЭтоWindowsСервер, "", "bash ")
		+ ФайлКомандыGit, РеквизитыХранилища.ЛокальныйКаталогGit, Истина);
	
	СоздатьКаталогиВИерерахии(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit) + КаталогВыгрузкиВРепозитории);
	
	ИмяФайлаИсключений = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit)
		+ ".gitignore";
	ЕстьИсключениеИндекса = Ложь;
	ЕстьИсключениеДампа = Ложь;

	Файл = Новый Файл(ИмяФайлаИсключений);
	ФайлИсключений = Новый ТекстовыйДокумент;
	Если Файл.Существует() Тогда
		ФайлИсключений.Прочитать(ИмяФайлаИсключений);
	КонецЕсли;
	Для Индекс = 1 По ФайлИсключений.КоличествоСтрок() Цикл
		СтрокаФайла = ФайлИсключений.ПолучитьСтроку(Индекс);
		Если НЕ ЕстьИсключениеИндекса И СтрокаФайла = "DumpFilesIndex.txt" Тогда
			ЕстьИсключениеИндекса = Истина;
		КонецЕсли;
		Если НЕ ЕстьИсключениеДампа И СтрокаФайла = "ConfigDumpInfo.xml" Тогда
			ЕстьИсключениеДампа = Истина;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ЕстьИсключениеИндекса Тогда
		ФайлИсключений.ДобавитьСтроку("DumpFilesIndex.txt");
	КонецЕсли;
	Если НЕ ЕстьИсключениеДампа Тогда
		ФайлИсключений.ДобавитьСтроку("ConfigDumpInfo.xml");
	КонецЕсли;
	
	Если НЕ ЕстьИсключениеИндекса ИЛИ НЕ ЕстьИсключениеДампа Тогда
		ФайлИсключений.Записать(ИмяФайлаИсключений, "CESU-8");
	КонецЕсли;
	
	// В атрибуты добавляем настройки для бинарных файлов
	ИмяФайлаАтрибутов = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit) 
		+ ".gitattributes";
	Файл = Новый Файл(ИмяФайлаАтрибутов);
	Если НЕ Файл.Существует() Тогда
		ФайлАтрибутов = Новый ТекстовыйДокумент();
		ФайлАтрибутов.ДобавитьСтроку("# Binary file extensions that should not be modified.");
		ФайлАтрибутов.ДобавитьСтроку("*.bin binary");
		ФайлАтрибутов.ДобавитьСтроку("*.axdt binary");
		ФайлАтрибутов.ДобавитьСтроку("*.addin binary");
		ФайлАтрибутов.Записать(ИмяФайлаАтрибутов, "CESU-8");
	КонецЕсли;
	
	// Добавляем изменения в индекс и проверяем статус
	СтрокаКоманды = "git add --all ./ >> ""%ФайлЛога%"" 2>&1 && git status >> ""%ФайлЛога%"" 2>&1";
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлЛога%", ФайлЛога);
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "")
		+ СтрокаКоманды, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
	
	Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Инициализация Git репозитория выполнена с ошибкой.'"));
	КонецЕсли;
	
	ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Окончание инициализации Git репозитория'"));
	
	Файл = Новый Файл(ФайлЛога);
	Если Файл.Существует() Тогда
		ЛогОперации.Прочитать(ФайлЛога, КодировкаТекста.UTF8);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает адрес серверного хранилища в Git в локальный репозиторий
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - Ссылка на хранилище, по которому выполняются коммиты
// 	ЛогОперации - ТабличныйДокумент - документ в который будет прочитан лог выполенения операции
// 	ВеткаСуществует - Булево - серверная ветка существует
Процедура УстановитьАдресРепозиторияGit(Хранилище, ЛогОперации, ВеткаСуществует) Экспорт

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	ИменаРеквизитов = Новый Структура("ВерсияВGit, Код, ЛокальныйКаталогGit, КаталогВыгрузкиВерсий, АдресРепозиторияGit, ПользовательСервераGit, ПарольСервераGit, ИмяВетки", "ВерсияВGit", "ВерсияВGit.Код");
	РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);

	ФайлЛога = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.КаталогВыгрузкиВерсий)
		+ "git_log_add_remote" + ".txt";
	ФайлКомандыGit = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.КаталогВыгрузкиВерсий)
		+ "git_command_add_remote" + ?(ЭтоWindowsСервер, ".bat", ".sh");
	
	Параметры = Новый Структура("ИмяФайлаЛога", ФайлЛога);
	ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Начало добавления адреса Git репозитория'"));
	
	ФайлКоманды = Новый ТекстовыйДокумент;

	Если ЭтоWindowsСервер Тогда
		ТекстКоманды = "chcp 65001";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	Иначе
		ТекстКоманды = "#!/bin/bash";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	КонецЕсли;

	ТекстКоманды = ?(ЭтоWindowsСервер, "set ", "") + "LOGFILE=""%ФайлЛога%""";
	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ФайлЛога);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	Если ЭтоWindowsСервер Тогда
		ВыводЛога = " >> %LOGFILE% 2>&1";
	Иначе
		ВыводЛога = " >> $LOGFILE 2>&1";
	КонецЕсли;
	
	Если ЭтоWindowsСервер Тогда
		ТекстКомандыУстановкиКаталога = "cd /D ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	Иначе
		ТекстКомандыУстановкиКаталога = "cd ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	КонецЕсли;
	ТекстКомандыУстановкиКаталога = СтрЗаменить(ТекстКомандыУстановкиКаталога, "%ЛокальныйКаталогGit%", РеквизитыХранилища.ЛокальныйКаталогGit);

	ФайлКоманды.ДобавитьСтроку(ТекстКомандыУстановкиКаталога);

	Если ЭтоWindowsСервер Тогда
		ТекстКоманды = "git remote remove origin" + ВыводЛога + " || set ERRORLEVEL=0";
	Иначе
		ТекстКоманды = "git remote remove origin" + ВыводЛога + " || true";
	КонецЕсли;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// Инициализация репозитория
	АдресРепозиторияGit = РеквизитыХранилища.АдресРепозиторияGit;
	Если ЗначениеЗаполнено(АдресРепозиторияGit) Тогда
		ТекстКоманды = "git remote add origin %АдресРепозиторияGit%" + ВыводЛога;
		ПозицияРазделителя = СтрНайти(АдресРепозиторияGit, "://");
		Если ПозицияРазделителя > 0 и ЗначениеЗаполнено(РеквизитыХранилища.ПользовательСервераGit) Тогда
			АдресРепозиторияGit = Лев(АдресРепозиторияGit, ПозицияРазделителя + 2)
				+ РеквизитыХранилища.ПользовательСервераGit + ":"
				+ РеквизитыХранилища.ПарольСервераGit + "@"
				+ Прав(АдресРепозиторияGit, СтрДлина(АдресРепозиторияGit)
				- ПозицияРазделителя - 2);
		КонецЕсли;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%АдресРепозиторияGit%", АдресРепозиторияGit);
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		ТекстКоманды = "git fetch origin" + ВыводЛога;
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		Если ВеткаСуществует Тогда
			ТекстКоманды = "git branch --set-upstream-to=origin/%ИмяВетки% %ИмяВетки%" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяВетки%", РеквизитыХранилища.ИмяВетки);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЕсли;
	КонецЕсли;

	Если ЭтоWindowsСервер Тогда
		ФайлКоманды.Записать(ФайлКомандыGit, "CESU-8");
	Иначе
		ФайлКоманды.Записать(ФайлКомандыGit, "CESU-8", Символы.ПС);
	КонецЕсли;

	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "", "bash ")
		+ ФайлКомандыGit, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);

	Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Установка адреса Git-сервера выполнена с ошибкой.'"));
	КонецЕсли;
	
	ДобавитьЗаписьВЛог(Параметры, НСтр("ru = 'Окончание добавления адреса Git репозитория'"));
	
	Файл = Новый Файл(ФайлЛога);
	Если Файл.Существует() Тогда
		ЛогОперации.Прочитать(ФайлЛога, КодировкаТекста.UTF8);
	КонецЕсли;

КонецПроцедуры

// Запускает удаление временных данных версии в фоне
// 
// Параметры:
// 	Версия - СправочникСсылка.ВерсииХранилища - ссылка на версию для запуска удаления
Процедура УдалитьФайлыВерсииВФоне(Версия) Экспорт
	
	КлючЗадания = Строка(Версия.УникальныйИдентификатор());
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.УдалитьФайлыВерсии");
	
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если МассивФоновыхЗаданий.Количество() = 0 Тогда
		
		ЗаданияНаименование = НСтр("ru = 'Удаление файлов версии: %Версия%'");
		ЗаданияНаименование = СтрЗаменить(ЗаданияНаименование, "%Версия%", Строка(Версия));
		
		ПараметрыЗадания = Новый Массив();
		ПараметрыЗадания.Добавить(Версия);
		
		ФоновыеЗадания.Выполнить("КонвертацияХранилища.УдалитьФайлыВерсии", ПараметрыЗадания, КлючЗадания, Лев(ЗаданияНаименование, 120)); 
		
		
	КонецЕсли;
КонецПроцедуры

// Процедура - Удалить файлы версии
//
// Параметры:
//  ВерсияХранилища - СправочникСсылка.ВерсииХранилища - ссылка на текущую версию в проекте
//
Процедура УдалитьФайлыВерсии(ВерсияХранилища) Экспорт

	ИменаРеквизитов = Новый Структура("УдалятьВременныеДанныеВерсииПослеКоммита, Состояние, 
		| КаталогВременныхФайлов", "Владелец.УдалятьВременныеДанныеВерсииПослеКоммита");
	РеквизитыВерсии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВерсияХранилища, ИменаРеквизитов);

	Если РеквизитыВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена
			И РеквизитыВерсии.УдалятьВременныеДанныеВерсииПослеКоммита Тогда

		Объект = ВерсияХранилища.ПолучитьОбъект();

		Если ЗначениеЗаполнено(РеквизитыВерсии.КаталогВременныхФайлов) Тогда

			Попытка

				УдалитьФайлы(РеквизитыВерсии.КаталогВременныхФайлов);
				Объект.КаталогВременныхФайлов = "";

			Исключение

			КонецПопытки;

		КонецЕсли;
		Объект.ОбменДанными.Загрузка = Истина;
		Объект.Записать();

	КонецЕсли;

КонецПроцедуры

// Версии в которых хранилище является источником - зависли
//
// Параметры:
//  Источник	 - СправочникСсылка.ХранилищаКонфигураций, СправочникСсылка.КопииХранилищКонфигурации - Ссылка на хранилище, по которому выполняется проверка
//
Процедура ПроверитьПолученныеВерсии(Источник) Экспорт

	Если ТипЗнч(Источник) = Тип("СправочникСсылка.КопииХранилищКонфигурации") Тогда
		Хранилище = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "Владелец");
	Иначе
		Хранилище = Источник;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ВерсииХранилища.Ссылка
		|ИЗ
		|	Справочник.ВерсииХранилища КАК ВерсииХранилища
		|ГДЕ
		|	ВерсииХранилища.Владелец = &Хранилище
		|	И (ВерсииХранилища.Источник = &Источник
		|			ИЛИ ВерсииХранилища.Источник = НЕОПРЕДЕЛЕНО
		|			ИЛИ ВерсииХранилища.Источник = ЗНАЧЕНИЕ(Справочник.ХранилищаКонфигураций.ПустаяСсылка)
		|			ИЛИ ВерсииХранилища.Источник = ЗНАЧЕНИЕ(Справочник.КопииХранилищКонфигурации.ПустаяСсылка))
		|	И ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.ПолучениеВерсии)";

	Запрос.УстановитьПараметр("Хранилище", Хранилище);
	Запрос.УстановитьПараметр("Источник", Источник);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Объект.Состояние = Перечисления.СостоянияВерсии.ПустаяСсылка();
		Объект.КаталогВременныхФайлов = "";
		Объект.Записать();

	КонецЦикла;

КонецПроцедуры

// Проверяет нужно ли запускать в фоне формирование файлов коммита по версиям
// 
// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - ссылка на хранилище
Процедура СформироватьФайлыGitНаСервере(Хранилище) Экспорт

	ИменаРеквизитов = Новый Структура("ЛокальныйКаталогGit, КаталогВыгрузкиВерсий, АдресРепозиторияGit");
	РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВерсииХранилища.Ссылка,
	|	ВерсииХранилища.Код
	|ИЗ
	|	Справочник.ВерсииХранилища КАК ВерсииХранилища
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПользователей
	|		ПО ВерсииХранилища.Пользователь = ИнформацияПользователей.Пользователь
	|		И ВерсииХранилища.Владелец = ИнформацияПользователей.Хранилище
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ОбщаяИнформацияПользователей
	|		ПО ВерсииХранилища.Пользователь = ОбщаяИнформацияПользователей.Пользователь
	|		И ОбщаяИнформацияПользователей.Хранилище = ЗНАЧЕНИЕ(Справочник.ХранилищаКонфигураций.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияПользователей КАК ИнформацияПоУмолчани
	|		ПО ИнформацияПоУмолчани.Пользователь = """"
	|ГДЕ
	|	ВерсииХранилища.Владелец = &Хранилище
	|	И ВерсииХранилища.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВерсии.МетаданныеЗагружены)
	|	И (ВерсииХранилища.Владелец.РазрешитьПомещатьАнонимноЕслиНеНайденПользователь
	|	ИЛИ ЕСТЬNULL(ИнформацияПользователей.Email, ЕСТЬNULL(ОбщаяИнформацияПользователей.Email,
	|		ЕСТЬNULL(ИнформацияПоУмолчани.Email, """"))) <> """")
	|УПОРЯДОЧИТЬ ПО
	|	ВерсииХранилища.Код";

	Запрос.УстановитьПараметр("Хранилище", Хранилище);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		ИмяФайлаКомандыGit = ИмяФайлаКомандыКоммитаВерсии(ВыборкаДетальныеЗаписи.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий, ЭтоWindowsСервер);
		ИмяФайлКомментария = ИмяФайлаКомментарияКоммитаВерсии(ВыборкаДетальныеЗаписи.Код, РеквизитыХранилища.КаталогВыгрузкиВерсий);

		ФайлКомандыGit = Новый Файл(ИмяФайлаКомандыGit);
		ФайлКомментария = Новый Файл(ИмяФайлКомментария);
		Если ФайлКомандыGit.Существует() ИЛИ ФайлКомментария.Существует() Тогда
			Продолжить;
		КонецЕсли;

		КлючЗадания = Строка(ВыборкаДетальныеЗаписи.Ссылка.УникальныйИдентификатор());

		Отбор = Новый Структура();
		Отбор.Вставить("Ключ", КлючЗадания);
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		Отбор.Вставить("ИмяМетода", "КонвертацияХранилища.СформироватьФайлыКоммитаВерсии");

		МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);

		Если МассивФоновыхЗаданий.Количество() = 0 Тогда

			ЗаданияНаименование = НСтр("ru = 'Формирование файлов коммита версии: %Версия%'");
			ЗаданияНаименование = СтрЗаменить(ЗаданияНаименование, "%Версия%", Строка(ВыборкаДетальныеЗаписи.Ссылка));

			ПараметрыЗадания = Новый Массив();
			ПараметрыЗадания.Добавить(ВыборкаДетальныеЗаписи.Ссылка);

			ФоновыеЗадания.Выполнить("КонвертацияХранилища.СформироватьФайлыКоммитаВерсии", ПараметрыЗадания, КлючЗадания, Лев(ЗаданияНаименование, 120));

		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Добавляет запись в журнал регистрации
// 
// Параметры:
// 	ИмяСобытия - Строка - имя события
// 	Уровень - Строка, УровеньЖурналаРегистрации - уровень записи журнала регистрации 
// 	ОбъектМетаданных - ОбъектМетаданных, Неопределено - объект метаданных
// 	Данные - Строка, Число, Дата, Булево, Неопределено - 
// 	Комментарий - Строка - комментарий
// 	РежимТранзакции - РежимТранзакцииЗаписиЖурналаРегистрации, Неопределено -
Процедура ДобавитьЗаписьВЖурналРегистрации(Знач ИмяСобытия,
		Знач Уровень = Неопределено, Знач ОбъектМетаданных = Неопределено,
		Знач Данные = Неопределено, Знач Комментарий = "",
		Знач РежимТранзакции = Неопределено) Экспорт

	Если ЗначениеЗаполнено(Уровень) и ТипЗнч(Уровень) = Тип("Строка") Тогда

		//@skip-warning
		Уровень = УровеньЖурналаРегистрации[Уровень];

	КонецЕсли;
	ЗаписьЖурналаРегистрации(ИмяСобытия, Уровень, ОбъектМетаданных, Данные, Комментарий, РежимТранзакции);

КонецПроцедуры

// Добавляет версию EDT в строку запуска.
// 
// Параметры:
// 	СтрокаКоманды - Строка - начальная строка запуска;
// 	ВерсияEDT - Строка - номер версии EDT.
// 
Процедура УстановитьВерсиюEDT(СтрокаКоманды, ВерсияEDT) Экспорт
	
	Если Не ЗначениеЗаполнено(ВерсияEDT) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(СтрокаКоманды, "ring edt") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "ring edt", "ring edt@" + ВерсияEDT);
	
КонецПроцедуры

#КонецОбласти

#Область ПакетныеОперации

// Создается пустая файловая база
// 
// Параметры:
// 	Параметры - Структура - Параметры создания
Процедура СоздатьФайловуюИнформационнуюБазу(Параметры) Экспорт

	Если Параметры.Свойство("СоздатьФайловуюИнформационнуюБазу")
			И Параметры.СоздатьФайловуюИнформационнуюБазу Тогда

		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало создания базы.'"));

		Файл = Новый Файл(Параметры.КаталогИБ);
		Если Файл.Существует() Тогда
			УдалитьФайлы(Параметры.КаталогИБ, "*");
		КонецЕсли;

		Если Параметры.Свойство("ИмяФайлаШаблона")
				И ЗначениеЗаполнено(Параметры.ИмяФайлаШаблона) Тогда

			Параметры.Вставить("ШаблонИБ", "/UseTemplate ""%ИмяФайлаШаблона%""");
			Параметры.ШаблонИБ = СтрЗаменить(Параметры.ШаблонИБ, "%ИмяФайлаШаблона%", Параметры.ИмяФайлаШаблона);

		Иначе

			Параметры.Вставить("ШаблонИБ", "");

		КонецЕсли;

		Если Параметры.Свойство("ДобавлятьВСписок")
				И Параметры.ДобавлятьВСписок Тогда
			Параметры.Вставить("ДобавлениеВСписок", "/AddInList ""%ИмяВСписке%""");
			Параметры.ДобавлениеВСписок = СтрЗаменить(Параметры.ДобавлениеВСписок, "%ИмяВСписке%", Параметры.КаталогИБ);
		Иначе
			Параметры.Вставить("ДобавлениеВСписок", "");
		КонецЕсли;

		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";

		СтрокаПараметров = "CREATEINFOBASE File=""%КаталогИБ%"" /DisableStartupDialogs /L ru /VL ru
			| %ДобавлениеВСписок% %ШаблонИБ%
			| /DumpResult ""%ИмяФайлаРезультатов%""";
		
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			СтрокаПараметров = СтрокаПараметров + "
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";
			СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		КонецЕсли;

		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%КаталогИБ%", Параметры.КаталогИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ШаблонИБ%", Параметры.ШаблонИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ДобавлениеВСписок%", Параметры.ДобавлениеВСписок);

		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;

		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);

		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание создания базы.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);

	КонецЕсли;

КонецПроцедуры

// Добавляет к конфигурации путое расширение.
//
Процедура ДобавитьРасширениеКонфигурации(Параметры) Экспорт
	
	Если Параметры.Свойство("ТипХранилища") И Параметры.ТипХранилища = Перечисления.ТипыХранилищаКонфигураций.Расширение Тогда
		
		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало добавления пустого расширения конфигурации.'"));
		
		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";
		
		УказыватьПустогоПользователя = ЗначениеЗаполнено(Параметры.ИмяПользователяИБ);
		
		СтрокаПараметров = "DESIGNER /WA- /DisableStartupDialogs /L ru /VL ru
			| %СтрокаСоединенияИБ% 
			| " + ?(УказыватьПустогоПользователя, "/N ""%ИмяПользователяИБ%"" ", "") + "
			| " + ?(УказыватьПустогоПользователя И ЗначениеЗаполнено(Параметры.ПарольПользователяИБ), "/P ""%ПарольПользователяИБ%"" ", "")+ "
			| /LoadConfigFromFiles ""%КаталогПустогоРасширения%"" -Extension %ИмяРасширения%
			| /DumpResult ""%ИмяФайлаРезультатов%""";
		
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			СтрокаПараметров = СтрокаПараметров + "
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";
			СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		КонецЕсли;
		
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);
		
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%СтрокаСоединенияИБ%", Параметры.СтрокаСоединенияИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяИБ%", Параметры.ИмяПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяИБ%", Параметры.ПарольПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%КаталогПустогоРасширения%", Параметры.КаталогПустогоРасширения);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяРасширения%", Параметры.ИмяРасширения);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);
		
		УдалитьФайлы(Параметры.КаталогПустогоРасширения);
		СоздатьКаталог(Параметры.КаталогПустогоРасширения);
		ТекстовыйДокумент = Справочники.ХранилищаКонфигураций.ПолучитьМакет("ПустоеРасширение");
		ТекстовыйДокумент.Записать(Параметры.КаталогПустогоРасширения + "Configuration.xml");
		
		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;
		
		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);
		
		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);
		УдалитьФайлы(Параметры.КаталогПустогоРасширения);
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание добавления пустого расширения конфигурации.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);
		
	КонецЕсли;
	
КонецПроцедуры

// Формируется ответ по версиям хранилища начиная с указанной версии
// 
// Параметры:
// 	Параметры - Структура - параметры формирования отчета
Процедура СформироватьОтчетПоВерсиямХранилища(Параметры) Экспорт

	Если Параметры.Свойство("СформироватьОтчетПоВерсиямХранилища")
			И Параметры.СформироватьОтчетПоВерсиямХранилища Тогда

		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

		ВерсияНачала = "";
		ВерсияОкончания = "";
		Если Параметры.Свойство("ВерсияНачала") Тогда
			Если ТипЗнч(Параметры.ВерсияНачала) <> Тип("Строка") Тогда
				Параметры.Вставить("ВерсияНачала", Формат(Параметры.ВерсияНачала, "ЧДЦ=; ЧГ=0"));
			КонецЕсли;
			ВерсияНачала = "-NBegin " + Параметры.ВерсияНачала;
		КонецЕсли;
		Если Параметры.Свойство("ВерсияОкончания") Тогда
			Если ТипЗнч(Параметры.ВерсияОкончания) <> Тип("Строка") Тогда
				Параметры.Вставить("ВерсияОкончания", Формат(Параметры.ВерсияОкончания, "ЧДЦ=; ЧГ=0"));
			КонецЕсли;
			ВерсияОкончания = "-NEnd " + Параметры.ВерсияОкончания;
		КонецЕсли;

		Расширение = "";
		Если Параметры.Свойство("ТипХранилища") И Параметры.ТипХранилища = Перечисления.ТипыХранилищаКонфигураций.Расширение Тогда
			Расширение = "-Extension " + Параметры.ИмяРасширения;
		КонецЕсли;
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало формирования отчета по хранилищу.'"));

		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";

		УказыватьПустогоПользователя = ЗначениеЗаполнено(Параметры.ИмяПользователяИБ);
		
		СтрокаПараметров = "DESIGNER /WA- /DisableStartupDialogs /L ru /VL ru
			| %СтрокаСоединенияИБ% 
			| " + ?(УказыватьПустогоПользователя, "/N ""%ИмяПользователяИБ%"" ", "") + "
			| " + ?(УказыватьПустогоПользователя И ЗначениеЗаполнено(Параметры.ПарольПользователяИБ), "/P ""%ПарольПользователяИБ%"" ", "")+ "
			| /ConfigurationRepositoryF ""%АдресХранилища%""
			| /ConfigurationRepositoryN ""%ИмяПользователяХранилища%""
			| " + ?(ЗначениеЗаполнено(Параметры.ПарольПользователяХранилища), "/ConfigurationRepositoryP ""%ПарольПользователяХранилища%"" ", "") + "
			| /ConfigurationRepositoryReport ""%ИмяФайлаОтчета%"" %ВерсияНачала% %ВерсияОкончания% %Расширение%
			| /DumpResult ""%ИмяФайлаРезультатов%""";
		
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			СтрокаПараметров = СтрокаПараметров + "
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";
			СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		КонецЕсли;

		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%СтрокаСоединенияИБ%", Параметры.СтрокаСоединенияИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяИБ%", Параметры.ИмяПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяИБ%", Параметры.ПарольПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%АдресХранилища%", Параметры.АдресХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяХранилища%", Параметры.ИмяПользователяХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяХранилища%", Параметры.ПарольПользователяХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаОтчета%", Параметры.ИмяФайлаОтчета);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ВерсияНачала%", ВерсияНачала);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ВерсияОкончания%", ВерсияОкончания);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%Расширение%", Расширение);

		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;

		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);

		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание формирования отчета по хранилищу.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);

		// Если выполнение операции содержит ошибки - далее не продолжаем
		Если КодВозврата <> 0 ИЛИ КодРезультата <> 0 Тогда
			Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'При формирования отчета по хранилищу конфигураций возникли ошибки. Подробнее см. файл лога:
											 |%1'"), Параметры.ИмяФайлаЛога);
			Иначе
				ВызватьИсключение НСтр("ru = 'При формирования отчета по хранилищу конфигураций возникли ошибки. Включите логирование и повторите операцию для анализа ошибок.'");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Обновление базы из хранилища на указанную версию
// 
// Параметры:
// 	Параметры - Структура - Параметры обновления
//
Процедура ОбновитьИнформационнуюБазуИзХранилища(Параметры) Экспорт

	Если Параметры.Свойство("ОбновитьИнформационнуюБазуИзХранилища")
			И Параметры.ОбновитьИнформационнуюБазуИзХранилища Тогда

		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
		Если НЕ Параметры.Свойство("ПрименитьКонфигурацию") Тогда
			Параметры.Вставить("ПрименитьКонфигурацию", Ложь);
		КонецЕсли;

		Если Параметры.Свойство("ВерсияХранилища") Тогда
			Если ТипЗнч(Параметры.ВерсияХранилища) <> Тип("Строка") Тогда
				Параметры.Вставить("ВерсияХранилища", Формат(Параметры.ВерсияХранилища, "ЧДЦ=; ЧГ=0"));
			КонецЕсли;
		Иначе
			Параметры.Вставить("ВерсияХранилища", "-1");
		КонецЕсли;

		Расширение = "";
		Если Параметры.Свойство("ТипХранилища") И Параметры.ТипХранилища = Перечисления.ТипыХранилищаКонфигураций.Расширение Тогда
			Расширение = "-Extension " + Параметры.ИмяРасширения;
		КонецЕсли;
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало обновления базы из хранилища.'"));

		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";

		УказыватьПустогоПользователя = ЗначениеЗаполнено(Параметры.ИмяПользователяИБ);
		
		СтрокаПараметров = "DESIGNER /WA- /DisableStartupDialogs /L ru /VL ru
			| %СтрокаСоединенияИБ% 
			| " + ?(УказыватьПустогоПользователя, "/N ""%ИмяПользователяИБ%"" ", "") + "
			| " + ?(УказыватьПустогоПользователя И ЗначениеЗаполнено(Параметры.ПарольПользователяИБ), "/P ""%ПарольПользователяИБ%"" ", "")+ "
			| /ConfigurationRepositoryF ""%АдресХранилища%""
			| /ConfigurationRepositoryN ""%ИмяПользователяХранилища%""
			| " + ?(ЗначениеЗаполнено(Параметры.ПарольПользователяХранилища), "/ConfigurationRepositoryP ""%ПарольПользователяХранилища%"" ", "") + "
			| /ConfigurationRepositoryUpdateCfg -force -v %ВерсияХранилища% %Расширение% "
			+ ?(Параметры.ПрименитьКонфигурацию, "/UpdateDBCfg %Расширение%", "") + "
			| /DumpResult ""%ИмяФайлаРезультатов%""";
		
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			СтрокаПараметров = СтрокаПараметров + "
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";
			СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		КонецЕсли;

		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%СтрокаСоединенияИБ%", Параметры.СтрокаСоединенияИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяИБ%", Параметры.ИмяПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяИБ%", Параметры.ПарольПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%АдресХранилища%", Параметры.АдресХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяХранилища%", Параметры.ИмяПользователяХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяХранилища%", Параметры.ПарольПользователяХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ВерсияХранилища%", Параметры.ВерсияХранилища);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%Расширение%", Расширение);

		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;

		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);

		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);
		
		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание обновления базы из хранилища.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);

		// Если выполнение операции содержит ошибки - далее не продолжаем
		Если КодВозврата <> 0 ИЛИ КодРезультата <> 0 Тогда
			Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'При обновлении ИБ из хранилища конфигураций возникли ошибки. Подробнее см. файл лога:
											 |%1'"), Параметры.ИмяФайлаЛога);
			Иначе
				ВызватьИсключение НСтр("ru = 'При обновлении ИБ из хранилища конфигураций возникли ошибки. Включите логирование и повторите операцию для анализа ошибок.'");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Выгрузка конфигурации в файлы из базы
// 
// Параметры:
// 	Параметры - Структура - параметры выгрузки
//
Процедура ВыгрузитьКонфигурациюВФайлы(Параметры) Экспорт

	Если Параметры.Свойство("ВыгрузитьКонфигурациюВФайлы")
			И Параметры.ВыгрузитьКонфигурациюВФайлы Тогда

		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало выгрузки конфигурации в файлы.'"));

		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";

		УказыватьПустогоПользователя = ЗначениеЗаполнено(Параметры.ИмяПользователяИБ);
		
		Если Параметры.Свойство("ВыгружатьИзменения") 
			И Параметры.ВыгружатьИзменения 
			И НЕ (Параметры.Свойство("ПолучитьСписокИзменений") 
			И Параметры.ПолучитьСписокИзменений) Тогда
			ДополнительныеПараметры = "-update -force -configDumpInfoForChanges ""%ФайлСостоянияПредыдущейВерсии%""";
			ДополнительныеПараметры = СтрЗаменить(ДополнительныеПараметры, "%ФайлСостоянияПредыдущейВерсии%", Параметры.ФайлСостоянияПредыдущейВерсии);
		ИначеЕсли Параметры.Свойство("ВыгружатьПоСписку") И Параметры.ВыгружатьПоСписку Тогда
			ДополнительныеПараметры = "-listFile ""%ФайлСпискаВыгрузки%""";
			ДополнительныеПараметры = СтрЗаменить(ДополнительныеПараметры, "%ФайлСпискаВыгрузки%", Параметры.ФайлСпискаВыгрузки);
		ИначеЕсли Параметры.Свойство("ВыгружатьИзменения") 
			И Параметры.ВыгружатьИзменения  
			И Параметры.Свойство("ПолучитьСписокИзменений") 
			И Параметры.ПолучитьСписокИзменений Тогда
			ДополнительныеПараметры = " -getChanges ""%ФайлИзменений%"" -configDumpInfoForChanges ""%ФайлСостоянияПредыдущейВерсии%""";
			ДополнительныеПараметры = СтрЗаменить(ДополнительныеПараметры, "%ФайлИзменений%", Параметры.ФайлИзменений);
			ДополнительныеПараметры = СтрЗаменить(ДополнительныеПараметры, "%ФайлСостоянияПредыдущейВерсии%", Параметры.ФайлСостоянияПредыдущейВерсии);
		Иначе
			ДополнительныеПараметры = "";
		КонецЕсли;
		
		Расширение = "";
		Если Параметры.Свойство("ТипХранилища") И Параметры.ТипХранилища = Перечисления.ТипыХранилищаКонфигураций.Расширение Тогда
			Расширение = "-Extension " + Параметры.ИмяРасширения;
		КонецЕсли;
		
		СтрокаПараметров = "DESIGNER /DisableStartupDialogs /L ru /VL ru
			| %СтрокаСоединенияИБ%
			| " + ?(УказыватьПустогоПользователя, "/N ""%ИмяПользователяИБ%"" ", "") + "
			| " + ?(УказыватьПустогоПользователя И ЗначениеЗаполнено(Параметры.ПарольПользователяИБ), "/P ""%ПарольПользователяИБ%"" ", "")+ "
			| /DumpConfigToFiles ""%КаталогФайловКонфигурации%"" %Расширение% %ДополнительныеПараметры%
			| /DumpResult ""%ИмяФайлаРезультатов%""";
		
		Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
			СтрокаПараметров = СтрокаПараметров + "
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";
			СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		КонецЕсли;

		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%СтрокаСоединенияИБ%", Параметры.СтрокаСоединенияИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяИБ%", Параметры.ИмяПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяИБ%", Параметры.ПарольПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%КаталогФайловКонфигурации%", Параметры.КаталогФайловКонфигурации);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%Расширение%", Расширение);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ДополнительныеПараметры%", ДополнительныеПараметры);

		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;

		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);
		
		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);

		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание выгрузки конфигурации в файлы.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);

		// Если выполнение операции содержит ошибки - далее не продолжаем
		Если КодВозврата <> 0 ИЛИ КодРезультата <> 0 Тогда
			Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'При выгрузке конфигурации в файлы возникли ошибки. Подробнее см. файл лога:
											 |%1'", Параметры.ИмяФайлаЛога));
			Иначе
				ВызватьИсключение НСтр("ru = 'При выгрузке конфигурации в файлы возникли ошибки. Включите логирование и повторите операцию для анализа ошибок'");
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Сохранение файла информации выгрузки ConfigDumpInfo.xml из ИБ без выгрузки конфигруации
// 
// Параметры:
// 	Параметры - Структура - Параметры настройки сохранения
Процедура СохранитьФайлИнформацииВыгрузки(Параметры) Экспорт
	
	Если Параметры.Свойство("СохранитьФайлИнформацииВыгрузки")
			И Параметры.СохранитьФайлИнформацииВыгрузки Тогда

		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Начало сохранения файла информации выгрузки.'"));

		СтрокаКоманды = """%КаталогИсполняемогоФайла%1cv8"" /@ ""%ФайлПараметровПакетнойОперации%""";

		УказыватьПустогоПользователя = ЗначениеЗаполнено(Параметры.ИмяПользователяИБ);
		
		СтрокаПараметров = "DESIGNER /DisableStartupDialogs /L ru /VL ru
			| %СтрокаСоединенияИБ%
			| " + ?(УказыватьПустогоПользователя, "/N ""%ИмяПользователяИБ%"" ", "") + "
			| " + ?(УказыватьПустогоПользователя И ЗначениеЗаполнено(Параметры.ПарольПользователяИБ), "/P ""%ПарольПользователяИБ%"" ", "")+ "
			| /DumpConfigToFiles ""%КаталогФайловКонфигурации%"" -configDumpInfoOnly
			| /DumpResult ""%ИмяФайлаРезультатов%""
			| /Out ""%ИмяФайлаЛога%"" -NoTruncate";

		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%КаталогИсполняемогоФайла%", Параметры.КаталогИсполняемогоФайла);
		СтрокаКоманды = СтрЗаменить(СтрокаКоманды, "%ФайлПараметровПакетнойОперации%", Параметры.ФайлПараметровПакетнойОперации);

		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаЛога%", Параметры.ИмяФайлаЛога);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%СтрокаСоединенияИБ%", Параметры.СтрокаСоединенияИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяПользователяИБ%", Параметры.ИмяПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ПарольПользователяИБ%", Параметры.ПарольПользователяИБ);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%КаталогФайловКонфигурации%", Параметры.КаталогФайловКонфигурации);
		СтрокаПараметров = СтрЗаменить(СтрокаПараметров, "%ИмяФайлаРезультатов%", Параметры.ИмяФайлаРезультатов);

		ФайлПараметров = Новый ТекстовыйДокумент;
		ФайлПараметров.УстановитьТекст(СтрокаПараметров);
		Если ЭтоWindowsСервер Тогда
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.ANSI);
		Иначе
			ФайлПараметров.Записать(Параметры.ФайлПараметровПакетнойОперации, КодировкаТекста.UTF8);
		КонецЕсли;

		КодВозврата = Неопределено;
		ЗапуститьПриложение(СтрокаКоманды, Параметры.КаталогИсполняемогоФайла, Истина, КодВозврата);
		
		КодРезультата = ПрочитатьФайлРезультата(Параметры.ИмяФайлаРезультатов);

		ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Окончание сохранения файла информации выгрузки.'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата) + ", Код результата: " + КодРезультата);

		// Если выполнение операции содержит ошибки - далее не продолжаем
		Если КодВозврата <> 0 ИЛИ КодРезультата <> 0 Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'При сохранении файла информации выгрузки возникли ошибки. Подробнее см. файл лога:
											 |%1'", Параметры.ИмяФайлаЛога));
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает версию хранилища в базу
// 
// Параметры:
// 	Параметры - Структура - параметры запуска
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - ссылка на хранилище
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - ссылка на получаемую версию
Процедура ПолучитьВерсиюВБазу(Параметры, Хранилище, ВерсияХранилища)

	КлючОперации = Строка(Хранилище.УникальныйИдентификатор()) + "_"
		+ Строка(ВерсияХранилища.УникальныйИдентификатор()) + "_1";

	Параметры.Вставить("КаталогВременныхФайлов", ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВыгрузкиВерсий)
		+ Формат(Параметры.Код, "ЧДЦ=; ЧГ=0"));

	Параметры.КаталогВременныхФайлов = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогВременныхФайлов);

	Объект = ВерсияХранилища.ПолучитьОбъект();
	Если ЗначениеЗаполнено(Объект.КаталогВременныхФайлов) Тогда
		Параметры.Вставить("КаталогВременныхФайлов", Объект.КаталогВременныхФайлов);
	Иначе
		Объект.КаталогВременныхФайлов = Параметры.КаталогВременныхФайлов;
		Объект.Записать();
	КонецЕсли;
	
	Параметры.Вставить("КаталогИБ", Параметры.КаталогВременныхФайлов + "db");
	Параметры.КаталогИБ = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогИБ);

	Файл = Новый Файл(Параметры.КаталогВременныхФайлов);
	Если НЕ Файл.Существует() Тогда
		СоздатьКаталог(Параметры.КаталогВременныхФайлов);
	КонецЕсли;

	Параметры.Вставить("Ключ", КлючОперации);
	Параметры.Вставить("ФайлПараметровПакетнойОперации", Параметры.КаталогВременныхФайлов
		+ "params_" + Параметры.Ключ + ".txt");
	Если НЕ Параметры.ОтключитьЛогирование Тогда
		Параметры.Вставить("ИмяФайлаЛога", ИмяФайлаЛогаКонвертацииХранилища(Параметры.КаталогВременныхФайлов));
	КонецЕсли;
	Параметры.Вставить("ИмяФайлаРезультатов", Параметры.КаталогВременныхФайлов
		+ "result.txt");

	Параметры.Вставить("СоздатьФайловуюИнформационнуюБазу", Истина);
	Параметры.Вставить("ДобавлятьВСписок", Истина);

	ПараметрыПодключенияКХранилищу(Хранилище, Параметры);
	
	ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);
	
	УдалитьФайлы(Параметры.КаталогИБ);

	СоздатьФайловуюИнформационнуюБазу(Параметры);

	СтрокаСоединения = " /F ""%Путь%""";
	СтрокаСоединения = СтрЗаменить(СтрокаСоединения, "%Путь%", Параметры.КаталогИБ);
	Параметры.Вставить("СтрокаСоединенияИБ", СтрокаСоединения);
	Параметры.Вставить("ИмяПользователяИБ", "");
	Параметры.Вставить("ПарольПользователяИБ", "");

	ПараметрыРасширения(Хранилище, Параметры);
	ДобавитьРасширениеКонфигурации(Параметры);
		
	ДлительныеОперации.СообщитьПрогресс(20, "Получения версии из хранилища");

	Параметры.Вставить("ОбновитьИнформационнуюБазуИзХранилища", Истина);
	Параметры.Вставить("ВерсияХранилища", Параметры.Код);
	ОбновитьИнформационнуюБазуИзХранилища(Параметры);
	
	ДлительныеОперации.СообщитьПрогресс(90, "Сохранение файла информации выгрузки");
	Параметры.Вставить("Это8315", ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.15.0") > 0);
	Параметры.Вставить("СохранитьФайлИнформацииВыгрузки", Параметры.Это8315);
	// Выгрузку файла информации выгрузки в каталог временных файлов
	Параметры.Вставить("КаталогФайловКонфигурации", Параметры.КаталогВременныхФайлов);
	Параметры.КаталогФайловКонфигурации = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Параметры.КаталогФайловКонфигурации);
	
	Файл = Новый Файл(Параметры.КаталогФайловКонфигурации + "ConfigDumpInfo.xml");
	Если Файл.Существует() Тогда
		УдалитьФайлы(Файл.ПолноеИмя);
	КонецЕсли;
	
	СохранитьФайлИнформацииВыгрузки(Параметры);
	
	ДлительныеОперации.СообщитьПрогресс(100, "Версия получена из хранилища");

	Справочники.ВерсииХранилища.УстановитьСостояние(ВерсияХранилища, Перечисления.СостоянияВерсии.ВерсияПолучена);

	ОбъектВерсии = ВерсияХранилища.ПолучитьОбъект();
	ОбъектВерсии.Источник = Неопределено;
	ОбъектВерсии.Записать();

	ЗапуститьОбработкуВерсииВФоне(ВерсияХранилища);

КонецПроцедуры

// Зачитываются реквизиты объекта метаданных из mdo файла
// 
// Параметры:
// 	СтруктураФайла - Структура - параметры файла
// 	КвалифицированныеОбъекты - Структура - список квалифицированных объектов для поиска в файле
// 	ПодчиненныеОбъекты - Массив - список подчиненных объектов
Процедура ПрочитатьСвойстваОбъектаМетаданныхEDT(СтруктураФайла, КвалифицированныеОбъекты, ПодчиненныеОбъекты)

	Попытка

		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(СтруктураФайла.ФайлПолноеИмя);

		Если ЧтениеXML.Прочитать() И СтрНачинаетсяС(ЧтениеXML.Имя, "mdclass:")
				И ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл

				Если ЧтениеXML.Имя = "uuid" Тогда

					СтруктураФайла.Вставить("UUID", ЧтениеXML.Значение);
					Прервать;
				КонецЕсли;
			КонецЦикла;

			Пока ЧтениеXML.Прочитать() Цикл
				Если КвалифицированныеОбъекты.Получить(ЧтениеXML.Имя) <> Неопределено Тогда
					Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда

						ПодчиненныйОбъект = Новый Структура;
						ПодчиненныйОбъект.Вставить("Тип", ЧтениеXML.Имя);

						Пока ЧтениеXML.ПрочитатьАтрибут() Цикл

							Если ЧтениеXML.Имя = "uuid" Тогда

								ПодчиненныйОбъект.Вставить("UUID", ЧтениеXML.Значение);
								Прервать;
							КонецЕсли;
						КонецЦикла;

						Пока ЧтениеXML.Прочитать()
							И ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Цикл

							Если ЧтениеXML.Имя = "name" Тогда

								ЧтениеXML.Прочитать();
								ПодчиненныйОбъект.Вставить("Имя", ЧтениеXML.Значение);
								ПодчиненныеОбъекты.Добавить(ПодчиненныйОбъект);
								ЧтениеXML.Пропустить();
							Иначе
								ЧтениеXML.Пропустить();
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ЧтениеXML.Закрыть();

	Исключение

	КонецПопытки;

КонецПроцедуры

// Составляет список объектов дополнительных объектов для выгрузки
// 
// Параметры:
// 	ФайлИзменений - ТекстовыйДокумент - Файл с описанием измененений
// 	ИмяФайлаДампа - Строка - Имя файл информации дампа
// Возвращаемое значение:
// 	Массив из Строка - список объектов которые необходимо выгрузить дополнительно
Функция ПолучитьСписокОбъектовДовыгрузки(ФайлИзменений, ИмяФайлаДампа)
	
	СписокОбъектов = Новый Массив;
	
	ВыгруженныеИзменения = Новый Соответствие();
	
	Для индекс = 1 По ФайлИзменений.КоличествоСтрок() Цикл
		СтрокаИзменений = ФайлИзменений.ПолучитьСтроку(индекс);
		СтрокаИзменений = СтрЗаменить(СтрокаИзменений, "New:", "");
		СтрокаИзменений = СтрЗаменить(СтрокаИзменений, "Modified:", "");
		СтрокаИзменений = СокрЛП(СтрокаИзменений);
		Если ПустаяСтрока(СтрокаИзменений) Тогда
			Продолжить;
		КонецЕсли;
		Сегменты = СтрРазделить(СтрокаИзменений, ".");
		Если Сегменты.Количество() > 0  И (Сегменты[0] = "Language" ИЛИ Сегменты[0] = "Configuration") Тогда
			Родитель = Сегменты[0];
		ИначеЕсли Сегменты.Количество() > 1 Тогда
			Родитель = Сегменты[0] + "." + Сегменты[1];
		Иначе
			Родитель = СтрокаИзменений;
		КонецЕсли;
		Изменения = ВыгруженныеИзменения.Получить(Родитель);
		Если Изменения = Неопределено Тогда
			Изменения = Новый Массив;
		КонецЕсли;
		Если Изменения.Найти(СтрокаИзменений) = Неопределено Тогда
			Изменения.Добавить(СтрокаИзменений);
		КонецЕсли;
		ВыгруженныеИзменения.Вставить(Родитель, Изменения);
		
		// Добавляем для корня конфигурации - языки, а для языков - корень конфигурации
		Если Сегменты.Количество() > 0  И (Сегменты[0] = "Language" ИЛИ Сегменты[0] = "Configuration") Тогда
			Если Сегменты[0] = "Configuration" Тогда
				Родитель = "Language";
			Иначе
				Родитель = "Configuration";
			КонецЕсли;
			Изменения = ВыгруженныеИзменения.Получить(Родитель);
			Если Изменения = Неопределено Тогда
				Изменения = Новый Массив;
				ВыгруженныеИзменения.Вставить(Родитель, Изменения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДочерниеОбъекты = Новый Массив;
	ДочерниеОбъекты.Добавить("Form");
	ДочерниеОбъекты.Добавить("Template");
	ДочерниеОбъекты.Добавить("Recalculation");
	ДочерниеОбъекты.Добавить("Subsystem");
	
	Попытка

		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайлаДампа);

		Если ЧтениеXML.Прочитать() И  ЧтениеXML.Имя = "ConfigDumpInfo" И ЧтениеXML.Прочитать() И ЧтениеXML.Имя = "ConfigVersions"  Тогда
			Стек = 0;
			Пока ЧтениеXML.Прочитать() И  ЧтениеXML.Имя = "Metadata" Цикл
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
					Стек = Стек - 1;
					Продолжить;
				КонецЕсли;
				
				Стек = Стек + 1;
				Если Стек > 1 Тогда
					Продолжить;
				КонецЕсли;
				ИмяОбъекта = "";
				Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
					Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
						Если ЧтениеXML.Имя = "name" Тогда

							ИмяОбъекта = ЧтениеXML.Значение;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				Если ЗначениеЗаполнено(ИмяОбъекта) Тогда
					Сегменты = СтрРазделить(ИмяОбъекта, ".");
					Если Сегменты.Количество() > 0 И (Сегменты[0] = "Language" ИЛИ Сегменты[0] = "Configuration") Тогда
						Родитель = Сегменты[0];
					ИначеЕсли Сегменты.Количество() > 1 Тогда
						Родитель = Сегменты[0] + "." + Сегменты[1];
					Иначе
						Родитель = ИмяОбъекта;
					КонецЕсли;
					
					Если Сегменты.Количество() > 2 И ДочерниеОбъекты.Найти(Сегменты[2]) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					Изменения = ВыгруженныеИзменения.Получить(Родитель);
					Если Изменения <> Неопределено И Изменения.Найти(ИмяОбъекта) = Неопределено Тогда
						СписокОбъектов.Добавить(ИмяОбъекта);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		ЧтениеXML.Закрыть();

	Исключение

	КонецПопытки;
	
	Возврат СписокОбъектов;
	
КонецФункции


// Формирует файлы коммита и записывает в зависимости от ОС
// 
// Параметры:
// 	ВерсияХранилища - СправочникСсылка.ВерсииХранилища - Ссылка на версию
// 	ПредыдущаяВерсия - СправочникСсылка.ВерсииХранилища - Ссылка на предыдущую версию
// 	РеквизитыХранилища - Структура - Реквизиты хранилища
// 	РеквизитыВерсии - Структура - Реквизиты версии
// 	ИмяФайлаКомандыGit - Строка - Имя файла команды коммита в гит
// 	ИмяФайлаЛога - Строка - Имя файла лога операции коммита или пустая строка, если логирование отключено
// 	ИмяФайлКомментария - Строка - имя файла комментария к коммиту
//
Процедура ЗаписатьФайлыКоммитаВерсии(ВерсияХранилища, ПредыдущаяВерсия, РеквизитыХранилища, РеквизитыВерсии, 
	ИмяФайлаКомандыGit, ИмяФайлаЛога, ИмяФайлКомментария)

	// Путь к проекту в репозитории /path_to_git/Project/
	РеквизитыХранилища.Вставить("ПутьКПроекту", ПутьКПроекту(РеквизитыХранилища));
	// Путь к файлам конфигурации проекта в репозитории /path_to_git/Project/src/
	РеквизитыХранилища.Вставить("ПутьКФайламПроекта", ПутьКФайламПроекта(РеквизитыХранилища));
	
	// Каталог исходных файлов проекта /path_to_dump/1/p/Project/
	РеквизитыВерсии.Вставить("ПутьКПроектуВерсии", ПутьКПроектуВерсии(РеквизитыВерсии));
	// Каталог исходных файлов проекта /path_to_dump/1/p/Project/src/
	РеквизитыВерсии.Вставить("ПутьКФайламПроектаВерсии", ПутьКФайламПроектаВерсии(РеквизитыВерсии));
		
	ФайлКоманды = Новый ТекстовыйДокумент;

	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();

	Если ЭтоWindowsСервер Тогда
		ТекстКоманды = "@ECHO OFF";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		ТекстКоманды = "chcp 65001";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	Иначе
		ТекстКоманды = "#!/bin/bash";
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяФайлаЛога) тогда
		ТекстКоманды = ?(ЭтоWindowsСервер, "set ", "") + "LOGFILE=""%ФайлЛога%""";
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ИмяФайлаЛога);
		ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

		Если ЭтоWindowsСервер Тогда
			ВыводЛога = " >> %LOGFILE% 2>&1";
		Иначе
			ВыводЛога = " >> $LOGFILE 2>&1";
		КонецЕсли;
	Иначе
		ВыводЛога = "";
	КонецЕсли;
	
	Если ЭтоWindowsСервер Тогда
		ТекстКомандыУстановкиКаталога = "cd /D ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	Иначе
		ТекстКомандыУстановкиКаталога = "cd ""%ЛокальныйКаталогGit%""" + ВыводЛога;
	КонецЕсли;
	ТекстКомандыУстановкиКаталога = СтрЗаменить(ТекстКомандыУстановкиКаталога, "%ЛокальныйКаталогGit%", 
		РеквизитыХранилища.ЛокальныйКаталогGit);

	ФайлКоманды.ДобавитьСтроку(ТекстКомандыУстановкиКаталога);
	
	ПрефиксКаталогаИсходников = КаталогФайловПроектаВРепозитории(РеквизитыХранилища);
	ИмяФайлаИндексов = РеквизитыВерсии.ПутьКФайламПроектаВерсии + "DumpFilesIndex.txt";

	ТаблицаИндексов = ПрочитатьТаблицуИндексов(ИмяФайлаИндексов);
	ТаблицаИндексов.Сортировать("Уровень, ПолноеИмя");
	
	ДатаСоздания = Формат(РеквизитыВерсии.ДатаСоздания, "ДФ='yyyy-MM-dd HH:mm:ss'");
	Пользователь = РеквизитыВерсии.Пользователь;
	Email = РеквизитыВерсии.Email;

	Если НЕ ЗначениеЗаполнено(Email) ИЛИ СтрНайти(Email, "@") = 0 Тогда

		Email = "anonimous@localhost";

	КонецЕсли;

	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_AUTHOR_DATE=""%Дата%""", "%Дата%", ДатаСоздания);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_COMMITTER_DATE=""%Дата%""", "%Дата%", ДатаСоздания);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_AUTHOR_NAME=""%Пользователь%""", "%Пользователь%", Пользователь);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_COMMITTER_NAME=""%Пользователь%""", "%Пользователь%", Пользователь);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_AUTHOR_EMAIL=""<%email%>""", "%email%", Email);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	ТекстКоманды = СтрЗаменить(?(ЭтоWindowsСервер, "set ", "export ")
		+ "GIT_COMMITTER_EMAIL=""<%email%>""", "%email%", Email);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	
	ЕстьПереименования = Ложь;
	ЕстьУдаления = Ложь;
		
	// Обрабатываем переименования 
	Если ЗначениеЗаполнено(ПредыдущаяВерсия) И НЕ РеквизитыВерсии.ВыгрузкаИзменений Тогда
		// Явное переименование всех файлов, если переименовывается папка, 
		// необходимо учесть переименование подчиненных папок
		
		ИменаРеквизитов = Новый Структура("КаталогВременныхФайлов, Состояние, ЛокальныйКаталогGit, ИмяПроектаEDT",
			"КаталогВременныхФайлов",
			"Состояние", 
			"Владелец.ЛокальныйКаталогGit",
			"Владелец.ИмяПроектаEDT");
		РеквизитыПредыдущейВерсии = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредыдущаяВерсия, ИменаРеквизитов);
		
		Если РеквизитыПредыдущейВерсии.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены Тогда
			РеквизитыПредыдущейВерсии.Вставить("КаталогФайловПроекта", ПутьКФайламПроектаВерсии(РеквизитыПредыдущейВерсии));
			ИмяФайлаИндексов = РеквизитыПредыдущейВерсии.КаталогФайловПроекта + "DumpFilesIndex.txt";
		ИначеЕсли РеквизитыПредыдущейВерсии.Состояние = Перечисления.СостоянияВерсии.ВерсияПомещена Тогда
			ИмяФайлаИндексов = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
				РеквизитыПредыдущейВерсии.ЛокальныйКаталогGit) 
				+ КаталогФайловПроектаВРепозитории(РеквизитыПредыдущейВерсии) 
				+ "DumpFilesIndex.txt";
		Иначе
			ВызватьИсключение Нстр("ru = 'Формирование файла невозможно для версии со статусом ""Начало коммита""';", 
				Метаданные.ОсновнойЯзык.КодЯзыка);
		КонецЕсли;
		
		Файл = Новый Файл(ИмяФайлаИндексов);
		Если НЕ Файл.Существует() И РеквизитыПредыдущейВерсии.Состояние = Перечисления.СостоянияВерсии.МетаданныеЗагружены Тогда
			Возврат;
		КонецЕсли;
		ТаблицаИндексовПредыдущейВерсии = ПрочитатьТаблицуИндексов(ИмяФайлаИндексов);
		ТаблицаИндексовПредыдущейВерсии.Сортировать("Уровень, UUID");
		
		// Индексы для поиска неизмененных файлов
		ТаблицаИндексовПредыдущейВерсии.Индексы.Добавить("Уровень, UUID");
		
		ТекстыКомандПереименования = Новый Массив; // Массив из Строка -
		
		Для Каждого СтрокаТЧ Из ТаблицаИндексов Цикл
			Отбор = Новый Структура();
			Отбор.Вставить("Уровень", СтрокаТЧ.Уровень);
			Отбор.Вставить("UUID",    СтрокаТЧ.UUID);
			
			НайденныеСтроки = ТаблицаИндексовПредыдущейВерсии.НайтиСтроки(Отбор);
			Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
				Если НайденнаяСтрока.ХешПолногоИмени <> СтрокаТЧ.ХешПолногоИмени Тогда
					// Добавляем переименование файлов

					// Создаем все кататалоги
					Если ЭтоWindowsСервер Тогда
						ТекстКоманды = "mkdir ""%Приемник%""" + ВыводЛога;
					Иначе
						ТекстКоманды = "mkdir -p ""%Приемник%""" + ВыводЛога;
					КонецЕсли;
					ДиректорияПриемника = ПрефиксКаталогаИсходников + СтрокаТЧ.ПолноеИмя;
					МассивПутей = СтрРазделить(ДиректорияПриемника, ПолучитьРазделительПути());
					МассивПутей.Удалить(МассивПутей.ВГраница());
					ДиректорияПриемника = СтрСоединить(МассивПутей, ПолучитьРазделительПути());
					ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", ДиректорияПриемника);
					ТекстыКомандПереименования.Добавить(ТекстКоманды);

					ТекстКоманды = "git mv -f %Источник% %Приемник%" + ВыводЛога;

					ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", ПрефиксКаталогаИсходников
						+ НайденнаяСтрока.ПолноеИмя);
					ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", ПрефиксКаталогаИсходников
						+ СтрокаТЧ.ПолноеИмя);

					ТекстыКомандПереименования.Добавить(ТекстКоманды);
					ЕстьПереименования = Истина;
					
				КонецЕсли;
				ТаблицаИндексовПредыдущейВерсии.Удалить(НайденнаяСтрока);

			КонецЦикла;

		КонецЦикла;
		
		// Все что не найдено - помечаем на удаление
		Для Каждого СтрокаТЧ Из ТаблицаИндексовПредыдущейВерсии Цикл
			ТекстКоманды = "git rm -f %Источник%" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", ПрефиксКаталогаИсходников
				+ СтрокаТЧ.ПолноеИмя);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			ЕстьУдаления = Истина;
		КонецЦикла;
		
		Если ЕстьУдаления Тогда
			ТекстКоманды = "git commit -F ""%ИмяФайлКомментария%"" --allow-empty-message --cleanup=verbatim" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяФайлКомментария%", ИмяФайлКомментария);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЕсли;
		
		// Переименования вставляются после удаления т.к. могут быть конфликты,
		// если новое имя при переименовании совпадает с объектом, который удаляется
		Для Каждого ТекстКоманды из ТекстыКомандПереименования Цикл
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЦикла;
		
		Если ЕстьПереименования Тогда
			ТекстКоманды = "git commit -F ""%ИмяФайлКомментария%"" --allow-empty-message --cleanup=verbatim" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяФайлКомментария%", ИмяФайлКомментария);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЕсли;
		
	КонецЕсли;
	
	МассивПутейПриемника = СтрРазделить(РеквизитыХранилища.ПутьКПроекту, ПолучитьРазделительПути(), Ложь);
	МассивПутейИсточника = СтрРазделить(РеквизитыВерсии.ПутьКПроектуВерсии, ПолучитьРазделительПути(), Ложь);

	// Для ускорения перемещаем исходные файлы если они на одном диске
	// А если на другом - позже скопируем файлы рекурсивно
	Если НРег(МассивПутейИсточника[0]) = НРег(МассивПутейПриемника[0]) Тогда

		Если ЭтоWindowsСервер Тогда
			
			// Если выгрузка только изменений - тогда обновляем существующие файлы
			Если НЕ РеквизитыВерсии.ВыгрузкаИзменений Тогда
				ТекстКоманды = "rmdir /S /Q ""%Приемник%""" + ВыводЛога;
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКФайламПроекта);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			КонецЕсли;
			
			Если РеквизитыВерсии.ВыгрузкаИзменений Тогда
				// Для изменений используется копирование с заменой
				Источник = СокрЛП(РеквизитыВерсии.ПутьКФайламПроектаВерсии);
				Если Прав(Источник, 1) = ПолучитьРазделительПути() Тогда
					Источник = Лев(Источник, СтрДлина(Источник) - 1);
				КонецЕсли;
				Приемник = СокрЛП(РеквизитыХранилища.ПутьКФайламПроекта);
				Если Прав(Приемник, 1) = ПолучитьРазделительПути() Тогда
					Приемник = Лев(Приемник, СтрДлина(Приемник) - 1);
				КонецЕсли;
				ТекстКоманды = "robocopy ""%Источник%"" ""%Приемник%"" /E /MOVE /NFL /NDL /NJH /NJS /NC /NS /NP" + ВыводЛога;
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", Источник);
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", Приемник);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			Иначе
				// Для полной выгрузки - команда перемещения
				ТекстКоманды = "for /d %%A in (""%Источник%*"") do IF EXIST ""%Приемник%%%~nxA"" ( robocopy ""%%~A"" ""%Приемник%%%~nxA"" /E /MOVE /NFL /NDL /NJH /NJS /NC /NS /NP) ELSE ( move /y ""%%~A"" ""%Приемник%"" )" + ВыводЛога;
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", РеквизитыВерсии.ПутьКПроектуВерсии);
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКПроекту);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
				ТекстКоманды = "move /y ""%Источник%*"" ""%Приемник%""" + ВыводЛога;
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", РеквизитыВерсии.ПутьКПроектуВерсии);
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКПроекту);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			КонецЕсли;
		Иначе
			Если НЕ РеквизитыВерсии.ВыгрузкаИзменений Тогда
				// При конвертации в формате EDT удаляем все каталоги, в том числе и служебные DT-INF, .settings
				ТекстКоманды = "for i in ""%Источник%""{.[!.],}*; do test -d ""$i"" && rm -rf ""%Приемник%$(basename $i)""; done";
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", РеквизитыВерсии.ПутьКПроектуВерсии);
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКПроекту);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			КонецЕсли;
			
			Если НЕ РеквизитыВерсии.ВыгрузкаИзменений Тогда
				ТекстКоманды = "mkdir -p ""%Приемник%""" + ВыводЛога;
				ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКПроекту);
				ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
			КонецЕсли;
			
			ТекстКоманды = "mv -f ""%Источник%""{.[!.],}* ""%Приемник%""" + ВыводЛога;
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Источник%", РеквизитыВерсии.ПутьКПроектуВерсии);
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%Приемник%", РеквизитыХранилища.ПутьКПроекту);
			ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
		КонецЕсли;

	КонецЕсли;
	
	// Все файлы новой версии добавляем в индекс
	ТекстКоманды = "git add --all ./" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);
	
	ТекстКоманды = "git commit -F ""%ИмяФайлКомментария%"" --allow-empty-message --cleanup=verbatim" + ВыводЛога;

	Комментарий = СформироватьТекстКомментария(РеквизитыВерсии);

	ФайлКомментария = Новый ТекстовыйДокумент;
	ФайлКомментария.УстановитьТекст(Комментарий);
	ФайлКомментария.Записать(ИмяФайлКомментария, "CESU-8");

	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяФайлКомментария%", ИмяФайлКомментария);
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	// Выполнение регламентных действий с репозиторием, если необходимо
	ТекстКоманды = "git gc --auto" + ВыводЛога;
	ФайлКоманды.ДобавитьСтроку(ТекстКоманды);

	Если ЭтоWindowsСервер Тогда
		ФайлКоманды.Записать(ИмяФайлаКомандыGit, "CESU-8");
	Иначе
		ФайлКоманды.Записать(ИмяФайлаКомандыGit, КодировкаТекста.Системная, Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры



// Возвращаемое значение:
// 	Структура:
// * ИмяПроектаEDT - Строка
// * Ссылка - СправочникСсылка.ВерсииХранилища - Ссылка на помещаемую версию в репозиторий
// * Код - Число - Код помещаемой версии
// * КаталогВременныхФайлов - Строка - Каталог временных файлов версии
// * Комментарий - Строка - Комментарий версии из Хранилища 1С
// * ДатаСоздания - Дата - Дата создания версии
// * Пользователь - Строка - Имя пользователя Git-репозитория
// * Email - Строка - E-mail пользователя Git-репозитория
// * ПользовательХранилища - Строка - Имя пользователя Хранилища 1С
// * КоличествоМетаданных - Число - Количество метаданных в версии
// * ВыгрузкаИзменений - Булево - Признак частичной выгрузки изменений версии
// * ПутьКПроектуВерсии - Строка - Путь ко временному каталогу проекта в формате EDT
// * ПутьКФайламПроектаВерсии - Строка - Путь ко временному каталогу файлов исходников (.../src/)
// * ИмяФайлаЛога - Строка - Путь к файлу логов текущей версии
Функция ПараметрыВерсииДляКоммита()
	
	ПараметрыВерсии = Новый Структура("Ссылка, Код, КаталогВременныхФайлов, Комментарий, ДатаСоздания, 
		| Пользователь, Email, ПользовательХранилища, КоличествоМетаданных, ВыгрузкаИзменений, ИмяПроектаEDT");
	Возврат ПараметрыВерсии;
	
КонецФункции


// Параметры:
// 	Хранилище - СправочникСсылка.ХранилищаКонфигураций - 
// Возвращаемое значение:
// 	Структура:
// 	* ПутьКПроекту - Строка - Полный путь к каталогу проекта, находящемуся в репозитории
// 	* ПутьКФайламПроекта - Строка - Полный путь каталогу исходных файлов проекта в репозитории (.../src/)
// 	* ВерсияВGit - СправочникСсылка.ВерсииХранилища - ссылка на помещенную версию, записанную в элементе объекта хранилища.
// 	* Код - Число - Код помещенной версии
// 	* ДобавлятьМеткиСВерсиейКонфигурации - Булево - Признак добавления меток с версией конфигурации
// 	* АдресРепозиторияGit - Строка - адрес Git-сервера
// 	* ИмяВетки - Строка - Имя ветки в Git-репозитории
// 	* ЛокальныйКаталогGit - Строка - Локальный путь к каталогу репозитория Git
// 	* КаталогВыгрузкиВерсий - Строка
// 	* МинимальноеКоличествоМетаданных - Число
// 	* КоличествоКоммитов - Число
// 	* ОтключитьЛогирование - Булево
// 	* РазрешитьПомещатьАнонимноЕслиНеНайденПользователь - Булево
// 	* ИмяПроектаEDT - Строка
// 	* ВыгружатьИзменения - Булево
// 	* КонвертироватьВФорматEDT - Булево
// 	* ДобавлятьМеткиСВерсиейКонфигурации - Булево
// 
Функция ПараметрыХранилищаДляКоммита(Знач Хранилище)
	
	ИменаРеквизитов = Новый Структура("ВерсияВGit, Код, ЛокальныйКаталогGit, КаталогВыгрузкиВерсий, АдресРепозиторияGit, 
		| ИмяВетки, МинимальноеКоличествоМетаданных, КоличествоКоммитов, ОтключитьЛогирование,
		| РазрешитьПомещатьАнонимноЕслиНеНайденПользователь, ИмяПроектаEDT, ВыгружатьИзменения,
		| КонвертироватьВФорматEDT, ДобавлятьМеткиСВерсиейКонфигурации", 
		"ВерсияВGit", 
		"ВерсияВGit.Код");
	РеквизитыХранилища = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Хранилище, ИменаРеквизитов);
	
	// Путь к проекту в репозитории /path_to_git/Project/
	РеквизитыХранилища.Вставить("ПутьКПроекту", ПутьКПроекту(РеквизитыХранилища));
	// Путь к файлам конфигурации проекта в репозитории /path_to_git/Project/src/
	РеквизитыХранилища.Вставить("ПутьКФайламПроекта", ПутьКФайламПроекта(РеквизитыХранилища));
	
	Возврат РеквизитыХранилища;
	
КонецФункции



// Выполняются действия перед коммитом версии, перед запуском командного файла, выполняющего копирование 
// файлов версии в каталог репозитория и последующим коммитом изменений. Этот метод можно дополнять в 
// подключаемых расширениях для реализации дополнительной логики после выполнения коммита 
// в локальный репозиторий.
// 
// Параметры:
// 	ПараметрыВерсии - См. ПараметрыВерсииДляКоммита
// 	РеквизитыХранилища - См. ПараметрыХранилищаДляКоммита
// Возвращаемое значение:
// 	Булево - Истина, если действия выполнены успешно, Ложь - если необходимо прервать коммит версии.
Функция ВыполнитьДействияПередКоммитом(ПараметрыВерсии, РеквизитыХранилища)

	Если ЗначениеЗаполнено(РеквизитыХранилища.АдресРепозиторияGit) И ЗначениеЗаполнено(РеквизитыХранилища.ВерсияВGit) Тогда
		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
		ДобавлятьМетки = ?(РеквизитыХранилища.ДобавлятьМеткиСВерсиейКонфигурации, "--tags", "");
		
		ТекстКоманды = "git pull %ДобавлятьМетки%";
		Если ПараметрыВерсии.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(ПараметрыВерсии.ИмяФайлаЛога) Тогда
			ТекстКоманды = ТекстКоманды + " >> ""%ФайлЛога%"" 2>&1";
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ПараметрыВерсии.ИмяФайлаЛога);
		КонецЕсли;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ДобавлятьМетки%", ДобавлятьМетки);
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Начало получения коммитов с Git-сервера'"));
		
		КодВозврата = Неопределено;
		ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "bash -c ")
			+ ТекстКоманды, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание получения коммитов с Git-сервера'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));

		Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
			ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание получения коммитов с Git-сервера'"), НСтр("ru='Получение коммитов (git pull) с Git-сервера выполнен не успешно. Необходимо проанализировать лог, устранить причину.'"));
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыХранилища.ДобавлятьМеткиСВерсиейКонфигурации Тогда
		Возврат ДобавитьМеткуСНомеромСборки(ПараметрыВерсии, РеквизитыХранилища)
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции


// Выполняются действия после коммита. Если установлен адрес Git-сервера, то выполняется отправка коммитов 
// на сервер. Этот метод можно дополнять в подключаемых расширениях для реализации дополнительной логики 
// после выполнения коммита в локальный репозиторий.
// 
// Параметры:
// 	ПараметрыВерсии - См. ПараметрыВерсииДляКоммита
// 	РеквизитыХранилища - См. ПараметрыХранилищаДляКоммита
// Возвращаемое значение:
// 	Булево - Истина, если действия выполнены успешно, Ложь - если необходимо прервать дальнейшую обработку.
Функция ВыполнитьДействаияПослеКоммита(ПараметрыВерсии, РеквизитыХранилища)
	
	Если ЗначениеЗаполнено(РеквизитыХранилища.АдресРепозиторияGit)
		И ЗначениеЗаполнено(РеквизитыХранилища.ВерсияВGit) Тогда
		ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
		ДобавлятьМетки = ?(РеквизитыХранилища.ДобавлятьМеткиСВерсиейКонфигурации, "--tags", "");
		
		ТекстКоманды = "git push --progress %ДобавлятьМетки% -u origin %ИмяВетки%";
		Если ПараметрыВерсии.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(ПараметрыВерсии.ИмяФайлаЛога) Тогда
			ТекстКоманды = ТекстКоманды + " >> ""%ФайлЛога%"" 2>&1";
			ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ПараметрыВерсии.ИмяФайлаЛога);
		КонецЕсли;
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ИмяВетки%", РеквизитыХранилища.ИмяВетки);
		
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ДобавлятьМетки%", ДобавлятьМетки);
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Начало отправки коммитов на Git-сервер'"));
		
		КодВозврата = Неопределено;
		ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "bash -c ")
			+ ТекстКоманды, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
		
		ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание отправки коммитов на Git-сервер'"), "Код возврата: "
			+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));
		
		Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
			ДобавитьЗаписьВЛог(ПараметрыВерсии, НСтр("ru='Окончание отправки коммитов на Git-сервер'"),
				НСтр("ru='Отправка коммитов (git push) на Git-сервер выполнен не успешно. Необходимо проанализировать лог, устранить причину и вручную завершить git push.'"));
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции


// Добавляет метку с версией конфигурации, если в следующем коммите меняется номер версии. 
// 
// Параметры:
// 	ПараметрыВерсии - Структура - Параметры версии:
// * Ссылка - СправочникСсылка.ВерсииХранилища - Ссылка на помещенную версию в репозиторий
// * Код - Число - Код помещенной версии
// * Комментарий - Строка - Комментарий версии из Хранилища 1С
// * ДатаСоздания - Дата - Дата создания версии
// * Пользователь - Строка - Имя пользователя Git-репозитория
// * Email - Строка - E-mail пользователя Git-репозитория
// * ПользовательХранилища - Строка - Имя пользователя Хранилища 1С
// * КоличествоМетаданных - Число - Количество метаданных в версии
// * ВыгрузкаИзменений - Булево - Признак частичной выгрузки изменений версии
// * ПутьКПроектуВерсии - Строка - Путь ко временному каталогу проекта в формате EDT
// * ПутьКФайламПроектаВерсии - Строка - Путь ко временному каталогу файлов исходников (.../src/)
// * ИмяФайлаЛога - Строка - Путь к файлу логов текущей версии
// 	РеквизитыХранилища - Структура - Параметры хранилища:
// * ПутьКПроекту - Строка - Полный путь к каталогу проекта, находящемуся в репозитории
// * ПутьКФайламПроекта - Строка - Полный путь каталогу исходных файлов проекта в репозитории (.../src/)
// * ВерсияВGit - СправочникСсылка.ВерсииХранилища - ссылка на помещенную версию, записанную в элементе объекта хранилища.
// * Код - Число - Код помещенной версии
// * ДобавлятьМеткиСВерсиейКонфигурации - Булево - Признак добавления меток с версией конфигурации
// * АдресРепозиторияGit - Строка - адрес Git-сервера
// * ИмяВетки - Строка - Имя ветки в Git-репозитории
// Возвращаемое значение:
// 	Булево - Истина, если действия выполнены успешно, Ложь - если необходимо прервать дальнейшую обработку.
Функция ДобавитьМеткуСНомеромСборки(ПараметрыВерсии, РеквизитыХранилища)
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	ПутьНоваяВерсия = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПараметрыВерсии.ПутьКФайламПроектаВерсии 
	                                                + "Configuration") + "Configuration.mdo";
	ПутьТекущаяВерсия = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ПутьКФайламПроекта 
	                                                + "Configuration") + "Configuration.mdo";
	
	ФайлНоваяВерсия = Новый Файл(ПутьНоваяВерсия);
	ФайлТекущаяВерсия = Новый Файл(ПутьТекущаяВерсия);
	
	Если Не ФайлНоваяВерсия.Существует() ИЛИ Не ФайлТекущаяВерсия.Существует() Тогда
		Возврат Истина;
	КонецЕсли; 
	
	НомерНовойВерсии = ПолучитьНомерКонфигурации(ПутьНоваяВерсия);
	НомерТекущейВерсии = ПолучитьНомерКонфигурации(ПутьТекущаяВерсия);
	
	Если НомерНовойВерсии = НомерТекущейВерсии Тогда
		Возврат Истина;
	КонецЕсли;
	
	МеткаСВерсией = СформироватьИмяМеткиВерсии(ПараметрыВерсии, РеквизитыХранилища, НомерТекущейВерсии);
	ПутьВременныхФайлов = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПараметрыВерсии.КаталогВременныхФайлов);
	ПутьФайлаПроверкиМетки = ПутьВременныхФайлов + "taginfo.txt";
	
	// Проверка существования метки с версией
	ТекстКоманды = "git tag -l ""%МеткаСВерсией%"" > ""%ПутьФайлаПроверкиМетки%"" 2>&1";
	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ПутьФайлаПроверкиМетки%", ПутьФайлаПроверкиМетки);
	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%МеткаСВерсией%", МеткаСВерсией);
	
	ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Начало проверки существования метки с номером: %1'"), МеткаСВерсией));
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "bash -c ")
		+ ТекстКоманды, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
	
	ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Конец проверки существования метки с номером: %1'"), МеткаСВерсией), "Код возврата: "
	+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));
	
	Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
		ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Окончание проверки существования метки с номером: %1'"), МеткаСВерсией),
			НСтр("ru='Проверка существования метки прошла неуспешно. Необходимо проанализировать лог и устранить причину.'"));
		Возврат Ложь;
	КонецЕсли;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ПутьФайлаПроверкиМетки);
	Текст = СокрЛП(ТекстовыйДокумент.ПолучитьТекст());
	Если Текст <> "" Тогда
		// Метка уже существует, 
		ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Метка с номером: %1 уже существует'"), МеткаСВерсией));
		Возврат Истина
	КонецЕсли; 
	
	// Добавление метки
	ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Начало добавления метки: %1'"), МеткаСВерсией));
	
	ТекстКоманды = "git tag -a ""%МеткаСВерсией%"" -m ""%МеткаСВерсией%""";
	Если ПараметрыВерсии.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(ПараметрыВерсии.ИмяФайлаЛога) Тогда
		ТекстКоманды = ТекстКоманды + " >> ""%ФайлЛога%"" 2>&1";
		ТекстКоманды = СтрЗаменить(ТекстКоманды, "%ФайлЛога%", ПараметрыВерсии.ИмяФайлаЛога);
	КонецЕсли;
	ТекстКоманды = СтрЗаменить(ТекстКоманды, "%МеткаСВерсией%", МеткаСВерсией);
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(?(ЭтоWindowsСервер, "cmd /C ", "bash -c ")
		+ ТекстКоманды, РеквизитыХранилища.ЛокальныйКаталогGit, Истина, КодВозврата);
	
	ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Конец добавления метки: %1'"), МеткаСВерсией), "Код возврата: "
		+ ?(КодВозврата = Неопределено, "Неопределено", КодВозврата));
	
	Если КодВозврата <> Неопределено И КодВозврата <> 0 Тогда
		ДобавитьЗаписьВЛог(ПараметрыВерсии, СтрШаблон(НСтр("ru='Окончание добавления метки: %1'"), МеткаСВерсией),
			НСтр("ru='Добавление метки прошло неуспешно. Необходимо проанализировать лог и устранить причину.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции

// Формирует имя метки с версией
// 
// Параметры:
// 	ПараметрыВерсии - Структура - Параметры версии:
// * Ссылка - СправочникСсылка.ВерсииХранилища - Ссылка на помещенную версию в репозиторий
// * Код - Число - Код помещенной версии
// * Комментарий - Строка - Комментарий версии из Хранилища 1С
// * ДатаСоздания - Дата - Дата создания версии
// * Пользователь - Строка - Имя пользователя Git-репозитория
// * Email - Строка - E-mail пользователя Git-репозитория
// * ПользовательХранилища - Строка - Имя пользователя Хранилища 1С
// * КоличествоМетаданных - Число - Количество метаданных в версии
// * ВыгрузкаИзменений - Булево - Признак частичной выгрузки изменений версии
// * ПутьКПроектуВерсии - Строка - Путь ко временному каталогу проекта в формате EDT
// * ПутьКФайламПроектаВерсии - Строка - Путь ко временному каталогу файлов исходников (.../src/)
// * ИмяФайлаЛога - Строка - Путь к файлу логов текущей версии
// 	РеквизитыХранилища - Структура - Параметры хранилища:
// * ПутьКПроекту - Строка - Полный путь к каталогу проекта, находящемуся в репозитории
// * ПутьКФайламПроекта - Строка - Полный путь каталогу исходных файлов проекта в репозитории (.../src/)
// * ВерсияВGit - СправочникСсылка.ВерсииХранилища - ссылка на помещенную версию, записанную в элементе объекта хранилища.
// * Код - Число - Код помещенной версии
// * ДобавлятьМеткиСВерсиейКонфигурации - Булево - Признак добавления меток с версией конфигурации
// * АдресРепозиторияGit - Строка - адрес Git-сервера
// * ИмяВетки - Строка - Имя ветки в Git-репозитории
// 	НомерВерсии - Строка - Номер версии конфигурации
// Возвращаемое значение:
// 	Строка - Истина, если действия выполнены успешно, Ложь - если необходимо прервать дальнейшую обработку.
Функция СформироватьИмяМеткиВерсии(ПараметрыВерсии, РеквизитыХранилища, НомерВерсии)
	Возврат "v" + НомерВерсии;
КонецФункции


// Возвращает версию конфигурации из файла Configuration.mdo   
// 
// Параметры:
// 	ИмяФайлаКонфигурации - Строка - Полный путь до файла Configuration.mdo   
// Возвращаемое значение:
//  Строка, Неопределено - Версия конфигурации, Неопределено - если не удалось прочитать Configuration.mdo
// 
Функция ПолучитьНомерКонфигурации(ИмяФайлаКонфигурации)
	
	НомерКонфигурации = Неопределено;
	Попытка
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайлаКонфигурации);
		
		Если ЧтениеXML.Прочитать() И СтрНачинаетсяС(ЧтениеXML.Имя, "mdclass:Configuration") Тогда
			
			Пока ЧтениеXML.Прочитать() И ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента Цикл
				
				Если ЧтениеXML.Имя = "version" Тогда
					ЧтениеXML.Прочитать();
					НомерКонфигурации = ЧтениеXML.Значение;
					ЧтениеXML.Пропустить();
				Иначе
					ЧтениеXML.Пропустить();
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
		ЧтениеXML.Закрыть();
		
	Исключение
	КонецПопытки;
	
	Возврат НомерКонфигурации
	
КонецФункции

// Формирование текста комменатрия для коммита в репозиторий. 
// Функция может быть переопределена в расширении для кастомизации текста.
// 
// Параметры:
// 	РеквизитыВерсии - Структура - Параметры версии:
// 	 * Ссылка - СправочникСсылка.ВерсииХранилища - Ссылка на версию хранилища
//   * Комментарий - Строка - комменатрий из хранилища, может быть пустым
//   * ДатаСоздания - Дата - дата создания версии в хранилище
//   * Пользователь - Строка - Пользователь Git
//   * Email - Строка - e-mail пользователя Git
//   * Код - Число - Номер версии в хранилище
//   * ПользовательХранилища - Строка - Пользователь хранилища
//   * ВыгрузкаИзменений - Булево - Признак выгрузки изменений или полная выгрузка
// Возвращаемое значение:
// 	Строка - Текст комментария
// 	
Функция СформироватьТекстКомментария(РеквизитыВерсии)

	ТекстКомментария = "%Комментарий%
		|//------
		| Версия хранилища №%НомерВерсии% от %Дата% %Автор%";
	
	ТекстКомментария = СтрЗаменить(ТекстКомментария, "%НомерВерсии%", Формат(РеквизитыВерсии.Код, "ЧДЦ=; ЧГ=0"));
	ТекстКомментария = СтрЗаменить(ТекстКомментария, "%Дата%", Формат(РеквизитыВерсии.ДатаСоздания, "ДФ='yyyy-MM-dd HH:mm:ss'"));
	ТекстКомментария = СтрЗаменить(ТекстКомментария, "%Автор%", РеквизитыВерсии.ПользовательХранилища);
	
	Если Не ЗначениеЗаполнено(РеквизитыВерсии.Комментарий) Тогда
		ТекстКомментария = СтрЗаменить(ТекстКомментария, "%Комментарий%", "<Комментарий не указан>");
	Иначе
		ТекстКомментария = СтрЗаменить(ТекстКомментария, "%Комментарий%", РеквизитыВерсии.Комментарий);
	КонецЕсли;

	Возврат ТекстКомментария;

КонецФункции

// Формирует полный путь к каталогу проекта в репозитории
// 
// Параметры:
// 	РеквизитыХранилища - Структура - Реквизиты хранилища
// Возвращаемое значение:
// 	Строка - полный путь к каталогу проекта в репозитории
Функция ПутьКПроекту(РеквизитыХранилища)
	
	Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit);
	Если НЕ ПустаяСтрока(РеквизитыХранилища.ИмяПроектаEDT) Тогда
		Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
			Путь + РеквизитыХранилища.ИмяПроектаEDT);
	КонецЕсли;
	
	Возврат Путь;
	
КонецФункции

// Формирует полный путь к каталогу файлов конфигурации в репозитории
// 
// Параметры:
// 	РеквизитыХранилища - Структура - Реквизиты хранилища
// Возвращаемое значение:
// 	Строка - полный путь к файлам конфиурации в репозитории
Функция ПутьКФайламПроекта(РеквизитыХранилища)
	
	Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(РеквизитыХранилища.ЛокальныйКаталогGit);
	Возврат Путь + КаталогФайловПроектаВРепозитории(РеквизитыХранилища);
	
КонецФункции

// Формирует относительный путь к каталогу файлов проекта в репозитории
// 
// Параметры:
// 	РеквизитыХранилища - Структура - Реквизиты хранилища
// Возвращаемое значение:
// 	Строка - путь к каталогу выгрузки
Функция КаталогФайловПроектаВРепозитории(РеквизитыХранилища) Экспорт
	
	ПрефиксКаталогаИсходников = "";
	Если НЕ ПустаяСтрока(РеквизитыХранилища.ИмяПроектаEDT) Тогда
		ПрефиксКаталогаИсходников = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
			ПрефиксКаталогаИсходников + РеквизитыХранилища.ИмяПроектаEDT);
	КонецЕсли;
	ПрефиксКаталогаИсходников = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
		ПрефиксКаталогаИсходников + "src");
	
	Возврат ПрефиксКаталогаИсходников;
	
КонецФункции

// Формирует полный путь к каталогу проекта версии
// 
// Параметры:
// 	ПараметрыВерсии - Структура - Параметры версии
// Возвращаемое значение:
// 	Строка - путь к каталогу проекта версии
Функция ПутьКПроектуВерсии(ПараметрыВерсии)

	Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПараметрыВерсии.КаталогВременныхФайлов);

	Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Путь + "p");
	СегментыПути = СтрРазделить(ПараметрыВерсии.ИмяПроектаEDT, ПолучитьРазделительПути());
	Если СегментыПути.Количество() > 0 Тогда
		Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Путь + СегментыПути[СегментыПути.ВГраница()]);
	КонецЕсли;

	Возврат Путь;

КонецФункции

// Формирует полный путь к каталогу файлов конфигурации версии внутри каталога проекта
// 
// Параметры:
// 	ПараметрыВерсии - Структура - Параметры версии
// Возвращаемое значение:
// 	Строка - путь к каталогу файлов конфигурации версии
Функция ПутьКФайламПроектаВерсии(ПараметрыВерсии)
	
	Путь = ПутьКПроектуВерсии(ПараметрыВерсии);
	Путь = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Путь+ "src");
	
	Возврат Путь;
КонецФункции

// Формирует путь к каталогу выгрузки файлов конфигурации в формате xml 1С:Предприятия
// 
// Параметры:
// 	КаталогВременныхФайлов - Строка - Каталог временных файлов версии
// Возвращаемое значение:
// 	Строка - путь к каталогу выгрузки файлов конфигурации версии
Функция КаталогФайловКонфигурации(Знач КаталогВременныхФайлов)
	
	Каталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогВременныхФайлов);
	Каталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Каталог + "dump");
	
	Возврат Каталог;
	
КонецФункции

// Добавляет запись в лог из пакетной операции
// 
// Параметры:
// 	Параметры - Структура - параметры запуска, состоящие из:
// 	  * ИмяФайлаЛога - Строка - Путь к файлу лога
// 	ТекстЛога - Строка - текст лога
// 	Комментарий - Строка - комментарий
// 	Уровень - Строка - уровень записи журнала регистрации
Процедура ДобавитьЗаписьВЛог(Параметры, ТекстЛога, Комментарий = "",
		Уровень = Неопределено)

	Если Уровень = Неопределено Тогда
		Уровень = "Информация";
	КонецЕсли;

	ДобавитьЗаписьВЖурналРегистрации(НСтр("ru = 'Пакетная операция'"), Уровень, , ТекстЛога, Комментарий);

	Если Параметры.Свойство("ИмяФайлаЛога") И ЗначениеЗаполнено(Параметры.ИмяФайлаЛога) Тогда
		Лог = Новый ЗаписьТекста();
		Лог.Открыть(Параметры.ИмяФайлаЛога, КодировкаТекста.UTF8, , Истина);
		Лог.ЗаписатьСтроку(Строка(ТекущаяДатаСеанса()) + " " + ТекстЛога);
		Если ЗначениеЗаполнено(Комментарий) Тогда
			Лог.ЗаписатьСтроку(Комментарий);
		КонецЕсли;
		Лог.Закрыть();
	КонецЕсли;

КонецПроцедуры

// Зачитывает результат из файла результатов пакетной операции Конфигуратора
// 
// Параметры:
// 	ИмяФайлаРезультатов - Строка - Имя файла результатов пакетной операции
// Возвращаемое значение:
// 	Число - Возвращается 0, если результат успешный (0 ошибок), 99999 если файл результата пустой, 
//  возвращается 100000 если ошибка чтения файла или в файле содержатся не числовые символы, N произвольное 
//  число, означающее наличие ошибок
Функция ПрочитатьФайлРезультата(ИмяФайлаРезультатов)
	
	Результат = 1;
	ФайлРезультата = Новый ТекстовыйДокумент();
	Попытка
		ФайлРезультата.Прочитать(ИмяФайлаРезультатов);
		ТекстРезультата = ФайлРезультата.ПолучитьТекст();
		Если ПустаяСтрока(ТекстРезультата) Тогда
			Результат = 99999;
		ИначеЕсли СокрЛП(ТекстРезультата) = "0" Тогда
			Результат = 0;
		Иначе
			Результат = Число(ТекстРезультата);
		КонецЕсли;
		
	Исключение
		Результат = 100000;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Копирует файлы рекурсивно
// 
// Параметры:
// 	Приемник - Строка - Описание
// 	Источник - Строка - Описание
Процедура СкопироватьФайлыРекурсивно(Источник, Приемник)

	Файлы = НайтиФайлы(Источник, "*");
	Для Каждого Файл Из Файлы Цикл
		ИмяПриемника = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Приемник)
			+ Файл.Имя;
		Если Файл.ЭтоКаталог() Тогда
			СоздатьКаталог(ИмяПриемника);
			СкопироватьФайлыРекурсивно(Файл.ПолноеИмя, ИмяПриемника);
		ИначеЕсли Файл.ЭтоФайл() Тогда
			КопироватьФайл(Файл.ПолноеИмя, ИмяПриемника);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Возвращает считанную из файла таблицу индексов
// 
// Параметры:
// 	ИмяФайлаИндексов - Строка - Полный путь к файлу индексов
// Возвращаемое значение:
// 	См. СозадатьТаблицуИндексов
Функция ПрочитатьТаблицуИндексов(ИмяФайлаИндексов)
	
	ТаблицаИндексов = СозадатьТаблицуИндексов();
	
	Файл = Новый Файл(ИмяФайлаИндексов);
	Если Не Файл.Существует() Тогда
		Возврат ТаблицаИндексов;
	КонецЕсли;
	
	Файл = Новый ЧтениеТекста();
	Файл.Открыть(ИмяФайлаИндексов, КодировкаТекста.UTF8);
	
	СледующаяСтрока = Файл.ПрочитатьСтроку(Символы.ПС);
	
	Пока СледующаяСтрока <> Неопределено Цикл
		
		ТекущаяСтрока = СледующаяСтрока;
		СледующаяСтрока = Файл.ПрочитатьСтроку(Символы.ПС);
		
		Если НЕ ПустаяСтрока(ТекущаяСтрока) Тогда
			МассивСтрок = СтрРазделить(ТекущаяСтрока, ":");
			Если МассивСтрок.Количество() = 4 Тогда
				UUID = МассивСтрок[0];
				ХешПолногоИмени = МассивСтрок[1];
				Попытка
					Уровень = Число(МассивСтрок[2]);
				Исключение
					Продолжить;
				КонецПопытки;
				ПолноеИмя = МассивСтрок[3];
				Если Уровень = 0 ИЛИ ПустаяСтрока(UUID) ИЛИ ПустаяСтрока(ХешПолногоИмени) ИЛИ ПустаяСтрока(ПолноеИмя) Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаИндексов.Добавить();
				НоваяСтрока.UUID = UUID;
				НоваяСтрока.ХешПолногоИмени = ХешПолногоИмени;
				НоваяСтрока.Уровень = Уровень;
				НоваяСтрока.ПолноеИмя = ПолноеИмя;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Файл.Закрыть();
	
	Возврат ТаблицаИндексов;
	
КонецФункции


// Создает таблицу индексов структуры метаданных
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица индексов:
// * UUID - Строка
// * ХешПолногоИмени - Строка
// * Уровень - Число
// * ПолноеИмя - Строка
Функция СозадатьТаблицуИндексов()
	
	КС = Новый КвалификаторыСтроки(240);
	ОписаниеТипаСтроки240 = Новый ОписаниеТипов("Строка", , КС);
	
	КС = Новый КвалификаторыСтроки(32);
	ОписаниеТипаСтроки32 = Новый ОписаниеТипов("Строка", , КС);
	
	ТаблицаИндексов = Новый ТаблицаЗначений();
	
	ТаблицаИндексов.Колонки.Добавить("UUID", ОписаниеТипаСтроки240);
	ТаблицаИндексов.Колонки.Добавить("ХешПолногоИмени", ОписаниеТипаСтроки32);
	ТаблицаИндексов.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Число"));
	ТаблицаИндексов.Колонки.Добавить("ПолноеИмя", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаИндексов;
	
КонецФункции

// Сохраняет табилцу индексов
// 
// Параметры:
// 	Таблица - См. СозадатьТаблицуИндексов
// 	ИмяФайлаИндексов - Строка - имя файла индексов
Процедура ЗаписатьТаблицуИндексов(Таблица, ИмяФайлаИндексов)
	
	Файл = Новый ЗаписьТекста();
	Файл.Открыть(ИмяФайлаИндексов, КодировкаТекста.UTF8);

	Таблица.Сортировать("Уровень, ПолноеИмя");
	
	Для Каждого СтрокаТЧ Из Таблица Цикл
		
		МассивСтрок = Новый Массив();
		МассивСтрок.Добавить(СтрокаТЧ.UUID);
		МассивСтрок.Добавить(СтрокаТЧ.ХешПолногоИмени);
		МассивСтрок.Добавить(Строка(СтрокаТЧ.Уровень));
		МассивСтрок.Добавить(СтрокаТЧ.ПолноеИмя);

		СтрокаИндекса = СтрСоединить(МассивСтрок, ":");
		Файл.ЗаписатьСтроку(СтрокаИндекса, Символы.ПС);
		
	КонецЦикла;
	
	Файл.Закрыть();
	
КонецПроцедуры

Процедура СоздатьКаталогиВИерерахии(Знач ПутьККаталогу)
	
	ЭтоWindowsСервер = ОбщегоНазначенияПовтИсп.ЭтоWindowsСервер();
	
	МассивКаталогов = СтрРазделить(ПутьККаталогу, ПолучитьРазделительПути(), Ложь);
	ПутьККаталогу = Лев(ПутьККаталогу, Найти(ПутьККаталогу, ПолучитьРазделительПути()));
	Если ЭтоWindowsСервер 
		И МассивКаталогов.Количество() > 1 
		И СтрНайти(МассивКаталогов[0], ":") > 0 
		ИЛИ СтрДлина(ПутьККаталогу) > СтрДлина(ПолучитьРазделительПути()) Тогда
		МассивКаталогов.Удалить(0);
	КонецЕсли;
	
	Для Каждого ИмяКаталога Из МассивКаталогов Цикл
		ПутьККаталогу = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПутьККаталогу + ИмяКаталога);
		Файл = Новый Файл(ПутьККаталогу);
		Если Не Файл.Существует() Тогда
			СоздатьКаталог(ПутьККаталогу);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
