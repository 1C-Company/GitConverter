///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2017-2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

// Обновление базы из хранилища на указанную версию, подключение к Хранилищу осуществляется на указанной 
// вресии Платформы для хранилища.
// 
// Параметры:
// 	Параметры - Структура - Параметры обновления
//
&Вместо("ОбновитьИнформационнуюБазуИзХранилища")
Процедура ВерсииПлатформыХранилища_ОбновитьИнформационнуюБазуИзХранилища(Параметры) Экспорт
	
	ВерсииПлатформыХранилища = "";
	Если Параметры.Свойство("КопияХранилища") Тогда
		ВерсииПлатформыХранилища = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.КопияХранилища, "ВерсииПлатформыХранилища");
	ИначеЕсли Параметры.Свойство("Хранилище") Тогда
		ВерсииПлатформыХранилища = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Хранилище, "ВерсииПлатформыХранилища");
	КонецЕсли;
	
	ВерсияПлатформы = Параметры.ВерсияПлатформы;
	КаталогИсполняемогоФайла = Параметры.КаталогИсполняемогоФайла;
	
	Если ЗначениеЗаполнено(ВерсииПлатформыХранилища) И ВерсияПлатформы <> ВерсииПлатформыХранилища Тогда
		Параметры.ВерсияПлатформы = ВерсииПлатформыХранилища;
		ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);
	КонецЕсли;
	
	ПродолжитьВызов(Параметры);
	
	Параметры.ВерсияПлатформы = ВерсияПлатформы;
	Параметры.КаталогИсполняемогоФайла = КаталогИсполняемогоФайла;
	
КонецПроцедуры

// Формируется ответ по версиям хранилища начиная с указанной версии, подключение к Хранилищу осуществляется 
// на указанной вресии Платформы для хранилища.
// 
// Параметры:
// 	Параметры - Структура - параметры формирования отчета
&Вместо("СформироватьОтчетПоВерсиямХранилища")
Процедура ВерсииПлатформыХранилища_СформироватьОтчетПоВерсиямХранилища(Параметры) Экспорт
	
	ВерсииПлатформыХранилища = "";
	Если Параметры.Свойство("Хранилище") Тогда
		ВерсииПлатформыХранилища = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Хранилище, "ВерсииПлатформыХранилища");
	КонецЕсли;
	
	ВерсияПлатформы = Параметры.ВерсияПлатформы;
	КаталогИсполняемогоФайла = Параметры.КаталогИсполняемогоФайла;
	
	Если ЗначениеЗаполнено(ВерсииПлатформыХранилища) И ВерсияПлатформы <> ВерсииПлатформыХранилища Тогда
		Параметры.ВерсияПлатформы = ВерсииПлатформыХранилища;
		ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);
	КонецЕсли;
	
	ПродолжитьВызов(Параметры);
	
	Параметры.ВерсияПлатформы = ВерсияПлатформы;
	Параметры.КаталогИсполняемогоФайла = КаталогИсполняемогоФайла;
	
КонецПроцедуры

