// Copyright (c) 2017-2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции для включения оптимизации на Платформе 8.3.15
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

&Вместо("СохранитьФайлИнформацииВыгрузки")
Процедура ОптимизацияВыгрузки_СохранитьФайлИнформацииВыгрузки(Параметры) Экспорт
	
	Если Параметры.Это8315 И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.15.0") > 0 Тогда
		ПродолжитьВызов(Параметры);
	Иначе
		
		ВерсияПлатформы8315 = ОптимизацияВыгрузки_ПолучитьВерсиюПлатформы();
		Параметры.Вставить("Это8315",  ЗначениеЗаполнено(ВерсияПлатформы8315) 
			И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПлатформы8315, "8.3.15.0") > 1);
		
		Если Параметры.Это8315 Тогда 
			ДобавитьЗаписьВЛог(Параметры, НСтр("ru='Включенна оптимизация выгрузки на платформе 8.3.15'"));
		
			Параметры.Вставить("СохранитьФайлИнформацииВыгрузки", Истина);

			ВерсияПлатформы = Параметры.ВерсияПлатформы;
			Параметры.ВерсияПлатформы = ВерсияПлатформы8315;
			ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);

			ПродолжитьВызов(Параметры);

			Параметры.ВерсияПлатформы = ВерсияПлатформы;
			ПараметрыКаталогаИсполняемогоФайлаНаСервере(Параметры);
		
			ЗаменитьВерсиюФормата(Параметры);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&Вместо("ПолучитьПараметрыОбработкиВерсии")
Функция ОптимизацияВыгрузки_ПолучитьПараметрыОбработкиВерсии(Знач ВерсияХранилища) Экспорт
	
	Параметры = ПродолжитьВызов(ВерсияХранилища);
	
	Если НЕ Параметры.Это8315 Тогда
		ВерсияПлатформы = ОптимизацияВыгрузки_ПолучитьВерсиюПлатформы();
		
		Параметры.Вставить("Это8315", ЗначениеЗаполнено(ВерсияПлатформы) 
			И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПлатформы, "8.3.15.0") > 1);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции


Функция ОптимизацияВыгрузки_ПолучитьВерсиюПлатформы() Экспорт
	
	Для каждого ВерсияПлатформы из ОбщегоНазначенияПовтИсп.СписокДоступныхВерсийПлатформы() Цикл
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПлатформы, "8.3.15.0") > 0
			И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияПлатформы, "8.3.16.0") < 0 Тогда
			Возврат ВерсияПлатформы;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заменить версию формата в ConfigDumpInfo.xml
Процедура ЗаменитьВерсиюФормата(Параметры)
	
	Файл = Новый Файл(Параметры.КаталогФайловКонфигурации + "ConfigDumpInfo.xml");
	Если Файл.Существует() Тогда
		ВерсияФормата = "2.9"; // Для 8.3.15
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.14.0") > 1 Тогда
			ВерсияФормата = "2.8";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.13.0") > 1 Тогда
			ВерсияФормата = "2.7";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.12.0") > 1 Тогда
			ВерсияФормата = "2.6";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.11.0") > 1 Тогда
			ВерсияФормата = "2.5";
		ИначеЕсли ОбщегоНазначенияКлиентСервер.СравнитьВерсии(Параметры.ВерсияПлатформы, "8.3.10.0") > 1 Тогда
			ВерсияФормата = "2.4";
		КонецЕсли;
		
		Текст = Новый ТекстовыйДокумент();
		Текст.Прочитать(Файл.ПолноеИмя);
		Для Индекс = 1 По Мин(4, Текст.КоличествоСтрок()) Цикл
			СтрокаСВерсией = Текст.ПолучитьСтроку(Индекс);
			// Ищем строку формата <...  version="2.9"> и проверяем вторую кавычку
			Позиция = СтрНайти(СтрокаСВерсией, " version=");
			Если Позиция > 0 И СтрНачинаетсяС(СтрокаСВерсией, "<ConfigDumpInfo ") И Сред(СтрокаСВерсией, Позиция + 13, 1) = """" Тогда

				СтрокаСВерсией = Лев(СтрокаСВерсией, Позиция + 9) + ВерсияФормата
					+ Сред(СтрокаСВерсией, Позиция + 13, СтрДлина(СтрокаСВерсией) - Позиция - 12);
				Текст.ЗаменитьСтроку(Индекс, СтрокаСВерсией);
				Текст.Записать(Файл.ПолноеИмя);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
